@EVENTLOAD
CALL RESET_GRAPH_200

;-----------------------------------------------------------
;境界の民の村エリア グラフ化
;-----------------------------------------------------------
@RESET_GRAPH_200
#DIM 場所番号, 2
#DIM 始点ノード
#DIM 終点ノード
#DIM DISTANCE_MAP200, MAX_NODE, MAX_NODE

;各変数の初期化
NODE_COUNT_MAP200 = 0
VARSET NODE_MAP200, -1
VARSET NODE_REVERSE_MAP200, -1
VARSET DISTANCE_MAP200, WEIGHT_DISCONNECTED
VARSET NEXT_MAP200, -1

;ノードの定義
FOR 場所番号:0, 0, マップ部屋番号上限
	SIF 存在しない移動先_200(場所番号:0)
		CONTINUE
	NODE_MAP200:NODE_COUNT_MAP200 = 場所番号:0
	NODE_REVERSE_MAP200:(場所番号:0) = NODE_COUNT_MAP200
	NODE_COUNT_MAP200 ++
NEXT

;ノードの距離の初期設定
;隣接しているもののみ
FOR 始点ノード, 0, NODE_COUNT_MAP200
	場所番号:0 = NODE_MAP200:始点ノード
	FOR 終点ノード, 0, NODE_COUNT_MAP200
		SIF 始点ノード == 終点ノード
			CONTINUE
		場所番号:1 = NODE_MAP200:終点ノード
		IF CAN_MOVE_200(場所番号:0, 場所番号:1) & 1
			DISTANCE_MAP200:始点ノード:終点ノード = EDGE_WEIGHT_200(場所番号:0, 場所番号:1)
			NEXT_MAP200:始点ノード:終点ノード = 終点ノード
		ENDIF
	NEXT
NEXT

CALL FLOYD_WARSHALL(NODE_COUNT_MAP200, DISTANCE_MAP200, NEXT_MAP200)

@EDGE_WEIGHT_200(場所番号:0, 場所番号:1)
#FUNCTION
#DIM 場所番号, 2

RETURNF 1
