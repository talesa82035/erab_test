;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――お祭り開催時に選べるお祭り関連処理
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●幻想読書会
;--------------------------------------------------
;読書会は、「指定された本を読んできて、感想を言い合う」会である。
;パッチェさんは実益も兼ねて、持ち込んだ本と交換で、お空の本を読もうとしている
;活字中毒者にとって、まだ読んだことのない活字は毒のたぐい。
;この辺、本に強くかかわるキャラ（ラジエルとか？）でも開催できるように、汎用的なテキストにしたほうがええかなー……とも思う
;--------------------------------------------------
;昼過ぎごろから図書室に集まって読書、夕方ごろに終了
;読書中は図書館にいるので、知識を教わるのが狙いやすい……だろうか。
;/
;図書室の増築が必要で、特定のキャラが必要で、効果が大きくなく、対象の範囲も狭く、春スタートのゲームで秋に発生する……と色々弱い。
;その代わりに、元手がほとんどかからないので、開催費用が安い（秋にルピが足りないということも少ないだろうが……）。
;まあキャラ固有だしこんなもんだろ、という調整。
;--------------------------------------------------

@MOVE_ROUTINE_FESTIVAL_幻想読書会(対象キャラ, ARGS)
#DIM 対象キャラ
#DIM 時間計算
#DIM CONST 開催費用 = 3000
#DIM キャラ番号パチュリー
#DIM キャラ番号小悪魔
#DIM キャラ番号咲夜
#DIM 参加判定値

	;判定にかかわる３名を検索しておく
	キャラ番号パチュリー = キャラ検索("[動かない大図書館]パチュリー・ノーレッジ")
	キャラ番号小悪魔 = キャラ検索("[名もなき小さな悪魔]小悪魔")
	キャラ番号咲夜 = キャラ検索("[完全で瀟洒な従者]十六夜咲夜")

	SELECTCASE ARGS
		CASE "お祭り説明"
			LOCALS = <div rect='2125,500,3762,2812'>
			LOCALS += "■幻想読書会<br>"
			LOCALS += "<br>"
			LOCALS += "%CALLNAME:キャラ番号パチュリー%が企画した、様々な本を読みあうイベントです。<br>"
			LOCALS += "リゾートの図書室の本のほか、幻想郷などの異世界から持ち込まれた本も持ち込まれるようです。<br>"
			LOCALS += "様々な知識人が図書館に集まるほか、読書を通じて価値観を語り合い、全員の信頼が上がりやすくなるでしょう。<br>"
			LOCALS += "<br>"
			LOCALS += "好感度要素：信頼の上昇率が1.2倍になります。<br>"
			LOCALS += "<br>"
			IF 開催費用 > MONEY
				LOCALS += @"ルピが不足しています。（必要ルピ：%NUM_FORMAT(開催費用)%）<br>"
			ELSE
				LOCALS += @"<button value='0'>[0] このお祭りを企画する（必要ルピ：%NUM_FORMAT(開催費用)%）</button><br>"
			ENDIF
			LOCALS += "</div>"
			HTML_PRINT LOCALS, 1
		CASE "出現条件"

			;図書室で実行するので、図書室が増築されていないとダメ
			SIF 施設改造度:5:6 < 1
				RETURN 0

			;季節限定の構文
			;『読書の秋』ということで、秋限定。
			;/
			;季節計算は下記の通りに行えばよい。
			;　「経過日数を120で割った余り」＝「X年目の、Y日目」を出す
			;　Y日目を30で割り、春夏秋冬を出す　※eraの変数は少数が入らないので、0.1.2.3のいずれかが結果になる
			;/
			;結果が２以外＝秋以外ならダメ
			SIF DAY % 120 / 30 != 2
				RETURN 0

			;パチュリー滞在
			SIF CFLAG:キャラ番号パチュリー:滞在期間 < 1
				RETURN 0
			;パチュリーが、従業員、常連、定住者、店持ち、その他滞在期間999……のいずれでもない場合、ダメ
			;（この場合、オーナーへの好感度はあまり関係ないかなー……リゾートそのものへの好感度が必要になってくるだろう）
			SIF !(TALENT:キャラ番号パチュリー:従業員 || TALENT:キャラ番号パチュリー:常連 || TALENT:キャラ番号パチュリー:定住者 || TALENT:キャラ番号パチュリー:店持ち || CFLAG:キャラ番号パチュリー:滞在期間 == 999)
				RETURN 0

			;小悪魔も咲夜もいないならダメ
			;パチュリーが企画し、小悪魔または咲夜が動くので、パチュリー＋どっちかがいないと出現しない
			SIF CFLAG:キャラ番号小悪魔:滞在期間 < 1 && CFLAG:キャラ番号咲夜:滞在期間 < 1
				RETURN 0
			;咲夜の場合は（小悪魔と違ってパチュリー専任ではないので）３の倍数の日しか発生しない
			;小悪魔がいない場合、３の倍数の日でなければダメ
			SIF CFLAG:キャラ番号小悪魔:滞在期間 < 1 && DAY % 3 != 0
				RETURN 0

			;ここまで来たら出現
			RETURN 1
		CASE "参加判定"

			;--------------------------------------------------
			;基礎
			;--------------------------------------------------
			;参加キャラの条件を記載する
			;全員参加なら単純に１を返す
			;基本参加率は100
			;以下、本が好きなタイプは高い確率で、嫌いそうなタイプは低い確率で参加
			;--------------------------------------------------
			参加判定値 = 100

			;--------------------------------------------------
			;100％参加キャラ
			;--------------------------------------------------
			;パッチェさんがいるなら絶対参加
			SIF 対象キャラ == キャラ番号パチュリー
				RETURN 1
			;小悪魔がいるなら絶対参加
			SIF 対象キャラ == キャラ番号小悪魔
				RETURN 1
			;咲夜がいるなら絶対参加（お菓子を作ってくれるよ！　紅茶とかも淹れてくれるんだと思う）（あとその辺の草のお茶とか）
			SIF 対象キャラ == キャラ番号咲夜
				RETURN 1
			;仕事が「図書室で司書」なら100％参加
			SIF 現在仕事:対象キャラ:1 == 506
				RETURN 1
			;この人は万難を排して絶対絶対絶対に参加するだろう
			SIF 対象キャラ == キャラ検索("[ザ・ペーパー]読子・リードマン")
				RETURN 1

			;--------------------------------------------------
			;逆に絶対参加しないだろうってキャラ
			;--------------------------------------------------
			;猫は……まあ……マジ半端ねぇと言っても……多分無理だろ……
			SIF 対象キャラ == キャラ検索("[若き自由猫]マジ半端ねぇ猫")
				RETURN 0

			;--------------------------------------------------
			;以下、「参加率が特に高そうな」タイプの人
			;--------------------------------------------------
			;異世界の本に興味があったり、学者先生だったりなんだったり。
			;--------------------------------------------------
			SIF 対象キャラ == キャラ検索("[妄想少女X]ルナール")
				参加判定値 += 50
			;パッチェさんの持ち込む魔法の本に興味がある！という具合。
			SIF 対象キャラ == キャラ検索("[ウィッチクラフト]マギサ")
				参加判定値 += 150
			;読むより書く方？　だが当然のように興味はある
			SIF 対象キャラ == キャラ検索("[神秘の天司]ラジエル")
				参加判定値 += 100
			;フェイトエピで「本を買って読む」言及があるとのこと。
			SIF 対象キャラ == キャラ検索("[東北東の守護神]シンダラ・パイ")
				参加判定値 += 50
			SIF 対象キャラ == キャラ検索("[北北東の守護神]シャトラ")
				参加判定値 += 50
			SIF 対象キャラ == キャラ検索("[踊り狂う暴風]グリームニル")
				参加判定値 += 50
			;他、アルシャ、アルタイルもそうらしいが、作成時点で未実装


			;アルは本の化身的な存在。他の本が気になるかもしれない。
			SIF 対象キャラ == キャラ検索("[ミズ・ネクロノミコン]アル・アジフ")
				参加判定値 += 50
			SIF 対象キャラ == キャラ検索("[悪魔の子]ニコ・ロビン")
				参加判定値 += 100
			SIF 対象キャラ == キャラ検索("[ポケモン博士]オーキド")
				参加判定値 += 100
			SIF 対象キャラ == キャラ検索("[スメールの草神]ナヒーダ")
				参加判定値 += 50
			;魔理沙も参加しそうだな～（参加側には誘わないだろう）（あとパッチェさんが邪険にするので参加率補正は低め）
			SIF 対象キャラ == キャラ検索("[普通の魔法使い]霧雨魔理沙")
				参加判定値 += 50
			;アリスは友人補正もあって特に高い参加率
			SIF 対象キャラ == キャラ検索("[七色の人形遣い]アリス・マーガトロイド")
				参加判定値 += 150
			;魔導書集めが趣味
			SIF 対象キャラ == キャラ検索("[葬送]フリーレン")
				参加判定値 += 100

			;その他特別に補正したいキャラ
;			SIF 対象キャラ == キャラ検索("XXXX")
;				参加判定値 += 150


			;--------------------------------------------------
			;素質等による補正
			;--------------------------------------------------
			;知識素質を見る
			;インドア系知識レベルが補正にプラス
			参加判定値 += (知識素質:対象キャラ:調合知識 * 10)
			;魔法知識持ちはパッチェさんの本に興味を持つため補正大きめ
			参加判定値 += (知識素質:対象キャラ:魔法知識 * 15)
			参加判定値 += (知識素質:対象キャラ:機械知識 * 5)
			参加判定値 += (知識素質:対象キャラ:錬金知識 * 10)
			;魔物知識はアウトドア一辺倒とも言い難い
			;３以上なら座学も必要と考えている……はず
			参加判定値 += (知識素質:対象キャラ:魔物知識 >= 3) * 15
			;高貴は補正率低めながらプラス
			参加判定値 += (知識素質:対象キャラ:高貴 * 5)
			参加判定値 += (知識素質:対象キャラ:歴史知識 * 10)
			参加判定値 += (知識素質:対象キャラ:経営知識 * 10)

			;TALENTを見る
			;戦闘好きならアクティブなタイプとみて、参加率マイナス
			SIF TALENT:対象キャラ:戦闘好き
				参加判定値 -= 30
			;おしゃべりなら参加率低下
			SIF TALENT:対象キャラ:会話資質 == 1
				参加判定値 -= 20
			;口下手、会話難ならちょっと上昇
			SIF TALENT:対象キャラ:会話資質 < 0
				参加判定値 += 20
			;幼稚、幼児、子供または子供っぽい、実年齢12歳以下なら低下
			;（個人の体験だと、本好きな子供も多いが、やはり集中力は持たなかったり、逆に集中しすぎたりで、読書会はどーだろ感ちょっとある）
			SIF TALENT:対象キャラ:幼児／幼児退行
				参加判定値 -= 20
			SIF TALENT:対象キャラ:幼稚
				参加判定値 -= 20
			SIF TALENT:対象キャラ:精神的余裕 == -1
				参加判定値 -= 20
			SIF TALENT:対象キャラ:年齢層 == 10
				参加判定値 -= 20

			;咲夜がいるとお菓子も出てくるのでちょっとだけ参加率が上昇
			SIF CFLAG:キャラ番号咲夜:滞在期間 >= 1
				参加判定値 += 10
			;対象キャラのプレゼント好みに本がある場合、参加率上昇
			;「読書が趣味」のキャラはここで拾う
			SIF STRFIND(CSTR:対象キャラ:プレゼント好み, "本") >= 0
				参加判定値 += 50

			;RAND100（＋α）が90を超えたら（＝基本は10％程度で）参加
			;リゾートに50人いるとして、運営側＋10人くらいが参加、が基本想定（結構多いねえ）
			;＋0で10/100＝10％
			;＋50で60/150＝40％
			;＋100で110/200＝55％
			;＋150で160/250＝64％
			;＋200で210/300＝70％
			;＋250で260/350＝74％
			;＋300で310/400＝77％
			;一応、マイナスにならないよう補正はする
			参加判定値 = MAX(参加判定値 ,1)
			SIF RAND:(参加判定値) >= 90
				RETURN 1

			;ここまでダメだったら不参加
			RETURN 0
		CASE "特設マップ"
			;存在しない場合はこのまま
			;作りたい場合は処理が激増するのでこのテンプレートにはまとめない
			RETURN 0
		CASE "起床時刻"
			;ルーチンは変動しないので必要なし

			;特別に行動ルーチンを設定しないのなら削除
;			;設定する場合は時間を記載
;			時間計算 = 480 - (TALENT:対象キャラ:早起き * 30)
;			RETURN 時間計算
		CASE "就寝時刻"
			;ルーチンは変動しないので必要なし

;			;特別に行動ルーチンを設定しないのなら削除
;			;設定する場合は時間を記載
;			時間計算 = 1320 - (TALENT:対象キャラ:早寝 * 60)
;			RETURN 時間計算
		CASE "初期位置"
			;ルーチンは変動しないので必要なし

;			;特別に行動ルーチンを設定しないのなら削除
;			;設定する場合は場所を記載
;			CFLAG:対象キャラ:現在位置 = 6
;			CFLAG:対象キャラ:現在マップ種別 = 0
		CASE "お祭り補正条件"
			;下の「お祭り補正内容」を適用するキャラの条件
			;基本的に参加判定と一致

			;キャラ条件はない
			;図書室にいなきゃダメ、というのは補正内容のほうで見る
			RETURN 1

		CASE "お祭り補正内容"
			;ソースの発生時に呼ばれる
			;好感度要素や快楽リソースなどに影響

			;現在マップがゼロ（リゾートマップ）かつ、現在位置が506（図書館）でなければ補正は与えない
			;（図書館に来て本を読んでるならお前も参加者だ！　ってことで、イベント参加有無は問わない）
			SIF CFLAG:対象キャラ:現在マップ種別 == 0 && CFLAG:MASTER:現在位置 == 506
				NOWEX:対象キャラ:信頼度上昇格納 = MAX(NOWEX:対象キャラ:信頼度上昇格納 * 12 / 10, 0)
		CASE "服装変更"
			;参加したキャラの服装を変える場合に記載

			;当然だが普段着での参加なのでコメントアウト
;			RSTR:服装名受け渡し = ;＊＊＊＊＊＊＊＊ここを編集＊＊＊＊＊＊＊＊
	ENDSELECT


@ROUTINE_SCHEDULE_FESTIVAL_幻想読書会(対象キャラ)
#DIM 対象キャラ
#DIM スケジュール番号
#DIMS ルーチン保存

	;まず変な値が入らないようにリセット
	CALL RESET_SCHEDULE(対象キャラ)
	スケジュール番号 = 0

	;一旦お祭りルーチンを保存してからAM行動用に汎用を呼ぶ
	ルーチン保存 = %移動ルーチン:対象キャラ:ルーチンID%
	CALL 汎用移動ルーチン決定(対象キャラ, GETNUM(移動ルーチン, "ルーチンID"))
	CALLFORM ROUTINE_SCHEDULE_AM_%移動ルーチン:対象キャラ:ルーチンID%(対象キャラ)
	移動ルーチン:対象キャラ:ルーチンID = %ルーチン保存%
	;ここまでの処理で起床～13時までの行動が入ってる

	;時間ごとにどの場所にいるべきかを決定
	;既存のルーチンプランを参考に決めると良い
	;ERB/リゾートマップ/キャラ移動ルーチン/汎用移動ルーチン.ERB

	;TIME/60で、今何時かを換算できる
	;1300～1730まで読書会なので、TIME780から、TIME780+270＝1050までは図書館にいる感じ。

	;13時から図書館へ
	CALL SET_SCHEDULE(対象キャラ, スケジュール番号, 780 + TCVAR:対象キャラ:行動時間ゆらぎ, "幻想読書会参加", "所要時間", "270")
	スケジュール番号++

	;17時半から夕食
	CALL SET_SCHEDULE(対象キャラ, スケジュール番号, 1050 + TCVAR:対象キャラ:行動時間ゆらぎ, "食事", "所要時間", "60", "優先度", "4")
	スケジュール番号++

	;19時半からお風呂
	CALL SET_SCHEDULE(対象キャラ, スケジュール番号, 1170 + TCVAR:対象キャラ:行動時間ゆらぎ, "お風呂", "所要時間", "60", "優先度", "2")
	スケジュール番号++

	IF TALENT:対象キャラ:早寝 > 0
		;早寝のキャラは21時の15分前(着替え時間)から寝るまでは自室
		CALL SET_SCHEDULE(対象キャラ, スケジュール番号, 1245 + TCVAR:対象キャラ:行動時間ゆらぎ, "休憩")
		スケジュール番号++
	ELSE
		;21時からロビーへ
		CALL SET_SCHEDULE(対象キャラ, スケジュール番号, 1260 + TCVAR:対象キャラ:行動時間ゆらぎ, "交流")
		スケジュール番号++

		;21時30分から寝るまでは自室
		CALL SET_SCHEDULE(対象キャラ, スケジュール番号, 1290 + TCVAR:対象キャラ:行動時間ゆらぎ, "休憩")
		スケジュール番号++
	ENDIF

@SCHEDULE_幻想読書会参加(対象キャラ, 小分類, 処理内容)
#DIM 対象キャラ
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:対象キャラ:目的マップ = 0
		スケジュール状態:対象キャラ:目的地 = 506
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT
