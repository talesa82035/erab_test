
;----------------------------------------------------
;キャラ固有コマンド処理
;----------------------------------------------------
;コマンド名
;----------------------------------------------------
@CHARACOMNAME370_1168
#FUNCTION
LOCAL = GETCHARA(1168)
IF CSTR:LOCAL:スタイル管理文字列 == "[護法少女]酒呑童子（キャスター）"
	TSTR:コマンド名受渡 = お仕置き完了
ELSEIF CSTR:LOCAL:スタイル管理文字列 == ""
	TSTR:コマンド名受渡 = 護法少女推参
ENDIF
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_370_1168
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_370_1168
LOCALS = <br>■護法少女推参<br>
LOCALS += "人間で言うところの検非違使。護法少女に酒呑童子を変身させる。<br>変身している間[お仕置き完了]に変化する。<br>名前、グラフィックが変化する。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

;-------------------------------------------------
;実行処理
;-------------------------------------------------
@CHARACOM370_1168
LOCAL = GETCHARA(1168)
IF CSTR:LOCAL:スタイル管理文字列 == "[護法少女]酒呑童子（キャスター）"
	CSTR:LOCAL:スタイル管理文字列 = 
	CALL スタイル変更_1168(LOCAL, "変更後処理")
ELSEIF CSTR:LOCAL:スタイル管理文字列 == ""
	CSTR:LOCAL:スタイル管理文字列 = [護法少女]酒呑童子（キャスター）
	CALL スタイル変更_1168(LOCAL, "変更後処理")
ENDIF

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370_1168
;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
LOCAL = GETCHARA(1168)
SIF TARGET != LOCAL
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM370_1168
LOCAL = GETCHARA(1168)
IF CSTR:LOCAL:スタイル管理文字列 == "[護法少女]酒呑童子（キャスター）"
	PRINTFORML 「%TEXTR("こっちのうちがええん？　あんじょうよろしゅう/なんて言いはったかな？　そう、護法。護法少女や/こっちの姿もたまには悪ないなあ？")%」
	PRINTFORML 
	PRINTFORMW %CALLNAME:TARGET%の服が光と共に切り替わった。
ELSEIF CSTR:LOCAL:スタイル管理文字列 == ""
	PRINTFORML 「%TEXTR("元のうちがええんやね？　はいはい/このうちには飽きてしもた？　……なんて、じょうだんじょうだん/あっちのべべも悪ないけど、いつも通りは落ち着くわあ")%」
	PRINTFORML 
	PRINTFORMW %CALLNAME:TARGET%の服が光と共に切り替わった。
ENDIF



@スタイル変更_1168(対象キャラ, 動作モード)
#DIM 対象キャラ
#DIMS 動作モード
;この関数はいくつ別スタイル作ってもキャラ毎に一個

SELECTCASE 動作モード
	CASE "スタイル名取得"
		VARSET RESULTS
		;RESULTS:Xに肩書を、RESULTS:X+10にリミテッドや水着など通称(？)を入れる
		RESULTS:0 = %NAME:対象キャラ%
		RESULTS:10 = 通常

		RESULTS:1 = [護法少女]酒呑童子（キャスター）
		RESULTS:11 = 護法少女

	CASE @"スタイル解説_%NAME:対象キャラ%"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += @"■%NAME:対象キャラ%<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "大江山の鬼。鬼の棟梁である酒呑童子<br>"
		TSTR:スタイル解説 += "あなたが彼女と親しくなれたと思っても油断は禁物<br>"
		TSTR:スタイル解説 += "彼女は「愛しいもの」「大切なもの」を突然壊し貪るかもしれない。<br>"
		TSTR:スタイル解説 += "「鬼」とはそういうものなのだから<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, NAME:対象キャラ)
	CASE "スタイル解説_[護法少女]酒呑童子（キャスター）"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += "■[護法少女]酒呑童子（キャスター）<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "護法の鬼。護法少女としての酒呑童子<br>"
		TSTR:スタイル解説 += "茨木童子の夢見る護法少女のイメージを崩さないよう奮闘している<br>"
		TSTR:スタイル解説 += "お腹をまるっきり出しているが、元の姿より露出度は低い<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[護法少女]酒呑童子（キャスター）")
	CASE "変更後処理"
		;CSTR:対象キャラ:スタイル管理文字列をキーにして処理を実行
		;一人称や二人称、素質の変化などを想定
		SELECTCASE CSTR:対象キャラ:スタイル管理文字列
			CASE ""
				CALLNAME:対象キャラ = 酒呑童子（通常）
				CSTR:対象キャラ:フォルダ判定キー '= CALLNAME:対象キャラ
				SWAP CSTR:対象キャラ:画像フォルダ, CSTR:対象キャラ:スタイル変更前画像フォルダ
				SIF !HAS_RESOURCES(対象キャラ, 0)
					CSTR:対象キャラ:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(対象キャラ)
			CASE "[護法少女]酒呑童子（キャスター）"

				CALLNAME:対象キャラ = 酒呑童子（護法少女）
				CSTR:対象キャラ:フォルダ判定キー '= CALLNAME:対象キャラ
				SWAP CSTR:対象キャラ:画像フォルダ, CSTR:対象キャラ:スタイル変更前画像フォルダ
				SIF !HAS_RESOURCES(対象キャラ, 0)
					CSTR:対象キャラ:画像フォルダ '= GET_RESOURCES_BASE_FOLDER(対象キャラ)
			;CASE RESULTS:2
			;CASE RESULTS:3
		ENDSELECT
ENDSELECT
