@固有奥義_1627(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 効果数値, 2
#DIM チルノ隊列
#DIM 大妖精隊列

チルノ隊列 = キャラ隊列検索(キャラ番号)
大妖精隊列 = キャラ隊列検索(キャラ検索("[霧の中で見つかる妖精]大妖精"))

;チルノと大妖精が共にメインメンバーにいて、チルノと隣り合う位置にいると違う奥義になる
IF INRANGE(チルノ隊列, 0, 3) && INRANGE(大妖精隊列, 0, 3) && ABS(チルノ隊列 - 大妖精隊列) == 1
	;効果が強くなっても名前は同じ
	奥義名 = フリーズアクトレス
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 74 && 陥落チェック(キャラ番号)
		奥義威力 = 220
		効果数値:0 = 7
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
		奥義威力 = 200
		効果数値:0 = 5
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 26
		奥義威力 = 180
		効果数値:0 = 3
	ELSE
		奥義威力 = 140
		効果数値:0 = 3
	ENDIF

	IF 装備ステータス補正保存:キャラ番号:属性 == 11
		;装備属性が水なら、奥義威力が倍になる
		奥義威力 *= 2
	ELSEIF 装備ステータス補正保存:キャラ番号:属性 != 0
		;それ以外の属性装備をしているなら、フィールド効果ターンが4/5になる
		効果数値:0 = 効果数値:0 * 4 / 5
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 凍符「フリーズアクトレス」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 '= @"奥義威力：{奥義威力}％<br>敵全体に水属性のダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>フィールドが（{効果数値:0}ターン）の間、白色の霧で覆いつくされる。"
			詳細文文字列受け渡し変数 += @"<br>＞フィールド：「氷精の白霧」"
			詳細文文字列受け渡し変数 += @"<br>＞敵味方全体の火属性攻撃を[20％]弱体化させる。"
			詳細文文字列受け渡し変数 += @"<br>＞敵味方全体の命中率を[20％]低下させる。"
			RETURN 0
		CASE "奥義前効果"
		CASE "奥義追加効果"
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義威力
			CALL アビテンプレート_ダメージ処理_水属性

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:フィールド枠 = 氷精の白霧
			アビ汎用変数:持続ターン = 効果数値:0
			CALL アビテンプレート_フィールド効果セット
	ENDSELECT
ELSE
	;効果が強くなっても名前は同じ
	奥義名 = パーフェクトフリーズ
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 74 && 陥落チェック(キャラ番号)
		奥義威力 = 200
		効果数値:0 = 2
		効果数値:1 = 270
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
		奥義威力 = 160
		効果数値:0 = 2
		効果数値:1 = 150
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 26
		奥義威力 = 140
		効果数値:0 = 1
		効果数値:1 = 150
	ELSE
		奥義威力 = 100
		効果数値:0 = 1
		効果数値:1 = 150
	ENDIF

	IF 装備ステータス補正保存:キャラ番号:属性 == 11
		;装備属性が水なら、奥義威力が倍になる
		奥義威力 *= 2
	ELSEIF 装備ステータス補正保存:キャラ番号:属性 != 0
		;それ以外の属性装備をしているなら、麻痺成功率が1/3になる
		効果数値:1 /= 3
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 凍符「パーフェクトフリーズ」！

	SELECTCASE ARGS
		CASE "奥義名"
			TSTR:コマンド名受渡 = %奥義名%
		CASE "奥義説明文"
			詳細文文字列受け渡し変数 '= @"奥義威力：{奥義威力}％<br>敵全体に水属性のダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>対象に{効果数値:0}回の麻痺［100％］効果を与える。基礎成功率{MIN(効果数値:1, 100)}％。"
			RETURN 0
		CASE "奥義前効果"
		CASE "奥義追加効果"
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 奥義威力
			CALL アビテンプレート_ダメージ処理_水属性

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 麻痺
			アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:持続回数 = 効果数値:0
			アビ汎用変数:基礎成功確率 = 効果数値:1
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ENDIF


@固有バフ・デバフ処理_フィールド_氷精の白霧_火強化(隊列番号, 補正前数値)
#DIM 隊列番号
#DIM 補正前数値
;火ダメ-20%
RETURN 補正前数値 - 20

@固有バフ・デバフ処理_フィールド_氷精の白霧_命中(隊列番号, 補正前数値)
#DIM 隊列番号
#DIM 補正前数値
;命中率-20%（大妖精は無効化）
IF 隊列番号 < 10 && キャラ一致チェック(BATTLE_STATE:隊列番号:キャラ登録番号, "[霧の中で見つかる妖精]大妖精")
	RETURN 補正前数値
ELSE
	RETURN 補正前数値 - 20
ENDIF

@固有バフ・デバフ処理_フィールド_氷精の白霧_回避(隊列番号, 補正前数値)
#DIM 隊列番号
#DIM 補正前数値
;回避率+50%（大妖精のみ）
IF 隊列番号 < 10 && キャラ一致チェック(BATTLE_STATE:隊列番号:キャラ登録番号, "[霧の中で見つかる妖精]大妖精")
	RETURN 補正前数値 + 50
ELSE
	RETURN 補正前数値
ENDIF

@フィールド説明文_氷精の白霧
#DIM 配列番号

LOCALS = チルノが周囲の水分を凍らせたことで発生した白色の霧。<br>
LOCALS += "敵味方全体の火属性攻撃を[20％]弱体化させる。<br>"
LOCALS += "敵味方全体の命中率を[20％]低下させる。<br>"

配列番号 = バフ・デバフ効果検索("フィールド", "枠", "氷精の白霧", -1)
LOCALS += @"効果時間：{DT_CELL_GET("戦闘効果データベース", 配列番号, "持続ターン")}ターン<br>"

詳細文文字列受け渡し変数 = %LOCALS%
