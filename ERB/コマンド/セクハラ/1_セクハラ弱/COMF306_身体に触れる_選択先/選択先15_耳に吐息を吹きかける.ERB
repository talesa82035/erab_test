
@COMNAME_S306_15
#FUNCTIONS
TSTR:コマンド名受渡 = 耳に吐息を吹きかける

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_306_15
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")
弱点コマンドカテゴリ = S
睡眠中許可 = 1


@COM_TOOLTIP_S306_15
LOCALS = <br>■耳に吐息を吹きかける<br>
LOCALS += "相手の耳に吐息を吹きかけるコマンド。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：接触・露出・快Ｓ・歓楽(一定条件時)・不快(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S306_15

;発生する快楽は受け手の状態に依存しない
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 40)
SIF TALENT:TARGET:種族 == 種族_エルーン
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 60)
TIME += 0

SIF CFLAG:TARGET:睡眠
	RETURN 1

CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ  3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応  3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 2
		;無反応
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れ時は反応なし
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 3
				;喜び時はちょっと喜ぶ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
		ENDSELECT
	CASE 3 TO 4
		;嫌悪と拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 200)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 20)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 20)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
		ENDSELECT
	CASEELSE
		;上記以外は同じソースを発生
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S306_15
;選択から飛んでくるのでABLEは常に0
RETURN 0

@身体に触れる_ABLE_耳に吐息を吹きかける
;一括管理
SIF GLOBAL_COMABLE_S("306_15") && !RFLAG:罰ゲーム中
	RETURN RESULT
;関数実行のため、派生番号を返す
RETURN 15

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S306_15

PRINTFORM %CALLNAME:PLAYER%は背後から%CALLNAME:TARGET%の
IF TALENT:TARGET:エルーン耳
	PRINT もふもふの
ELSEIF TALENT:TARGET:ハーヴィン耳
	PRINT 尖った
ENDIF
PRINTL 耳に優しく吐息を吹きかけた…
IF CUP:TARGET:快Ｓ > 1000
	;一定以上の快楽が発生した時にちょっと声が出る
	IF CFLAG:TARGET:睡眠
		PRINTFORML 不意の快楽に、%CALLNAME:TARGET%は眠ったまま小さく喘いだ…
	ELSE
		PRINTFORML 不意の快楽に、%CALLNAME:TARGET%は思わず少し声を漏らしてしまった…
	ENDIF
	PRINTL
ENDIF

@MESSAGE_COM_S306_15_非口上時

IF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:TARGET%はすやすやと眠っている……
	RETURN 0
ENDIF


SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れに違い無し
				PRINTFORML %CALLNAME:TARGET%は何か用だろうか、と首を傾げている
			CASE 3
				;喜び時は喜ぶ
				PRINTFORML %CALLNAME:TARGET%はよくわかっていないが、%CALLNAME:PLAYER%が楽しいなら良いと嬉しそうにしている
		ENDSELECT
	CASE 2
		;無反応
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れ時は反応なし
				PRINTFORML %CALLNAME:TARGET%は何も反応を示さず、平然としている
			CASE 3
				;喜び時はちょっと喜ぶ
				PRINTFORML %CALLNAME:TARGET%は平然としているが、少し顔を赤くして%CALLNAME:PLAYER%に体を擦り寄せてきた…
		ENDSELECT
	CASE 3
		;嫌悪
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は嫌そうな顔で離れ、%CALLNAME:PLAYER%から離れた…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を無視し、反応しないように我慢している
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は無視している風だが、少し%CALLNAME:PLAYER%に近づいてきた…
		ENDSELECT
	CASE 4
		;拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は気安くそんなことをするべきではない、と%CALLNAME:PLAYER%を拒んだ
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は不満そうな顔をしているが、%CALLNAME:PLAYER%を拒むことはしないようだ…
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は口だけで不満を漏らしながらも、%CALLNAME:PLAYER%の好きにさせている…
		ENDSELECT
	CASE 5
		;余裕
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を窘め、気安くこういうことはしないようにと諭した
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は困ったように笑いながら、口だけの注意を%CALLNAME:PLAYER%にしている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は笑みを浮かべて余裕を見せている…
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は真っ赤になって、%CALLNAME:PLAYER%から少し距離を取ってしまった…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は顔を赤くしながら、%CALLNAME:PLAYER%にされるがままになっている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は赤くなった顔で%CALLNAME:PLAYER%を見つめ、そっと体を寄せてきた…
		ENDSELECT
	CASE 7
		;愉悦
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は面白そうに微笑み、%CALLNAME:PLAYER%のやり方がなっていないとダメ出しをしてきた
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%はそれだけでいいのかと、冗談交じりで%CALLNAME:PLAYER%をからかっている…
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%はそれだけでいいのかと、色っぽい表情で%CALLNAME:PLAYER%を挑発している…
		ENDSELECT
	CASE 8
		;汎用
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は嫌がっている…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は苦笑しながら%CALLNAME:PLAYER%を受け入れている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%はくすぐったがりながらも、%CALLNAME:PLAYER%が構ってくれることを少し嬉しく思っているようだ…
		ENDSELECT
ENDSELECT

