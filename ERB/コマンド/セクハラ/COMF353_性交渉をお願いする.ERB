
@COMNAME353
#FUNCTIONS
TSTR:コマンド名受渡 = 性交渉をお願いする

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_353
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_353
LOCALS = <br>■性交渉をお願いする<br>
LOCALS += "相手に頼みこみ、ベッドに誘い性交渉を行うコマンド。<br>好感度レベルが下がる唯一のコマンド。性交渉まで必要な回数は素質によって増減する。<br>また、性知識がマイナスの場合は必ず成功する。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

;可視化できるよう別関数化
@COM353_SUB(ARG)
#FUNCTION
VARSET LOCAL
SWAP LOCAL:1, TARGET
TARGET = ARG

SIF BASE:性欲 > 1000
	LOCAL += 10
IF TCVAR:トランス残り時間 > 0
	LOCAL += 15
ELSEIF BASE:酩酊 >= 500
	LOCAL += 15
ELSEIF BASE:酩酊 > 0
	LOCAL += 5
ENDIF
SIF TALENT:処女
	LOCAL -= 20
SIF TALENT:度胸 == -1
	LOCAL -= 5
SIF TALENT:マイペース == -1
	LOCAL += 10
SIF TALENT:態度
	LOCAL -= TALENT:態度 * 5
SIF TALENT:応答
	LOCAL -= TALENT:応答 * 5
SIF TALENT:プライド
	LOCAL -= TALENT:プライド * 10
SIF TALENT:ツンデレ
	LOCAL += 5
SIF TALENT:自制心
	LOCAL -= TALENT:自制心 * 5
SIF TALENT:感情乏しい
	LOCAL += 5
SIF TALENT:性的興味
	LOCAL += TALENT:性的興味 * 10
SIF TALENT:一線越えない
	LOCAL -= 15
SIF TALENT:性欲
	LOCAL += TALENT:性欲 * 5
SIF TALENT:貞操
	LOCAL -= TALENT:貞操 * 15
SIF TALENT:抵抗
	LOCAL -= 10
SIF TALENT:羞恥心
	LOCAL -= TALENT:羞恥心 * 5
SIF TALENT:献身的
	LOCAL += 5
SIF TALENT:倒錯的
	LOCAL += 10
IF TALENT:性別嗜好 == 1 && TALENT:PLAYER:性別 == 2
	LOCAL -= 20
ELSEIF TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1
	LOCAL -= 20
ELSEIF TALENT:性別嗜好 == 3
	LOCAL -= 20
ENDIF
IF TALENT:恋人持ち == 1
	LOCAL -= 20
ELSEIF TALENT:恋人持ち == 2
	LOCAL -= 30
ELSEIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:TARGET:人物番号} Or 対象キャラ２ = {CFLAG:TARGET:人物番号}) And 関係性種別 = '恋慕'")
	LOCAL -= 20
ELSEIF TALENT:恋人持ち == 3
	LOCAL -= 10
ENDIF
IF TALENT:子持ち == 1
	LOCAL -= 10
ENDIF
SIF TALENT:淫乱 == 1
	LOCAL += 999
SIF 性癖素質:ゆきずりセックス > 0
	LOCAL += 999


LOCAL += ABL:欲望 * 5
LOCAL += TALENT:従業員 * 10
LOCAL += TALENT:定住者 * 10

SWAP LOCAL:1, TARGET

RETURNF LOCAL

;性交渉をお願いする
@COM353
#DIM 下降基準値
#DIM 今回成功度
#DIM 成功度上限
#DIM 成功度下限

;まず成功度の蓄積量を判定、蓄積100で成功
;想定としては有利素質を盛りに盛って２回目（つまり上限は99）
;不利素質をどれだけ持っていても蓄積可能に（つまり下限は1）、ただ100回交渉するのは現実的じゃないのでZPショップで下限上昇出来るようにする

今回成功度 = CFLAG:好感度レベル / 2
IF CFLAG:TARGET:発情期フラグ < 0
	今回成功度 += 恋慕_性愛比較(TARGET) * 2
ELSE
	今回成功度 += 恋慕_性愛比較(TARGET) / 2
ENDIF
今回成功度 += COM353_SUB(TARGET)
SIF フィールド展開:欲情強化 && 今回成功度 > 0
	今回成功度 = 今回成功度 * 3 / 2
今回成功度 += (ABL:PLAYER:話術技能 - 3) * 5
今回成功度 += COM_353_性交渉回数取得(TARGET) * 30
;遊び人素質による補正 （下限の底上げ）
SELECTCASE あなた特殊能力:遊び人
	CASE IS >= 5
		成功度下限 = 50
	CASE 4
		成功度下限 = 33
	CASE 3
		成功度下限 = 20
	CASE 2
		成功度下限 = 15
	CASE 1
		成功度下限 = 9
	CASEELSE
		成功度下限 = 0
ENDSELECT
;好感度が無二の友未満である場合、大きく成功度と下限が減少
SELECTCASE CFLAG:好感度レベル
	CASE IS <= 関係閾値:2
		;顔見知り以下
		今回成功度 -= 55
		SIF 今回成功度 > 0
			今回成功度 /= 2
		成功度下限 = -1
	CASE IS <= 関係閾値:3
		;友人
		今回成功度 -= 40
		SIF 今回成功度 > 0
			今回成功度 /= 2
		成功度下限 /= 8
	CASE IS <= 関係閾値:4
		;親友
		今回成功度 -= 20
		SIF 今回成功度 > 0
			今回成功度 /= 2
		成功度下限 /= 2
ENDSELECT

;二回目以降は一発OKもアリ
IF BASE:酩酊 >= 500
	成功度上限 = 99 + CFLAG:TARGET:お願いックス回数 + CFLAG:TARGET:泥酔ックス回数
ELSE
	成功度上限 = 99 + CFLAG:TARGET:お願いックス回数
ENDIF

RFLAG:コマンド結果受渡し変数２ = 今回成功度
;今回セフレ取得可能な場合は上限判定しない
SIF !COM_353_セフレ取得判定(TARGET)
	RFLAG:コマンド結果受渡し変数２ = MIN(RFLAG:コマンド結果受渡し変数２, 成功度上限)
RFLAG:コマンド結果受渡し変数２ = MAX(RFLAG:コマンド結果受渡し変数２, 1 + 成功度下限)

CFLAG:TARGET:お願いックス蓄積値 += RFLAG:コマンド結果受渡し変数２
;性知識-2の場合、無二の友以上なら基本OK
IF 知識素質:TARGET:性知識 < -1 && CFLAG:好感度レベル > 関係閾値:4
	CFLAG:TARGET:お願いックス蓄積値 = 101
ENDIF

;セフレ不可の場合、問答無用でゼロにする
IF GETBIT(TALENT:TARGET:陥落不可, 1) && BASE:酩酊 < 500 && TCVAR:トランス残り時間 <= 0
	RFLAG:コマンド結果受渡し変数２ = 0
	CFLAG:TARGET:お願いックス蓄積値 = 0
ENDIF

IF CFLAG:TARGET:お願いックス蓄積値 > 99
	RFLAG:コマンド結果受渡し変数 = 1
	CFLAG:TARGET:お願いックス蓄積値 = 0
	;うふふへ移行
	CFLAG:PLAYER:うふふ = 1
	IF BASE:酩酊 >= 500
		SIF TCVAR:TARGET:性交渉成功フラグ == 0
			CFLAG:TARGET:泥酔ックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>酔った%CALLNAME:TARGET%を手籠めにした</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>酔って%CALLNAME:PLAYER%と一線を越えてしまった</font><img src='えっちハート'>","うふふ")
	ELSEIF TCVAR:トランス残り時間 > 0
		SIF TCVAR:TARGET:性交渉成功フラグ == 0
			CFLAG:TARGET:お願いックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%の判断力を奪って手籠めにした</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>一時の気の迷いで%CALLNAME:PLAYER%と一線を越えてしまった</font><img src='えっちハート'>","うふふ")
	ELSE
		SIF TCVAR:TARGET:性交渉成功フラグ == 0
			CFLAG:TARGET:お願いックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%に頼み込んでえっちをした</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%に頼み込まれて身体を許してしまった</font><img src='えっちハート'>","うふふ")
	ENDIF
	IF 初体験日取得("初うふふ", TARGET, MASTER) == 0
		IF TALENT:TARGET:恋人持ち > 0 && !TALENT:TARGET:浮気
			TALENT:TARGET:浮気 = 1
			RFLAG:コマンド結果受渡し変数３ = 1
			CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"素質[浮気]を得た","うふふ実績")
		ENDIF
		;泥酔時かつ初うふふの場合、翌日釘刺しに来る
		IF BASE:酩酊 >= 500 && 知識素質:TARGET:性知識 > -1
			CSTR:TARGET:特殊起床パターン記録 = 初うふふ泥酔ックス後
		ELSEIF TCVAR:トランス残り時間 > 0 && 知識素質:TARGET:性知識 > -1
			CSTR:TARGET:特殊起床パターン記録 = 初うふふ気の迷いックス後
		ENDIF
	ENDIF
	TCVAR:TARGET:性交渉成功フラグ = 1
	CALL うふふ開始キャラ処理(TARGET, "性交渉お願い")

	IF COM_353_セフレ取得判定(TARGET)
		;セフレ取得処理
		;・セフレ取得済の場合はコマンドが実行できなくなるので二重取得の考慮は不要
		SETBIT CFLAG:1 , 0
		TALENT:TARGET:セフレ = 1
		初体験日:セフレ = DAY
		RFLAG:コマンド結果受渡し変数４ = TALENT:陥落不可
		CLEARBIT TALENT:陥落不可, 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%を[セフレ]陥落させた</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>陥落素質[セフレ]を得た</font><img src='えっちハート'>","うふふ")
	ENDIF
ELSE
	RFLAG:コマンド結果受渡し変数 = 0

	;好感度レベルを下げる。拒否度が高いと更に下がる
	IF CFLAG:TARGET:好感度レベル > 関係閾値:1
		下降基準値 = 20
		SIF RFLAG:コマンド結果受渡し変数２ < 16
			下降基準値 += 10

		;好感度を下降基準値に応じてそれぞれ下降
		;友情信頼は下降基準値通りに低下
		CFLAG:TARGET:友情度 = MIN(CFLAG:TARGET:友情度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - 下降基準値))
		CFLAG:TARGET:信頼度 = MIN(CFLAG:TARGET:信頼度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - 下降基準値))
		;恋心は１．５倍
		CFLAG:TARGET:恋心度 = MIN(CFLAG:TARGET:恋心度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - (下降基準値 * 3 / 2)))
		;性愛は半分
		CFLAG:TARGET:性愛度 = MIN(CFLAG:TARGET:性愛度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - (下降基準値  / 2)))
		;固有好感度は変化させない（好感度レベルに関係ないため）

		;好感度レベルの調整
		FOR LOCAL, 0, 100
			IF 経験値テーブル:LOCAL >= MAX(CFLAG:TARGET:友情度, CFLAG:TARGET:信頼度, CFLAG:TARGET:恋心度, CFLAG:TARGET:性愛度)
				;顔見知り未満にはならない
				CFLAG:TARGET:好感度レベル = MAX(LOCAL, 1 + 関係閾値:1)
				BREAK
			ENDIF
		NEXT
	ELSE
		;他人は強制的に下限値
		CFLAG:TARGET:好感度レベル = 1

		;好感度をレベルに応じた値に減少
		CFLAG:TARGET:友情度 = MIN(CFLAG:TARGET:友情度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
		CFLAG:TARGET:信頼度 = MIN(CFLAG:TARGET:信頼度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
		CFLAG:TARGET:恋心度 = MIN(CFLAG:TARGET:恋心度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
		CFLAG:TARGET:性愛度 = MIN(CFLAG:TARGET:性愛度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
	ENDIF

	;恋慕レベルを下げる。拒否度が高いと更に下がる
	CFLAG:TARGET:恋慕レベル -= 20
	SIF RFLAG:コマンド結果受渡し変数２ < 16
		CFLAG:TARGET:恋慕レベル -= 10
	CFLAG:TARGET:恋慕レベル = MAX(CFLAG:TARGET:恋慕レベル, 0)
	;恋慕度をレベルに応じた値に減少
	CFLAG:TARGET:恋慕度 = MIN(CFLAG:TARGET:恋慕度, 経験値テーブル:(CFLAG:TARGET:恋慕レベル))
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE353
;お願いする実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(353)
	RETURN RESULT
;飲み会時はだめ
SIF TFLAG:調教モード == 3
	RETURN 0
;ハーヴィンオナホ中はだめ
SIF MODE_存在判定_プレイヤー側("ハーヴィンオナホ系", PLAYER)
	RETURN 0
;全裸散歩中はだめ
SIF MODE_存在判定_プレイヤー側("全裸散歩", PLAYER)
	RETURN 0
;二人きりでないとだめ
SIF GET_TARGETNUM() > 1 && フィールド展開:うふふ目撃スルー == 0
	RETURN 0
;友人以上(遊び人素質を持っていれば可能)
SIF !あなた特殊能力:遊び人 && CFLAG:好感度レベル < 1 + 関係閾値:2
	RETURN 0
;既に恋慕・セフレを得ている場合はだめ
SIF 陥落チェック_性的(TARGET)
	RETURN 0
SIF TALENT:TARGET:疑似恋慕
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM353
; RFLAG:コマンド結果受渡し変数    お願い判定結果(1でお願い成功/うふふ移行)
; RFLAG:コマンド結果受渡し変数２  成功度の蓄積量
; RFLAG:コマンド結果受渡し変数３  [浮気]素質の取得有無(1で[浮気]素質取得)
; RFLAG:コマンド結果受渡し変数４  [セフレ]素質取得前の陥落不可状態判定

IF RFLAG:コマンド結果受渡し変数
	PRINTFORM %CALLNAME:TARGET%は
	IF 知識素質:TARGET:性知識 < 0
		PRINT よくわかっていない様子で
	ELSEIF 性癖素質:TARGET:ゆきずりセックス
		PRINT 何でもないことかのように、気軽に
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 16
		PRINT 根負けした様子で
	ENDIF
	PRINTFORML %CALLNAME:PLAYER%の願いに答えてくれた。
	IF 知識素質:TARGET:性知識 < 0
		PRINT 何をするのか理解していないまま、
	ELSEIF BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		PRINT ベッドに寝転がり、
	ELSEIF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		PRINT 湯船から立ち上がり、
	ELSEIF SWIMROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:TARGET:現在位置) && CSTR:TARGET:着せ替え服 == "水着"
		PRINT 水着から水を滴らせたまま、
	ENDIF
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%がどうするかを待っている…
	IF RFLAG:コマンド結果受渡し変数３
		PRINTFORM %CALLNAME:TARGET%は
		SETCOLOR カラーパレット("えっちな色")
		PRINT [浮気]
		RESETCOLOR
		PRINT を得た……
		PRINTL
	ENDIF
	IF TALENT:TARGET:セフレ
		PRINTL
		;淫乱のキャラは1回でセフレになって文章と齟齬が出るので表示しない
		SIF !TALENT:TARGET:淫乱
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%と幾度か体を重ね、抵抗感が薄れてきたようだ…
		PRINTFORM %CALLNAME:TARGET%は
		SETCOLOR カラーパレット("えっちな色")
		PRINT [セフレ]
		RESETCOLOR
		PRINT を得た
		PRINTL
		IF GETBIT(RFLAG:コマンド結果受渡し変数４, 1)
			;
			IF GETBIT(TALENT:TARGET:陥落不可, 0)
				PRINTFORML %CALLNAME:TARGET%の[陥落不可]が[恋慕不可]に変化しました。
			ELSE
				PRINTFORML %CALLNAME:TARGET%の[セフレ不可]が消滅しました。
			ENDIF
		ENDIF
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の願いを
	IF RFLAG:コマンド結果受渡し変数２ < 16
		PRINT きっぱりと
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 31
		PRINT 呆れた様子で
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 50
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 71
		PRINT 迷いながらも
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 99
		PRINT 真っ赤になって
	ENDIF
	PRINTL 断った
	
	LOCAL = 100
	SIF RFLAG:コマンド結果受渡し変数２ > 0
		LOCAL = (100 - CFLAG:TARGET:お願いックス蓄積値) / RFLAG:コマンド結果受渡し変数２
	IF LOCAL < 1
		;次でイケる
		PRINTL もうひと押しでイケそうな気がする…
	ELSEIF LOCAL < 2
		;あと２回
		PRINTL 近いうちに願いを聞いてくれそうな気がする…
	ELSEIF LOCAL < 4
		;あと３～４回
		PRINTL 願いを聞いてくれるには、まだ何度か回数を重ねる必要があるだろう…
	ELSEIF LOCAL < 10
		;５～１０回
		PRINTL 願いを聞いてくれるには、かなり根気が必要そうだ…
	ELSE
		;それ以上
		PRINTL 何かしらの手段を講じない限り、このままではほぼほぼ無理な話だろう…
	ENDIF

	IF あなた特殊能力:フレンドリーセンス
		PRINTL
		PRINTFORML お願い蓄積値：{CFLAG:TARGET:お願いックス蓄積値, 2} / 100
		PRINTL ※100に到達するとお願い成功
	ENDIF
ENDIF

@COM_353_性交渉回数取得(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 性交渉回数
性交渉回数  = CFLAG:対象キャラ:お願いックス回数
性交渉回数 += CFLAG:対象キャラ:泥酔ックス回数 / 3
性交渉回数 += EXP:対象キャラ:貸出経験 / 2
RETURNF 性交渉回数

@COM_353_セフレ取得判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
SIF 性癖素質:対象キャラ:ゆきずりセックス > 0
	RETURNF 0
SIF TALENT:対象キャラ:淫乱
	RETURNF 1
SIF COM_353_性交渉回数取得(対象キャラ) >= 4 
	RETURNF 1
RETURNF 0

