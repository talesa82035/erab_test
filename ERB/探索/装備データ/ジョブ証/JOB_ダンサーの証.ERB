@証関数_ダンサーの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = ダンサーの証
	CASE "クラス名"
		; SIF 対象キャラ <= 0
		; 	RETURN 0
		; SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
		; 	CASEELSE
		; 		RESULTS = ダンサー
		; 		RESULTS:1 = Extra.I
		; 		RETURN 1
		; ENDSELECT
		RETURN 0
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "舞い踊りながら戦い、連続攻撃と回避に特化した証。<br>"
		詳細文文字列受け渡し変数 += "ＨＰや防御力が減少するが、多くの手数で攻撃することが出来る。<br>"
		詳細文文字列受け渡し変数 += "装備するとダンジョン探索中に「踊り子Lv1」と同じ効果が得られる。<br>"
		詳細文文字列受け渡し変数 += "装備すると［自身の踊り子Lv✕5％］の回避率上昇効果を得る。<br>"
	CASE "装備補正"
		一時ステータス補正保存:回避率 += 知識素質:対象キャラ:踊り子 * 5
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「境界の導き」の宝箱で入手）
;@証出現率_ダンサーの証(入手タイプ, 装備ランク)

@証アビ関数_ダンサーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフ番号
#DIM CONST 消費ＭＰ = 50

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = アトラクト・サルサ
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■アトラクト・サルサ<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身に［現在ＨＰの15％］のＨＰ消費を与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　自身に［100％］（独自枠・1ターン）の回避率上昇効果を与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　自身に［20％］（独自枠・3ターン）の連続攻撃率上昇効果を与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%は華麗に踊り始めた！
		BATTLE_STATE:戦闘行動キャラ:ＨＰ = MAX(1, BATTLE_STATE:戦闘行動キャラ:ＨＰ * 85 / 100)
		アビテンプレート用_行動消費無しフラグ = 1
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率_ダンサー
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率_ダンサー
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:効果量 = 20
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_ダンサーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIM 追撃キャラ
#DIM 候補配列, 10
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 80

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = プリンシパル・クラシック
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■プリンシパル・クラシック<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：通常攻撃を行う。<br>"
		詳細文文字列受け渡し変数 += "　　　　ランダムな味方一人が通常攻撃を行う。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体

		VARSET 候補配列
		LOCAL:1 = 0
		FOR LOCAL, 0, 4
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:LOCAL:ＨＰ < 1
				CONTINUE
			候補配列:(LOCAL:1) = LOCAL
			LOCAL:1 ++
		NEXT
		追撃キャラ = 候補配列:(RAND:(LOCAL:1))
		IF 追撃キャラ != 戦闘行動キャラ
			アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%は%CALLNAME:(BATTLE_STATE:追撃キャラ:キャラ登録番号)%と優雅に舞い踊った！
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_通常攻撃処理
			
			CALL アビ汎用変数文字列リセット
			SWAP 戦闘行動キャラ, 追撃キャラ
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_通常攻撃処理
			SWAP 戦闘行動キャラ, 追撃キャラ
		ELSE
			アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%は優雅に舞い踊った！
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_通常攻撃処理
			CALL アビテンプレート_通常攻撃処理
		ENDIF
ENDSELECT

@証アビ関数_ダンサーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 110

SELECTCASE 処理モード
	CASE "アビ名"
		IF 対象キャラ <= 0
			TSTR:コマンド名受渡 = リンガリング・セント
		ELSEIF 知識素質:対象キャラ:踊り子 > 1
			TSTR:コマンド名受渡 = リンガリング・セント
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
			TSTR:コマンド名受渡 = リンガリング・セント
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■リンガリング・セント（装備者が踊り子Lv2以上or連撃タイプの時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 '= @"　効果：自身に（通常枠・4ターン・消去不可）の再行動効果を与える。"
		詳細文文字列受け渡し変数 '= @"　　　　［30％］（C枠・4ターン・消去不可）の防御力減少効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%は独特な回転する舞を踊り始めた！
		CALL アビ対象選択テンプレート_自己のみ

		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力C
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_ダンサーの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 60

SELECTCASE 処理モード
	CASE "アビ名"
		IF 対象キャラ <= 0
			TSTR:コマンド名受渡 = 情熱の舞
		ELSEIF 知識素質:対象キャラ:踊り子 > 1
			TSTR:コマンド名受渡 = 情熱の舞
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("回避")
			TSTR:コマンド名受渡 = 情熱の舞
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■情熱の舞（装備者が踊り子Lv2以上or回避タイプの時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身に［50％］（A枠・4ターン）の敵対心上昇効果を与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　［攻撃力✕200％］（通常枠・4ターン）の回避反撃効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%は敵に流し目を送り、情熱的に舞い踊った！

		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 敵対心A
		アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 回避反撃
		アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
		アビ汎用変数:効果割合 = 200
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

