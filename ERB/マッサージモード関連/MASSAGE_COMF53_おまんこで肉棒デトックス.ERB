;-------------------------------------------------
;肉棒でおまんこマッサージ♡
;-------------------------------------------------
@MASSAGE_COMNAME53
#FUNCTIONS

TSTR:コマンド名受渡 = おまんこで肉棒デトックス_H_
TSTR:コマンドカラー受渡 = FFD900

;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_53
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_53
LOCALS = <br>■おまんこで肉棒デトックス<br>
LOCALS += "ガチガチに凝って苦しそうなおちんぽを、専用の『施術道具』を使ってマッサージする。"
LOCALS += "<br>挿入するまでは一兆歩譲ってエステだったかもしれないが、挿入してしまったらそれはもうセックスなので、うふふモードに突入する。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ｃ・露出・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：・Ｃ性交経験_Ｖ挿入(TARGET)・Ｖ性交経験(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@MASSAGE_COM53
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ
;まずキャラの状態によって３つに分岐する

;肉棒マッサージを流用
CALL MASSAGE_分岐_COM51(TARGET)
RFLAG:コマンド結果受渡し変数 = RESULT
;RFLAG:コマンド結果受渡し変数に結果を入れる
; 1:通常許可
; 2:なし崩し許可
; 3:レイプ
;失敗はそもそもコマンドを出さないので分岐しない

;ここでレイプ時の記憶喪失手段使うよ～とか
;レイプするけどOK～？　みたいな確認分岐を入れる
IF RFLAG:コマンド結果受渡し変数 == 2
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%が何をしようとしているのかに気付き、やめるように言ってくる。
	PRINTW しかしその拒否は弱々しく、強く押せばなし崩しで許可してくれそうだ……
	PRINTL このまま実行しますか？
	PRINTL
	PRINTBUTTONLC "[0] はい", 0
	PRINTBUTTONLC "[1] いいえ", 1
	BINPUT
	IF RESULT == 1
		PRINTFORMW %CALLNAME:PLAYER%は思い直すことにした。
		RETURN -1
	ENDIF
	;履歴セット
	CFLAG:TARGET:泥酔ックス回数 += 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中に快楽でとろとろにした%CALLNAME:TARGET%を手籠めにした</font><img src='えっちハート'>","うふふ")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中に快楽に負け、%CALLNAME:PLAYER%と一線を越えてしまった</font><img src='えっちハート'>","うふふ")
	

	IF ((CFLAG:TARGET:お願いックス回数 + CFLAG:TARGET:泥酔ックス回数 / 3) >= 4 || TALENT:淫乱) && 性癖素質:ゆきずりセックス < 1
		SETBIT CFLAG:1 , 0
		TALENT:セフレ = 1
		初体験日:セフレ = DAY
		RFLAG:コマンド結果受渡し変数３ = 1
		RFLAG:コマンド結果受渡し変数４ = TALENT:陥落不可
		CLEARBIT TALENT:陥落不可, 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%を[セフレ]陥落させた</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>陥落素質[セフレ]を得た</font><img src='えっちハート'>","うふふ")
	ENDIF
ELSEIF RFLAG:コマンド結果受渡し変数 == 3
	;レイプしないを選んだ時はキャンセル扱いでコマンド選択に戻す
	PRINTFORM 『マッサージ』を建前にしながら下半身の服を脱ぎ
	PRINTFORM %TEXTR("とろとろに濡れ/準備の整っ")%た%TEXTR("おまんこ/秘部/膣口")%を見せつけた
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("が、今から何をしようとしているのか/に、これから何をされてしまうのか")%に気付いた%CALLNAME:TARGET%は
	IF TFLAG:マッサージ_鬼灯の香使用時間 + 20 < DAY * 1440 + TIME
		PRINTFORM 夢見心地にぼんやりとしながらも
		IF TALENT:TARGET:度胸 == -1 || TALENT:TARGET:応答 == -1 || TALENT:TARGET:プライド == -1 || TALENT:TARGET:陽気／陰気 == -1
		;臆病、大人しい、プライド低い、陰気
			PRINTFORML やめて欲しいと懇願する。
		ELSE
			PRINTFORML 懸命に、やめ%TEXTR("るように言ってくる/て欲しいと口にする")%。
		ENDIF
		PRINTFORMW 無理やりコトに及ぼうとすれば、きっと普段の%CALLNAME:TARGET%であれば%TEXTR("強く/激しく")%抵抗していただろう……。
	ELSE
		PRINTFORML やめるように言ってくる。
		PRINTFORMW 無理やりコトに及ぼうとすれば、きっと%CALLNAME:TARGET%は強く抵抗してくるだろう……。
	ENDIF
	PRINTL
	;香使用から20分以上経ってると許可
	IF TFLAG:マッサージ_鬼灯の香使用時間 > 0 && TFLAG:マッサージ_鬼灯の香使用時間 + 20 < DAY * 1440 + TIME
		;ここで記憶を曖昧にする薬の所持分岐
		PRINTFORML しかし「鬼灯の香」が十分に効いている今ならば、快楽と薬効で朦朧とした%CALLNAME:TARGET%にはもはや赤子程度の抵抗力しか残っていない。
		PRINTFORML おまけに香の効果で行為中の記憶は曖昧になり、「よく覚えていないけど気持ちよかった」程度にしか残らない……。
		PRINTFORM これならば%CALLNAME:PLAYER%の力でも容易に抵抗を封じ、
		SELECTCASE RAND:5
			CASE 0
				IF TALENT:TARGET:恋人持ち == 1 || TALENT:TARGET:恋人持ち == 2
					PRINTFORM %CALLNAME:TARGET%の一番奥に無理やり%TEXTR("不貞の/浮気の/浮気セックスの/想い人以外に犯される")%快楽を%TEXTR("覚えこませる/教えてやる/刻み付けてやる")%
				ELSE
					PRINTFORM 無理やり%TEXTR("に関係を持つ/犯す")%
				ENDIF
			CASE 1
				IF アイドル判定(TARGET)
					PRINTFORM %TEXTR("犯され/朦朧逆レイプ/セックス/望まぬ")%スキャンダルでファンを裏切らせる
				ELSE
					PRINTFORM 合意のないままに%CALLNAME:TARGET%の体を貪る
				ENDIF
			CASE 2
				PRINTFORM 男としての『使い心地』を存分に楽しむ
			CASE 3
				PRINTFORM マッサージで%CALLNAME:TARGET%の体に触れたせいで散々ムラつかされた%TEXTR("獣欲/欲望")%%TEXTR("を思う存分その肉棒にぶつける/の責任を取らせる")%
			CASE 4
				PRINTFORM ノーリスクで逆レイプを楽しむ
		ENDSELECT
		PRINTFORMW ことが出来るだろう。
		PRINTL
		PRINTL このまま実行しますか？
		PRINTL
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		BINPUT
		IF RESULT == 1
			PRINTFORMW %CALLNAME:PLAYER%は思いとどまることにした。
			RETURN -1
		ENDIF
		CFLAG:TARGET:泥酔ックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中、快楽とお香で抵抗力を奪った%CALLNAME:TARGET%を無理やり手籠めにした</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>マッサージ中、快楽とお香で朦朧とする中で抵抗むなしく%CALLNAME:PLAYER%に逆レイプされてしまった</font><img src='えっちハート'>","うふふ")
		IF GETBIT(TALENT:TARGET:非童貞, 0) == 0
			IF 知識素質:TARGET:高貴 >= 1
				CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%のロイヤル童貞を無理やり奪ってやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>快楽とお香で朦朧とする意識の中、%CALLNAME:PLAYER%に無理やり犯され、高貴な身でありながら童貞を%TEXTR("奪われ/散らされ")%てしまった</font><img src='えっちハート'>","うふふ")
			ELSE
				CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%の童貞を無理やり奪ってやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>快楽とお香で朦朧とする意識の中、%CALLNAME:PLAYER%に無理やり犯され童貞を%TEXTR("奪われ/散らされ")%てしまった</font><img src='えっちハート'>","うふふ")
			ENDIF
		ENDIF
		;うわべだけ実行判定
		;騎乗位のみ
		PRINTL 騎乗位

		実行値 = 0
		＋表示フラグ = 0

		;すべての命令に共通の要素を考慮
		CALL COM_ORDER
		実行値 = RESULT
		＋表示フラグ = RESULT:1

		;ABL:欲望
		IF ABL:欲望
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += ABL:欲望 * 3
			PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 3})
			＋表示フラグ = 1
		ENDIF
		;ABL:Ｃ感覚
		IF ABL:Ｃ感覚
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += ABL:Ｃ感覚 * 3
			PRINTFORM Ｃ感覚LV{ABL:Ｃ感覚}({ABL:Ｃ感覚 * 3})
			＋表示フラグ = 1
		ENDIF
		;快楽刻印
		IF MARK:快楽刻印
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += MARK:快楽刻印 * 3
			PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 3})
			＋表示フラグ = 1
		ENDIF
		;PALAM:欲情
		IF GETPALAMLV(PALAM:欲情, 5)
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += GETPALAMLV(PALAM:欲情, 5) * 3
			PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 3})
			＋表示フラグ = 1
		ENDIF
		;恥じらい
		IF TALENT:羞恥心 > 0
			PRINT  - 
			実行値 -= 2
			PRINT 羞恥心(2)
			＋表示フラグ = 1
		ENDIF
		;快感の否定
		IF TALENT:快感応答 < 0 && !TALENT:恋慕
			PRINT  - 
			実行値 -= 5
			PRINT 快感の否定(5)
			＋表示フラグ = 1
		ENDIF
		;恋慕
		IF TALENT:恋慕
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 15
			PRINT 恋慕(15)
			＋表示フラグ = 1
		ELSE
			;男嫌い（恋慕で無効）
			IF TALENT:性別嗜好 & 1 && TALENT:PLAYER:性別 == 2
				PRINT  - 
				実行値 -= 12
				PRINT 男嫌い(12)
				＋表示フラグ = 1
			ENDIF
			;女嫌い（恋慕で無効）
			IF TALENT:性別嗜好 & 2 && TALENT:PLAYER:性別 == 1
				PRINT  - 
				実行値 -= 12
				PRINT 女嫌い(12)
				＋表示フラグ = 1
			ENDIF
		ENDIF
		;セフレ
		IF TALENT:セフレ
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 10
			PRINT セフレ(10)
			＋表示フラグ = 1
		ENDIF
		;未陥落
		IF TALENT:セフレ == 0 && TALENT:恋慕 == 0
			PRINT  - 
			実行値 -= 10
			PRINT 未陥落(10)
			＋表示フラグ = 1
		ENDIF
		;童貞
		IF GETBIT(TALENT:非童貞, 0) == 0
			PRINT  - 
			実行値 -= 10
			PRINTFORM 童貞({LOCAL})
			＋表示フラグ = 1
		ENDIF
		;C経験
		IF EXP:Ｃ経験 < EXPLV:2
			PRINT  - 
			実行値 -= 5
			PRINT Ｃ経験不足(5)
			＋表示フラグ = 1
		ENDIF
		;媚薬
		IF TEQUIP:媚薬
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 6
			PRINT 媚薬(6)
			＋表示フラグ = 1
		ENDIF
		;発情期
		IF CFLAG:発情期フラグ < 0
			SIF ＋表示フラグ
				PRINT  + 
			実行値 += 6
			PRINT 発情中(6)
			＋表示フラグ = 1
		ENDIF

		;合計を表示(20以上で実行…？)
		PRINTFORM  = {実行値}
		SIF 実行値 < 必要実行値
			PRINT  < 
		SIF 実行値 == 必要実行値
			PRINT  = 
		SIF 実行値 > 必要実行値
			PRINT  > 
		PRINTFORM 実行値{必要実行値}
		WAIT
		PRINTL

		;ここまでの実行値
		PRINTFORM %CALLNAME:TARGET%の尊厳({実行値})

		;鬼灯の香
		PRINT  + 
		実行値 += 999
		SETCOLOR カラーパレット("赤")
		PRINTFORM 鬼灯の香(999)
		RESETCOLOR

		;再度合計を表示()
		PRINT  = 
		SETCOLOR カラーパレット("赤")
		PRINTFORM {実行値}
		RESETCOLOR
		PRINT  >>>> 
		PRINTFORM 実行値{必要実行値}
		WAIT
		PRINTL

		CALL PRINT_STR(@"_FS_4_騎乗位_")
		SETCOLOR カラーパレット("赤")
		PRINTFORML 　逆レイプ成功
		RESETCOLOR
	ELSEIF TFLAG:マッサージ_鬼灯の香使用時間
		PRINTL 鬼灯の香が効いてくるまでは、まだもう少し時間が必要だろう。
		PRINTFORMW %CALLNAME:PLAYER%はまだ待つことにした。
		RETURN -1
	ELSE
		PRINTL このままでは実行出来ない。何か抵抗を封じるための道具が必要になるだろう。
		PRINTFORMW %CALLNAME:PLAYER%は思い直すことにした。
		RETURN -1
	ENDIF
	

ENDIF

;通常モードに移行はターンエンド時に行う
;騎乗位
;引数は前から快楽量、露出量、接触量
PALAM:PLAYER:潤滑 += 5000
CALL MASSAGE_逆体位COM(500, 120, 150)
CALL SOURCE_CALC_奉仕(PLAYER, TARGET, 250)
RETURN RESULT


@MASSAGE_逆体位COM(ARG, ARG:1, ARG:2)
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる

;Ｖ挿入から持ってきた
CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, ARG)
CALL SOURCE_CALC_露出(PLAYER, TARGET, ARG:1)
CALL SOURCE_CALC_接触(PLAYER, TARGET, ARG:2)

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, ARG)


;苦痛
LOCAL = 200 * PAIN_CHECK_V(PLAYER) / 1000
CALL SOURCE_CALC_苦痛(PLAYER, TARGET, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 300)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 150)
	ENDIF
ENDIF
;処女喪失
CALL LOST_VIRGIN(PLAYER, TARGET, 1)
CALL LOST_MAN_VIRGIN(TARGET, PLAYER)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

EXP:PLAYER:Ｖ性交経験 ++
EXP:Ｃ性交経験_Ｖ挿入 ++

;同性愛
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4

	EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE53
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(53)
	RETURN RESULT

;あなた以外PLAYERだととりあえず封印
SIF PLAYER != MASTER
	RETURN 0

;Vがない場合駄目
SIF TALENT:PLAYER:Ｖ感度 == -2
	RETURN 0
;オトコだとダメ
SIF !(TALENT:PLAYER:性別 & 1)
	RETURN 0
;二人きりのみ
SIF GET_TARGETNUM() > 1
	RETURN 0

;勃起必須
SIF BASE:TARGET:勃起 < 1000
	RETURN 0

;調教者がオトコ、半人半妖、ふたなりがないとダメ
SIF GETBIT(TALENT:TARGET:性別, 1) == 0
	RETURN 0

;理性警戒の合計が1以下じゃないとダメ
;エロマッサージの終着点ポジションなのに、香さえ回れば他のエロマッサージより先に解禁されるのは風情に欠けるため
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM53
#DIM 対象キャラ
#DIM 対象キャラ２
;RFLAG:コマンド結果受渡し変数に結果を入れる
; 1:通常許可
; 2:なし崩し許可
; 3:レイプ

;下着がある場合はRFLAG:コマンド結果受渡し変数２に1が入り、下着を剥ぎ取っている

PRINTL
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		PRINTFORM %CALLNAME:PLAYER%は
		IF RFLAG:コマンド結果受渡し変数２
			PRINTFORM %CALLNAME:TARGET%の施術着を脱がせ、
		ENDIF
		PRINT とろとろに濡れた自らのおまんこに、
		PRINTFORM %CALLNAME:TARGET%の肉棒
		PRINTL をゆっくりと挿入した……。
	CASE 2
		PRINTFORML %CALLNAME:PLAYER%がどうしてもと頼み込むと、%CALLNAME:TARGET%は迷いながらも『マッサージ』ではなくなることに頷いた。
		PRINTFORM %CALLNAME:PLAYER%は
		IF RFLAG:コマンド結果受渡し変数２
			PRINTFORM %CALLNAME:TARGET%の施術着を脱がせ、
		ENDIF
		PRINT とろとろに濡れた自らのおまんこに、
		PRINTFORM %CALLNAME:TARGET%の肉棒
		PRINTL をゆっくりと挿入した……。

		IF RFLAG:コマンド結果受渡し変数３
			SIF !TALENT:TARGET:淫乱
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%と幾度か体を重ね、抵抗感が薄れてきたようだ…
			PRINTFORML %CALLNAME:TARGET%は[セフレ]を得た
			IF GETBIT(RFLAG:コマンド結果受渡し変数４, 1)
				IF GETBIT(TALENT:TARGET:陥落不可, 0)
					PRINTFORML %CALLNAME:TARGET%の[陥落不可]が[恋慕不可]に変化しました。
				ELSE
					PRINTFORML %CALLNAME:TARGET%の[セフレ不可]が消滅しました。
				ENDIF
			ENDIF
		ENDIF
	CASE 3
	;むりやり犯すコマンドから持ってきた
		PRINTFORM %CALLNAME:PLAYER%は
		IF RFLAG:コマンド結果受渡し変数２
			PRINTFORM %CALLNAME:TARGET%の施術着を脱がし、
		ENDIF

		;仰向けだと正常位
		PRINTFORM 仰向けのまま%TEXTR("か弱い力で抵抗する/微かな力でもがいて抵抗しようとする/ぼんやりした頭でイヤイヤとかぶりを振る/ろくな抵抗すらままならない/抵抗力を奪われ言いなりの身となった")%
		PRINTFORML %CALLNAME:TARGET%の%TEXTR("上に馬乗りになり/上に覆いかぶさって/男根を眺めながら")%、

		IF TALENT:TARGET:恋人持ち == 1
			;恋人持ち
			IF GETBIT(TALENT:TARGET:非童貞, 0) == 0
				PRINTFORM %TEXTR("まだ恋人にも手を付けられていない/恋人に捧げるはずだったであろう/恋人のために大事にしていたであろう")%
			ELSE
				PRINTFORM 恋人にしか許してはいけないはずだった
			ENDIF
		ELSEIF TALENT:TARGET:恋人持ち == 2
			;人妻
				IF GETBIT(TALENT:TARGET:非童貞, 0) == 0
				PRINTFORM 結婚していながら、いまだ清き身だった
			ELSE
				PRINTFORM 妻にしか許してはいけないはずだった
			ENDIF
		ELSEIF TALENT:TARGET:恋人持ち == 3
		;未亡人
			IF GETBIT(TALENT:TARGET:非童貞, 0) == 0
				PRINTFORM かつての嫁に%TEXTR("捧げることすら叶わなかった/操をたてていただろう")%寡夫の
			ELSE
				PRINTFORM 妻を亡くして%TEXTR("持て余していた/欲求不満だった")%であろう
			ENDIF
		ELSEIF TALENT:TARGET:恋人持ち == -1
		;想い人あり
			IF GETBIT(TALENT:TARGET:非童貞, 0) == 0
				PRINTFORM %TEXTR("まだ誰にも手を付けられていない/想い人に捧げるはずだったであろう/想い人のために残しておいたであろう")%
			ELSE
				PRINTFORM 想い人にしか許してはいけないはずだった
			ENDIF
		ELSEIF キャラ一致チェック(TARGET, "[蒼天の剣閃]モニカ") || キャラ一致チェック(TARGET, "[スカイセイバー]リーシャ")
			IF GETBIT(TALENT:TARGET:非童貞, 0) == 0
				PRINTFORM 秩序の如くかたく守ってきたであろう
			ELSE
				PRINTFORM 秩序の如く乱れてなどいないはずの
			ENDIF
		ELSEIF TALENT:TARGET:種族 == 5 && GETBIT(TALENT:TARGET:非童貞, 0) == 0
		;星晶獣かつ処女
			PRINTFORM %TEXTR("不老/長命")%の存在でありながら純潔を保ったままの
		ELSEIF GETBIT(TALENT:TARGET:非童貞, 0) == 0
			IF RAND:3 == 0
				PRINTFORM %CALLNAME:PLAYER%に犯されるために今まで純潔を守ってくれていた
			ELSE
				PRINTFORM %TEXTR("純潔を守っていた/まだ穢れを知らなかった")%
			ENDIF
		ELSE
		;その他　犯す側の、自分勝手であまりにも都合の良い解釈
			PRINTFORM %CALLNAME:PLAYER%%TEXTR("の股間をムラつかせる/の獣欲をムラつかせてやまない/に食べられるための美味しそうな")%
		ENDIF

		PRINTFORM %CALLNAME:TARGET%の

		;おまんこ冠詞群
		;範囲が狭くシコリティの高そうなものから順につけていく
		IF 知識素質:TARGET:高貴 >= 1
		;高貴持ち
			PRINTFORM %TEXTR("高貴な/ロイヤル")%
		ELSEIF アイドル判定(TARGET)
		;アイドル勢
			PRINTFORM アイドル
		ELSEIF 固有素質チェック(TARGET, "女子高生")
			PRINTFORM 女子高生
		ELSEIF 固有素質チェック(TARGET, "中学生")
			PRINTFORM 中学生
		ELSEIF 固有素質チェック(TARGET, "小学生")
			PRINTFORM 小学生
		ELSEIF 固有素質チェック(TARGET, "サーヴァント")
		;包括して英霊という表現を使っていいものかは……まぁ文句来たら変えればヨシ
			PRINTFORM %TEXTR("英霊/サーヴァント")%
		ELSEIF TALENT:TARGET:ギャル
		;ギャル
			PRINTFORM ギャル
		ELSEIF TALENT:TARGET:経験豊富
		;経験豊富
			PRINTFORM %TEXTR("経験豊富な/使いこまれた")%
		ELSEIF TALENT:TARGET:性別嗜好 == 2
		;女嫌い
			PRINTFORM 女嫌いな
		ELSEIF TALENT:TARGET:種族 == 5
		;星晶獣
			PRINTFORM 強大な星晶獣の
		ELSEIF TALENT:TARGET:男性器サイズ <= -1
		;短小以下
			PRINTFORM %TEXTR("小さく可愛らしい/ちっちゃくて可愛い")%
		ELSEIF TALENT:TARGET:男性器サイズ >= 1
		;巨根以上
			PRINTFORM エラの張った立派な
		ENDIF
		IF RISKY_DAY(TARGET) >= 1
			IF CFLAG:TARGET:発情期フラグ < 0
				PRINTFORM 発情
			ENDIF
		ENDIF
	
		SIF TALENT:TARGET:恋人持ち == 3
			PRINTFORM 独り身
		SIF TALENT:TARGET:恋人持ち == 2
			PRINTFORM 既婚者

		SIF TALENT:TARGET:処女
			PRINTFORM %TEXTR("新品/未使用")%
		SIF TALENT:TARGET:年齢層 == 10 || (BASE:TARGET:年齢 <= 13 && BASE:TARGET:年齢 != 0)
			PRINTFORM %TEXTR("ショタ/こども/ガキ")%
	
		PRINTFORM ちんぽを、

		PRINTFORM とろとろに濡れた自らのおまんこに
		PRINTFORML %TEXTR("ゆっくり/ぬぷぬぷ")%と%TEXTR("挿入していった/飲み込み、味わった/挿入し『男』を堪能した/飲み込み、『男』としての尊厳を無残に踏み躙ってやった/押し込んでいった/飲み込んでいった/埋めていった")%…
		PRINTL

		;おまけ
		IF TALENT:TARGET:恋人持ち == 2
			PRINTW 
			PRINTFORM %CALLNAME:TARGET%は朦朧とした意識の中で、うわごとのように伴侶への謝罪を繰り返してい
			IF PALAM:欲情 >= PALAMLV:3
				PRINTFORML たが、
				PRINTFORML %CALLNAME:PLAYER%が膣内を締めてやると次第にその声は快楽に蕩け嬌声の中に埋もれていった…
			ELSE
				PRINTFORML る…
			ENDIF
		ENDIF
ENDSELECT

