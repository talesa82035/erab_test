
;----------------------------------------------------
;キャラ固有コマンド処理
;----------------------------------------------------
;コマンド名
;----------------------------------------------------
@CHARACOMNAME370_2
#FUNCTION
;アンチラの固有コマンド
;10歳にしては賢い頭と年齢相応の無邪気さで、コスハメ感覚で知り合いのカラダを使って遊ぶえっちっちモンキー。過ち。

TSTR:コマンド名受渡 = 変化の法えっち

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_370_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ｖ系")
;COM60「V挿入する」の派生
TFLAG:派生コマンド指定 = 60

@COM_TOOLTIP_370_2
LOCALS = <br>■変化の法えっち<br>
LOCALS += "アンチラが、みんなに内緒で「わるいコト」を楽しませてくれるコマンド。<br>体力の消費が多く、また短時間に連続で使うこともできない。<br>Ｖ挿入系モード時のみ実行可能。"
LOCALS += "<br>COMタイプ：Ｖ系<br>取得ソース：快Ｖ・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：Ｖ性交経験(TARGET)・Ｇスポ開発(TARGET)・Ｃ性交経験_Ｖ挿入(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

;-------------------------------------------------
;実行処理
;-------------------------------------------------
@CHARACOM370_2
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


;体力消費多め、時間経過長め、かわりに得られるソースも多め
;内部的には腰を動かすと同様のソースを時間経過のぶん増やした。副次効果として射精を合わせやすくなった。
;口上とはまぁ齟齬は出るけど……管理ムリ！
CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, 800)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 300)
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 800)

DOWNBASE:TARGET:体力 += 100

TIME += 10

;連打できたら情緒がオワオワリなのでとりあえずスキンシップのをもってきた。TCVARを新設するならここも変えること
変化の法時間登録 = 1440 * DAY + TIME

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:Ｃ性交経験_Ｖ挿入 ++
EXP:Ｖ性交経験 ++

;同性愛
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4

;だ～い好きなセフレのあなたに目一杯楽しんでほしいという愛から出た行動ゆえに愛情経験をあげたい
;まぁ、アンチラ自身が楽しんでないとも言わないが。Win-Winってやつ

EXP:愛情経験 += TALENT:セフレ * 2


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370_2
;作成中につき封印。実装する時やテストする時は真下にあるRETURN 0を消してね
;RETURN 0

;設定上は、V挿入中に「アンチラの気まぐれで」行われるコマンドなので
;実行可能タイミングは運次第にしたい。条件がそろった時に、運がよければ見られるレアコマンド的な
;でも、口上には射精を伴うことで見られるようになる部分もちょいちょいあるからなぁ……うぅむ……。
;アンチラへの負担は大きいものとすることで一時的なものとしてワンコマンドで収めている。

;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
;TARGETがアンチラ
LOCAL = GETCHARA(2)
SIF TARGET != LOCAL
	RETURN 0
;Ｖ挿入系モードじゃない時はアウト
SIF !MODE_存在判定_完全一致("Ｖ挿入系", PLAYER, TARGET)
	RETURN 0
;ベッドがないとダメ
SIF BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置) == 0
	RETURN 0
;二人きり限定
SIF GET_TARGETNUM() > 1
	RETURN 0
	
;セフレかつ性知識がある程度ないとダメ。アンチラは元々0だから、1か2で解禁が妥当？2だとちょとハードル高いか？
;恋慕だと自分だけを見ててほしいからやってくれない。セフレ時のみ。恋慕でもして欲しければ、マゾ性感一定以上との組み合わせ？要検討

SIF !TALENT:セフレ
	RETURN 0
SIF 知識素質:性知識 < 1
	RETURN 0
	
;これを連打できちゃったらあまりにも情緒に欠けるので、とりあえず1時間間隔
SIF TIME_PROGRESS(変化の法時間登録) < 60
	RETURN 0
	
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM370_2


PRINTFORML %CALLNAME:TARGET%は変化の法を使って、他人のカラダと膣の締まりを%CALLNAME:PLAYER%に堪能させた…

