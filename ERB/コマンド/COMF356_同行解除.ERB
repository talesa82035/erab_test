
@COMNAME356
#FUNCTIONS
TSTR:コマンド名受渡 = 同行解除
IF CFLAG:TARGET:デート == MASTER + 100
	TSTR:コマンド名受渡 = デートを終える
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_356
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_356
LOCALS = <br>■同行解除<br>
LOCALS += "キャラの同行を解除するコマンド。<br>一緒に隠密中・うふふ中は同行が解除されない。<br>デート中は[デートを終える]に変化。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM356
IF CFLAG:TARGET:デート == MASTER + 100
	;将来的にデートの内容に応じて好感度上昇とかしたいが未実装
	PRINTFORMW %CALLNAME:TARGET%とのデートを終え、別れた
ELSEIF TCVAR:TARGET:デートちょっかい値 > 0
	PRINTFORM %CALLNAME:TARGET%は
	IF CHECK_SPERM(TARGET, MASTER) > 0
		PRINTFORM %CALLNAME:MASTER%の精液を胎に残したまま、
	ENDIF
	PRINTFORMW %CALLNAME:(ABS(CFLAG:TARGET:デート) - 100)%とのデートに戻っていった
	CALL デートちょっかい終了処理(TARGET)
ELSE
	PRINTFORMW %CALLNAME:TARGET%との同行を終え、別れた
ENDIF

;SOURCEまで回さないのでここで口上を表示させる
RESULT = 0
RESULTS =
IF FLAG:口上表示切り替えオプション > 0
	TRYCALLFORM KOJO_COM_{NO:TARGET}_356(TARGET)
ENDIF

CALL 同行解除処理(TARGET)
IF CFLAG:TARGET:デート == MASTER + 100
	CALL デート採点処理(TARGET)
ENDIF
TIME += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE356
;連れ出す実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(356)
	RETURN RESULT
;同行状態
SIF CFLAG:TARGET:同行 < 1 && CFLAG:TARGET:デート != MASTER + 100
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------




@同行解除処理(対象キャラ)
#DIM 対象キャラ

CFLAG:対象キャラ:同行 = 0
CALL MODE_DELETE_完全一致("同行系", PLAYER, 対象キャラ)
