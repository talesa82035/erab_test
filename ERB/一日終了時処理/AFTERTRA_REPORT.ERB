
;[SKIPSTART]

;────────────────────────────────────
;調教終了の処理
;────────────────────────────────────
@EVENTEND0
#DIM 現在ページ
#DIM 改行数
#DIM 前カット位置
#DIM 後カット位置
#DIMS 表示カテゴリ
#DIMS HTML文字列
#DIM CONST ページ送り用行数 = 40

;#DIMS CONST カテゴリ表示順制御 = "オナニー・夜這い報告", "従業員業務報告", "妊娠報告", "リゾート収支計算", "リゾート人気変動", "その他報告"
#DIMS CONST カテゴリ表示順制御 = "オナニー報告", "夜這い報告", "縁結び報告", "性癖開拓報告", "従業員業務報告", "妊娠報告", "リゾート収支計算", "リゾート人気変動", "依頼報告", "その他報告"
#DIMS カテゴリテキスト

;もしデート中ならデートを終わらせる
IF CFLAG:PLAYER:デート
	CALL デート採点処理(CFLAG:PLAYER:デート - 100)
ENDIF

;一日終了時の諸々の処理
探索フラグ = 0
施設ルピ補正保存 = 0
CALL 口上変数初期化
; SIF STRLENS(TSTR:1)
; 	CALL SELL_VIDEO

;睡眠フラグ
CFLAG:MASTER:睡眠 = 1

;高速船派遣の処理
IF 高速船記録:派遣状態 == "派遣準備中"
	高速船記録:派遣状態 = 派遣中
	IF STRFIND(高速船記録:初回派遣先記録, @"%高速船記録:派遣先登録%_") < 0
		高速船記録:初回派遣先記録 += @"%高速船記録:派遣先登録%_"
		RSTR:高速船初派遣報告_記録文字列 = 初めて%高速船記録:派遣先登録%へ高速船を派遣した。<br>
		CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"初めて%高速船記録:派遣先登録%へ高速船を派遣した","リゾート_実績")
	ENDIF
ENDIF

;知識学習が終わってない時の処理
CALL 学習判定_一日終了時()

;精力の回復(あなた)
CALL 精力ゲージ_一日終了時回復処理(MASTER)

CALL 寝取らせ夜這い判定()
SELECTCOM = -1

CALL JUEL_CHECK(MASTER)
CALL DENIAL_CHECK(MASTER)

CALL 性知識経験値_初体験ボーナス()

料理ZP補正保存 = 0
料理ルピ補正保存 = 0
その他ZP補正保存 = 0
その他ルピ補正保存 = 0
FLAG:シーガイド補正記録 = 0
RSTR:その他収支報告文字列 = 
VARSET レポート強調フラグ

PRINTW 一日が終わりました

;表示の前にステータスなどの処理を全て終わらせる
;以降に表示されるのは全て保存した文字列だけ（何度も表示呼んだりしても大丈夫なように処理する）

FOR LOCAL, 0, VARSIZE("カテゴリ表示順制御")
	;オナニー、夜這い、性癖開拓は同じ関数でまとめて処理している
	IF LOCAL == 0
		CALL 一日終了時レポート処理_オナニー・夜這い報告()
		LOCAL += 3
		CONTINUE
	ENDIF
	;収支計算だけは最後にやる
	SIF カテゴリ表示順制御:LOCAL == "リゾート収支計算"
		CONTINUE
	CALLFORM 一日終了時レポート処理_%カテゴリ表示順制御:LOCAL%()
NEXT
CALL 一日終了時レポート処理_リゾート収支計算()

SIF 表示カテゴリ == ""
	表示カテゴリ = %カテゴリ表示順制御:0%

$表示ラベル
DRAWLINE
HTML文字列 = 
;レポートの枠を表示
HTML文字列 += "<div rect='1315,0,6000,5000' border='31' bcolor='#C0C0C0'></div>"

;次にカテゴリを表示していく
FOR LOCAL, 0, VARSIZE("カテゴリ表示順制御")
	カテゴリテキスト '= カテゴリ表示順制御:LOCAL
	SIF レポート強調フラグ:LOCAL
		カテゴリテキスト '= @"！%カテゴリテキスト%"
	IF 表示カテゴリ == カテゴリ表示順制御:LOCAL
		; 表示中のカテゴリ/非ボタン
		HTML文字列 += @"<div rect='30,{LOCAL * 270},1300,200' padding='25' border='31' bcolor='#C0C0C0'>"
		HTML文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>%カテゴリテキスト%</font>"
		HTML文字列 += "</div>"
		HTML文字列 += @"<div rect='1300,{LOCAL * 268 + 31},31,150' color='#101010'></div>"
	ELSEIF レポート強調フラグ:LOCAL
		;強調表示するカテゴリ
		HTML文字列 += @"<div rect='30,{LOCAL * 270},1300,200' padding='25' border='31' bcolor='#C0C0C0'>"
		IF レポート強調フラグ:LOCAL > 0
			;ポジティブ系の強調表示
			HTML文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
		ELSE
			;ネガティブ系の強調表示
			HTML文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
		ENDIF
		HTML文字列 += @"<button value='{LOCAL}'>[{LOCAL, 2}] %カテゴリテキスト%</button>"
		HTML文字列 +=  "</font>"
		HTML文字列 +=  "</div>"
	ELSE
		; 通常表示
		HTML文字列 += @"<div rect='30,{LOCAL * 270},1300,200' padding='25' border='31' bcolor='#C0C0C0'>"
		HTML文字列 += @"<button value='{LOCAL}'>[{LOCAL, 2}] %カテゴリテキスト%</button>"
		HTML文字列 += "</div>"
	ENDIF
NEXT

HTML文字列 += @"<div rect='30,4800,1300,200' padding='25' border='31' bcolor='#C0C0C0'>"
HTML文字列 += @"<button value='99'>一日の報告表示を終わる</button>"
HTML文字列 += "</div>"

;そんでレポートの中身を表示する
LOCALS = %GETVARS(@"RSTR:%表示カテゴリ%_記録文字列")%
;ページの切り替えのために縦列を取得
改行数 = STRCOUNT(LOCALS, "<br>")
HTML文字列 += "<div rect='1400,4700,5800,300'>"
IF 改行数 > ページ送り用行数
	前カット位置 = -4
	IF 現在ページ > 0
		FOR LOCAL, 0, 現在ページ * ページ送り用行数
			IF STRFIND(LOCALS, "<br>", 前カット位置 + 4) > -1
				前カット位置 = STRFIND(LOCALS, "<br>", 前カット位置 + 4)
			ELSE
				BREAK
			ENDIF
		NEXT
	ENDIF
	LOCALS = %SUBSTRING(LOCALS, 前カット位置 + 4, -1)%
	後カット位置 = 0
	FOR LOCAL, 0, ページ送り用行数
		IF STRFIND(LOCALS, "<br>", 後カット位置 + 4) > -1
			後カット位置 = STRFIND(LOCALS, "<br>", 後カット位置 + 4)
		ELSE
			後カット位置 = -1
			BREAK
		ENDIF
	NEXT
	LOCALS = %SUBSTRING(LOCALS, 0, 後カット位置)%
	HTML文字列 += "　　　"
	IF 現在ページ > 0
		HTML文字列 += @"<button value='97'>[97] 前ページへ</button>"
	ELSE
		HTML文字列 += "　　　　　　 　"
	ENDIF
	HTML文字列 += "　　　　　　　　"
	IF 後カット位置 > -1
		HTML文字列 += @"<button value='98'>[98] 次ページへ</button>"
	ENDIF
ENDIF
IF 表示カテゴリ == "性癖開拓報告"
	HTML文字列 += "<br>"
	IF OPTION変数:性癖開拓自動継続
		HTML文字列 += @"<button value='96'>[96] 失敗時自動継続オプション ON / <font color='#%カラーパレット_HTML("グレーアウト")%'>OFF</font></button>"
	ELSE
		HTML文字列 += @"<button value='96'>[96] 失敗時自動継続オプション <font color='#%カラーパレット_HTML("グレーアウト")%'>ON</font> / OFF</button>"
	ENDIF
ENDIF
HTML文字列 += "</div>"
HTML文字列 += "<div rect='1400,100,5800,5000'>"
HTML文字列 += @"<nobr>%LOCALS%</nobr>"
HTML文字列 += "</div>"

HTML_PRINT HTML文字列
FOR LOCAL, 0, 45
	PRINTL
NEXT

BINPUT 100
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 96
		INVERTBIT OPTION変数:性癖開拓自動継続, 0
	CASE 97
		現在ページ --
	CASE 98
		現在ページ ++
	CASE 99
		現在ページ = 0
		;詳細必須の項目をここで表示する
		FOR LOCAL, 0, CHARANUM
			SIF CFLAG:LOCAL:妊娠経過日 >= 妊娠期間
				CALL BIRTHING(LOCAL)
			SIF PREGNANCY_SIZE(LOCAL) >= 2 && CFLAG:LOCAL:妊娠発覚 == 0
				CALL DETECT_PREGNANCY(LOCAL)
		NEXT
		CALL レポート後全体処理()

		BEGIN ABLUP
	CASE 0 TO 90
		現在ページ = 0
		表示カテゴリ = %カテゴリ表示順制御:LOCAL%
	CASEELSE
		IF GROUPMATCH(表示カテゴリ, "オナニー報告", "夜這い報告", "縁結び報告", "性癖開拓報告")
			CALL 一日終了時レポート表示_オナニー・夜這い報告(LOCAL)
		ELSE
			CALLFORM 一日終了時レポート表示_%表示カテゴリ%(LOCAL)
		ENDIF
ENDSELECT

GOTO 表示ラベル


@一日終了時レポート表示_オナニー・夜這い報告(ボタン番号)
#DIMS 分割用文字列, 3
#DIM ボタン番号
#DIM 分割番号

SELECTCASE ボタン番号
	CASE IS >= 60000
		PRINTL
		TRYCALLFORM KOJO_キャラ同士デート_{NO:(ボタン番号 - 60000)}(ボタン番号 - 60000)
		PRINTL
		TRYCALLFORM KOJO_キャラ同士デート_{NO:(ABS(CFLAG:(ボタン番号 - 60000):デート))}(ABS(CFLAG:(ボタン番号 - 60000):デート))
		PRINTL
	CASE IS >= 50000
		SPLIT CSTR:(ボタン番号 - 50000):夜這い報告_記録文字列, "ここでWAITを入れて表示を分離", RESULTS
		HTML_PRINT RESULTS:0
		WAIT
		HTML_PRINT RESULTS:1
		PRINTL
		VARSET RESULTS
		SPLIT CSTR:(ボタン番号 - 50000):夜這い口上渡し_記録文字列, "+++", RESULTS
		TRYCALLFORM KOJO_夜這い実行時_{NO:(ボタン番号 - 50000)}(ボタン番号 - 50000, RCVAR:(ボタン番号 - 50000):夜這い相手保存, TOINT(RESULTS:1), TOINT(RESULTS:2))
		;性癖自動取得チェック
		VARSET RESULTS
		SPLIT CSTR:(ボタン番号 - 50000):自動取得性癖_記録文字列, "___", RESULTS
		FOR 分割番号, 0, RESULT
			CALL 口上変数初期化
			VARSET 分割用文字列
			SIF STRFIND(RESULTS:分割番号, "+++") < 0
				CONTINUE
			SPLIT RESULTS:分割番号, "+++", 分割用文字列
			;分割用文字列:0に対象キャラ、:1に性癖名が入ってるはず
			CALLFORM 自動取得性癖メッセージ_%分割用文字列:1%(TOINT(分割用文字列:0))
			TRYCALLFORM KOJO_性癖開拓_%分割用文字列:1%_{NO:(TOINT(分割用文字列:0))}(TOINT(分割用文字列:0), 2)
		NEXT
		;性癖自動成長チェック
		VARSET RESULTS
		SPLIT CSTR:(ボタン番号 - 50000):自動成長性癖_記録文字列, "___", RESULTS
		FOR 分割番号, 0, RESULT
			CALL 口上変数初期化
			VARSET 分割用文字列
			SIF STRFIND(RESULTS:分割番号, "+++") < 0
				CONTINUE
			SPLIT RESULTS:分割番号, "+++", 分割用文字列
			;分割用文字列:0に対象キャラ、:1に性癖名、:2に上がる前のレベルが入ってるはず
			CALLFORM 性癖段階成長メッセージ_%分割用文字列:1%(TOINT(分割用文字列:0), TOINT(分割用文字列:2))
			TRYCALLFORM KOJO_性癖成長_%分割用文字列:1%_{NO:(TOINT(分割用文字列:0))}(TOINT(分割用文字列:0))
		NEXT
	CASE IS >= 40000
		HTML_PRINT CSTR:(ボタン番号 - 40000):夜這い報告_記録文字列
		PRINTL
		TRYCALLFORM KOJO_夜這い失敗時_{NO:(ボタン番号 - 40000)}(ボタン番号 - 40000, RCVAR:(ボタン番号 - 40000):夜這い相手保存)
	CASE IS >= 30000
		HTML_PRINT CSTR:(ボタン番号 - 30000):オナニー報告_記録文字列
		PRINTL
		TRYCALLFORM KOJO_一日終了時オナニー_{NO:(ボタン番号 - 30000)}(ボタン番号 - 30000)
	CASE IS >= 20000
		IF STRFIND(CSTR:(ボタン番号 - 20000):性癖開拓報告_記録文字列, "性癖開拓_固有条件") >= 0
			;固有条件時文章があるなら表示
			CALLFORM 性癖開拓_固有条件_%ERDNAME(性癖素質, 性癖素質:(ボタン番号 - 20000):性癖開発用フラグ変数)%_{NO:(ボタン番号 - 20000)}(ボタン番号 - 20000)
			CALL 口上変数表示(ボタン番号 - 20000, 0)
			CSTR:(ボタン番号 - 20000):性癖開拓報告_記録文字列 '= REPLACE(CSTR:(ボタン番号 - 20000):性癖開拓報告_記録文字列, "性癖開拓_固有条件", "")
		ENDIF
		HTML_PRINT CSTR:(ボタン番号 - 20000):性癖開拓報告_記録文字列
		PRINTL
		TRYCALLFORM KOJO_性癖開拓_%ERDNAME(性癖素質, 性癖素質:(ボタン番号 - 20000):性癖開発用フラグ変数)%_{NO:(ボタン番号 - 20000)}((ボタン番号 - 20000), RCVAR:(ボタン番号 - 20000):性癖開拓結果保存)
	CASE IS >= 10000
		TRYCALLFORM KOJO_欲求不満_野外オナニー_{NO:(ボタン番号 - 10000)}(ボタン番号 - 10000)
ENDSELECT
WAIT

@一日終了時レポート処理_オナニー・夜這い報告()
#DIM DYNAMIC 順番シャッフル用配列, キャラクタ数上限
#DIM シャッフル数
#DIM 結果保存
#DIM 口上あり
#DIMS 分割用文字列, 3
#DIM 分割番号
#DIM フォント色変更

;--------------------------------------------------------------------------------
;縁結びの一日進行をここで行う
;--------------------------------------------------------------------------------
CALL 縁結び進展処理()
RFLAG:オート縁結び中 = 1
CALL キャラ番号配列_初期化_シャッフル(順番シャッフル用配列, 0)
シャッフル数 = RESULT

CALL オート縁結び処理(順番シャッフル用配列)
;レポート強調用記憶：縁結び報告(＋オート縁結び成立)
SIF RESULT
	レポート強調フラグ:縁結び報告 = 1
RFLAG:オート縁結び中 = 0

RSTR:オナニー報告_記録文字列 = 
RSTR:夜這い報告_記録文字列 = 
RSTR:性癖開拓報告_記録文字列 = 

;シャッフル配列で処理
;口上ボタンの表示判定のために非表示フラグをセット
RFLAG:口上非表示フラグ = 1
FOR LOCAL, 0, シャッフル数
	;文字列リセット
	CSTR:(順番シャッフル用配列:LOCAL):性癖開拓報告_記録文字列 = 
	CSTR:(順番シャッフル用配列:LOCAL):オナニー報告_記録文字列 = 
	CSTR:(順番シャッフル用配列:LOCAL):夜這い報告_記録文字列 = 
	CSTR:(順番シャッフル用配列:LOCAL):夜這い口上渡し_記録文字列 = 
	CSTR:(順番シャッフル用配列:LOCAL):自動取得性癖_記録文字列 = 
	CSTR:(順番シャッフル用配列:LOCAL):自動成長性癖_記録文字列 = 
	口上あり = 0
	;滞在してないキャラはスルー
	IF CFLAG:(順番シャッフル用配列:LOCAL):滞在期間 < 0
		性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数 = 0
		CONTINUE
	ENDIF
	CALL JUEL_CHECK((順番シャッフル用配列:LOCAL))
	CALL DENIAL_CHECK((順番シャッフル用配列:LOCAL))

	;野外オナニー欲求不満
	IF 性癖素質:(順番シャッフル用配列:LOCAL):野外オナニー > 2 && TCVAR:(順番シャッフル用配列:LOCAL):野外オナニー_実行回数 < 3
		CALL 野外オナニー欲求不満処理(順番シャッフル用配列:LOCAL)
		RSTR:オナニー報告_記録文字列 += @"%RESULTS, 80, LEFT%"
		CALL 口上変数初期化
		;非表示状態で一旦口上を読んで、ちゃんと文字が入ってるなら表示ボタンを出す
		TRYCCALLFORM KOJO_欲求不満_野外オナニー_{NO:(順番シャッフル用配列:LOCAL)}((順番シャッフル用配列:LOCAL))
			IF 口上有無確認(0,1)
				RSTR:オナニー報告_記録文字列 += @"<button value='{10000 + (順番シャッフル用配列:LOCAL)}'>[口上を表示]</button>"
				レポート強調フラグ:オナニー報告 = 1
			ENDIF
		CATCH
		ENDCATCH
		RSTR:オナニー報告_記録文字列 += "<br>"
	ENDIF
	IF (順番シャッフル用配列:LOCAL) == FLAG:挿入睡眠相手記録
		;挿入睡眠相手の時は性欲チャレンジもオナニーも発生しない
	ELSEIF TCVAR:(順番シャッフル用配列:LOCAL):夜這い済み
		;夜這いされた側については性癖チャレンジもオナニーも発生しない
	ELSEIF 性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数
		;性癖開発チャレンジ
		;チャレンジ時はオナニー判定が出ない
		TRYCALLFORM 性癖開拓チャレンジ処理_%ERDNAME(性癖素質, 性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数)%((順番シャッフル用配列:LOCAL))
		RCVAR:(順番シャッフル用配列:LOCAL):性癖開拓結果保存 = RESULT
		SELECTCASE RCVAR:(順番シャッフル用配列:LOCAL):性癖開拓結果保存
			CASE -1
				;知識不足
				フォント色変更 = 1
				RSTR:性癖開拓報告_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
				RSTR:性癖開拓報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%はエロ本を理解出来なかったようだ。", 80, LEFT%"
				;レポート強調用記憶：性癖開拓報告(－性癖開拓不可)
				SIF レポート強調フラグ:性癖開拓報告 == 0
					レポート強調フラグ:性癖開拓報告 = -1
			CASE 0
				;失敗
				RSTR:性癖開拓報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%は%ERDNAME(性癖素質, 性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数)%性癖を取得しなかった……。", 80, LEFT%"
			CASE 1
				;成功
				フォント色変更 = 1
				RSTR:性癖開拓報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
				RSTR:性癖開拓報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%は%ERDNAME(性癖素質, 性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数)%性癖を取得した。", 80, LEFT%"
				;レポート強調用記憶：性癖開拓報告(＋性癖開拓成功)
				レポート強調フラグ:性癖開拓報告 = 1
		ENDSELECT
		CALL 口上変数初期化
		;非表示状態で一旦口上を読んで、ちゃんと文字が入ってるなら表示ボタンを出す。入ってないなら詳細表示
		TRYCCALLFORM KOJO_性癖開拓_%ERDNAME(性癖素質, 性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数)%_{NO:(順番シャッフル用配列:LOCAL)}((順番シャッフル用配列:LOCAL), RCVAR:(順番シャッフル用配列:LOCAL):性癖開拓結果保存)
			IF 口上有無確認(0,1)
				口上あり = 1
			ENDIF
		CATCH
		ENDCATCH
		IF 口上あり == 0
			TRYCCALLFORM 性癖開拓_固有条件_%ERDNAME(性癖素質, 性癖素質:(順番シャッフル用配列:LOCAL):性癖開発用フラグ変数)%_{NO:(順番シャッフル用配列:LOCAL)}(順番シャッフル用配列:LOCAL)
				IF 口上有無確認(0,1)
					口上あり = 1
				ENDIF
			CATCH
			ENDCATCH
		ENDIF
		IF 口上あり
			RSTR:性癖開拓報告_記録文字列 += @"<button value='{20000 + (順番シャッフル用配列:LOCAL)}'>[口上を表示]</button>"
		ELSE
			RSTR:性癖開拓報告_記録文字列 += @"　<button value='{20000 + (順番シャッフル用配列:LOCAL)}'>[詳細表示]</button>"
		ENDIF
		IF フォント色変更
			RSTR:性癖開拓報告_記録文字列 += "</font>"
			フォント色変更 = 0
		ENDIF
		RSTR:性癖開拓報告_記録文字列 += "<br>"
	ELSE
		;一日終了時
		;夜這い判定、性癖取得や成長のメッセージをキャンセルするために一旦SKIPDISP
		SKIPDISP 1
		;この文字列に結果を保存して後で格納する
		RSTR:コマンド結果受渡し文字列 = 
		CALL 一日終了時夜這い判定((順番シャッフル用配列:LOCAL))
		結果保存 = RESULT
		SKIPDISP 0
		IF 結果保存 < 0
			;夜這い失敗時
			RSTR:夜這い報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%は夜這い出来なかったようだ…", 80, LEFT%"
			RCVAR:(順番シャッフル用配列:LOCAL):夜這い相手保存 = ABS(結果保存 + 1)
			TRYCCALLFORM KOJO_夜這い失敗時_{NO:(順番シャッフル用配列:LOCAL)}(順番シャッフル用配列:LOCAL, RCVAR:(順番シャッフル用配列:LOCAL):夜這い相手保存)
				IF 口上有無確認(0,1)
					RSTR:夜這い報告_記録文字列 += @"<button value='{40000 + (順番シャッフル用配列:LOCAL)}'>[口上を表示]</button>"
					レポート強調フラグ:夜這い報告 = 1
				ELSE
					RSTR:夜這い報告_記録文字列 += @"　<button value='{40000 + (順番シャッフル用配列:LOCAL)}'>[詳細表示]</button>"
				ENDIF
			CATCH
				RSTR:夜這い報告_記録文字列 += @"　<button value='{40000 + (順番シャッフル用配列:LOCAL)}'>[詳細表示]</button>"
			ENDCATCH
			RSTR:夜這い報告_記録文字列 += "<br>"
		ELSEIF 結果保存 >= 100
			;夜這い成功時
			フォント色変更 = 1
			RSTR:夜這い報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
			RSTR:夜這い報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%は%CALLNAME:(結果保存 - 100)%と熱い夜を過ごした…", 80, LEFT%"
			CSTR:(順番シャッフル用配列:LOCAL):夜這い口上渡し_記録文字列 = %RSTR:コマンド結果受渡し文字列%
			;性癖自動取得チェック
			VARSET RESULTS
			SPLIT CSTR:(順番シャッフル用配列:LOCAL):自動取得性癖_記録文字列, "___", RESULTS
			FOR 分割番号, 0, RESULT
				CALL 口上変数初期化
				VARSET 分割用文字列
				SPLIT RESULTS:分割番号, "+++", 分割用文字列
				;分割用文字列:0に対象キャラ、:1に性癖名が入ってるはず
				TRYCCALLFORM KOJO_性癖開拓_%分割用文字列:1%_{NO:(TOINT(分割用文字列:0))}(TOINT(分割用文字列:0), 2)
					IF 口上有無確認(0,1)
						口上あり = 1
					ENDIF
				CATCH
				ENDCATCH
			NEXT
			;性癖自動成長チェック
			VARSET RESULTS
			SPLIT CSTR:(順番シャッフル用配列:LOCAL):自動成長性癖_記録文字列, "___", RESULTS
			FOR 分割番号, 0, RESULT
				CALL 口上変数初期化
				VARSET 分割用文字列
				SPLIT RESULTS:分割番号, "+++", 分割用文字列
				;分割用文字列:0に対象キャラ、:1に性癖名、:2に上がる前のレベルが入ってるはず
				TRYCCALLFORM KOJO_性癖成長_%分割用文字列:1%_{NO:(TOINT(分割用文字列:0))}(TOINT(分割用文字列:0))
					IF 口上有無確認(0,1)
						口上あり = 1
					ENDIF
				CATCH
				ENDCATCH
			NEXT
			;夜這い実行時口上チェック
			CALL 口上変数初期化
			VARSET RESULTS
			SPLIT CSTR:(順番シャッフル用配列:LOCAL):夜這い口上渡し_記録文字列, "+++", RESULTS
			RCVAR:(順番シャッフル用配列:LOCAL):夜這い相手保存 = TOINT(RESULTS:0)
			TRYCCALLFORM KOJO_夜這い実行時_{NO:(順番シャッフル用配列:LOCAL)}(順番シャッフル用配列:LOCAL, RCVAR:(順番シャッフル用配列:LOCAL):夜這い相手保存, TOINT(RESULTS:1), TOINT(RESULTS:2))
				IF 口上有無確認(0,1)
					口上あり = 1
				ENDIF
			CATCH
			ENDCATCH
			IF 口上あり
				RSTR:夜這い報告_記録文字列 += @"<button value='{50000 + (順番シャッフル用配列:LOCAL)}'>[口上を表示]</button>"
			ELSE
				RSTR:夜這い報告_記録文字列 += @"　<button value='{50000 + (順番シャッフル用配列:LOCAL)}'>[詳細表示]</button>"
			ENDIF
			IF フォント色変更
				RSTR:夜這い報告_記録文字列 += "</font>"
				フォント色変更 = 0
			ENDIF
			RSTR:夜這い報告_記録文字列 += "<br>"
			;###
			;レポート強調用記憶：夜這い報告(＋夜這い成功)
			レポート強調フラグ:夜這い報告 = 1
		ELSEIF 結果保存 == 0
			;自慰判定
			CALL 一日終了時オナニー判定((順番シャッフル用配列:LOCAL))
			RCVAR:(順番シャッフル用配列:LOCAL):オナニー結果保存 = RESULT
			;オプションによる制御
			IF 一日終了時オナニーOPTION:(TALENT:(順番シャッフル用配列:LOCAL):性別) == 0
				SELECTCASE RCVAR:(順番シャッフル用配列:LOCAL):オナニー結果保存
					CASE -1
						RSTR:オナニー報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%はただ悶々としている…", 80, LEFT%"
					CASE 1
						RSTR:オナニー報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%はオナニーで性欲を発散している", 80, LEFT%"
					CASE 2
						RSTR:オナニー報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%は自らの母乳を絞っている", 80, LEFT%"
					CASE 3
						RSTR:オナニー報告_記録文字列 += @"%@"%CALLNAME:(順番シャッフル用配列:LOCAL)%は覚えたばかりの自慰の虜になっている", 80, LEFT%"
				ENDSELECT
				IF RCVAR:(順番シャッフル用配列:LOCAL):オナニー結果保存
					CALL 口上変数初期化
					TRYCCALLFORM KOJO_一日終了時オナニー_{NO:(順番シャッフル用配列:LOCAL)}((順番シャッフル用配列:LOCAL))
						IF 口上有無確認(0,1)
							RSTR:オナニー報告_記録文字列 += @"<button value='{30000 + (順番シャッフル用配列:LOCAL)}'>[口上を表示]</button>"
						ELSE
							RSTR:オナニー報告_記録文字列 += @"　<button value='{30000 + (順番シャッフル用配列:LOCAL)}'>[詳細表示]</button>"
						ENDIF
					CATCH
						RSTR:オナニー報告_記録文字列 += @"　<button value='{30000 + (順番シャッフル用配列:LOCAL)}'>[詳細表示]</button>"
					ENDCATCH
					RSTR:オナニー報告_記録文字列 += "<br>"
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	CALL EXP変動記録処理((順番シャッフル用配列:LOCAL))
NEXT
RFLAG:口上非表示フラグ = 0



@一日終了時レポート表示_妊娠報告(ボタン番号)
#DIM ボタン番号
DRAWLINE

PRINTL
SELECTCASE ボタン番号
	CASE IS >= 30000
		CALL BIRTHING(ボタン番号 - 30000)
	CASE IS >= 20000
		;妊娠知覚イベント
		CALL DETECT_PREGNANCY(ボタン番号 - 20000)
		HTML_PRINT CSTR:(ボタン番号 - 20000):妊娠報告_記録文字列
	CASE IS >= 10000
		VARSET RESULTS
		SPLIT CSTR:(ボタン番号 - 10000):妊娠報告_記録文字列, "_受精アニメ_", RESULTS
		HTML_PRINT RESULTS:0
		IF RESULTS:1 != ""
			SIF FLAG_アニメーション
				CALL 受精アニメ(0)
			HTML_PRINT RESULTS:1
		ENDIF
		IF RESULTS:2 != ""
			SIF FLAG_アニメーション
				CALL 受精アニメ(1)
			HTML_PRINT RESULTS:2
		ENDIF
ENDSELECT
WAIT

@一日終了時レポート処理_妊娠報告()
#DIM 口上ありフラグ
#DIM フォント色変更

;口上ボタンの表示判定のために非表示フラグをセット
RFLAG:口上非表示フラグ = 1

;妊娠判定
FOR LOCAL,0,CHARANUM
	口上ありフラグ = 0
	CSTR:LOCAL:妊娠報告_記録文字列 = 
	CALL PREGNANCY_CHECK(LOCAL)
	SELECTCASE RESULT
		CASE -3
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%は既に妊娠している。", 80, LEFT%"
		CASE -2
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%は避妊薬を使っている。", 80, LEFT%"
		CASE -1
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%は妊娠しなかったようだ……。", 80, LEFT%"
		CASE 1
			フォント色変更 = 1
			RSTR:妊娠報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%は妊娠したようだ。", 80, LEFT%"
			;レポート強調用記憶：妊娠報告(＋妊娠)
			レポート強調フラグ:妊娠報告 = 1

	ENDSELECT
	IF RESULT
		RSTR:妊娠報告_記録文字列 += @"　<button value='{10000 + LOCAL}'>[詳細表示]</button>"
		IF フォント色変更
			RSTR:妊娠報告_記録文字列 += "</font>"
			フォント色変更 = 0
		ENDIF
		RSTR:妊娠報告_記録文字列 +=  "<br>"
	ENDIF
	CALL PREGNANCY_COUNTUP(LOCAL)
	SELECTCASE RESULT
		CASE -1
			RSTR:妊娠報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%の授乳期間が終了した。", 80, LEFT%"
			RSTR:妊娠報告_記録文字列 += @"　<button value='{20000 + LOCAL}'>[詳細表示]</button></font><br>"
			;レポート強調用記憶：妊娠報告(＋妊娠状態進行)
			レポート強調フラグ:妊娠報告 = 1
		CASE 1
			RSTR:妊娠報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%の腹部が大きくなった。", 80, LEFT%"
			;口上の判定のために同じ分岐をここでも見る
			IF PREGNANCY_SIZE(LOCAL) >= 2 && CFLAG:LOCAL:妊娠発覚 == 0 && CFLAG:LOCAL:滞在期間 > 0
				IF 知識素質:LOCAL:性知識 == -2
					TRYCCALLFORM KOJO_無知_妊娠発覚_1_{NO:LOCAL}(LOCAL)
						SIF 口上有無確認(0,1)
							口上ありフラグ = 1
					CATCH
						TRYCCALLFORM KOJO_無知_妊娠発覚_2_{NO:LOCAL}(LOCAL)
							SIF 口上有無確認(0,1)
								口上ありフラグ = 1
						CATCH
							TRYCCALLFORM KOJO_無知_妊娠発覚_3_{NO:LOCAL}(LOCAL)
							SIF 口上有無確認(0,1)
								口上ありフラグ = 1
							CATCH
							ENDCATCH
						ENDCATCH
					ENDCATCH
				ELSEIF TALENT:LOCAL:恋慕
					TRYCCALLFORM KOJO_恋慕_妊娠発覚_{NO:LOCAL}(LOCAL)
						SIF 口上有無確認(0,1)
							口上ありフラグ = 1
					CATCH
					ENDCATCH
				ELSE
					TRYCCALLFORM KOJO_その他_妊娠発覚_{NO:LOCAL}(LOCAL)
						SIF 口上有無確認(0,1)
							口上ありフラグ = 1
					CATCH
					ENDCATCH
				ENDIF
			ENDIF
			IF 口上ありフラグ
				RSTR:妊娠報告_記録文字列 += @"<button value='{20000 + LOCAL}'>[口上の表示]</button>"
			ELSE
				RSTR:妊娠報告_記録文字列 += @"　<button value='{20000 + LOCAL}'>[詳細表示]</button>"
			ENDIF
			RSTR:妊娠報告_記録文字列 += "</font>"
			RSTR:妊娠報告_記録文字列 += "<br>"
			;レポート強調用記憶：妊娠報告(＋妊娠状態進行)
			レポート強調フラグ:妊娠報告 = 1
		CASE 2
			RSTR:妊娠報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
			RSTR:妊娠報告_記録文字列 += @"%@"%CALLNAME:LOCAL%は子供を出産したようだ", 80, LEFT%"
			IF CFLAG:LOCAL:滞在期間 == -1 && LOCAL != MASTER
				TRYCCALLFORM KOJO_出産_リゾート外_{NO:LOCAL}(LOCAL)
					SIF 口上有無確認(0,1)
						口上ありフラグ = 1
				CATCH
				ENDCATCH
			ELSE
				TRYCCALLFORM KOJO_出産_リゾート内_{NO:LOCAL}(LOCAL)
					SIF 口上有無確認(0,1)
						口上ありフラグ = 1
				CATCH
				ENDCATCH
			ENDIF
			IF 口上ありフラグ
				RSTR:妊娠報告_記録文字列 += @"<button value='{30000 + LOCAL}'>[口上の表示]</button>"
			ELSE
				RSTR:妊娠報告_記録文字列 += @"　<button value='{30000 + LOCAL}'>[詳細表示]</button>"
			ENDIF
			RSTR:妊娠報告_記録文字列 += "</font>"
			RSTR:妊娠報告_記録文字列 += "<br>"
			;レポート強調用記憶：妊娠報告(＋出産)
			レポート強調フラグ:妊娠報告 = 1
	ENDSELECT
NEXT

RFLAG:口上非表示フラグ = 0

@一日終了時レポート表示_従業員業務報告(ボタン番号)
#DIM ボタン番号
DRAWLINE

PRINTL 
HTML_PRINT 業務別報告文字列:(ボタン番号 - 100)

WAIT

@一日終了時レポート処理_従業員業務報告()
#DIM 仕事変数
#DIM 業務量
#DIM 撃退値
#DIM 雑務ゲージ減少量
#DIM 売上分ルピ
#DIM 人気度変動一時保存
#DIM 最大レベル
#DIMS DYNAMIC 学校利用者配列, 2000
#DIM DYNAMIC LCOUNT
#DIM 口上番号
#DIM 料理人レベル退避
#DIM フォント色変更, 13
#DIM 固有業務人数記録

;記録用文字列リセット
RSTR:従業員業務報告_記録文字列 = 
VARSET 業務別報告文字列
VARSET 港商店常駐_収支報告文字列
;--------------------------------------------------------------------------------
;従業員の仕事処理
;--------------------------------------------------------------------------------
VARSET LOCALS
雑務ゲージ減少量 = 0
売上分ルピ = 0
最大レベル = 最大レベル算出()
LOCAL:10 = 0
人気度変動一時保存 = 0
撃退値 = 0
VARSET フォント色変更
;表示用に一旦人数再セット
VARSET 仕事人数表示用
CALL 口上変数初期化

;表示順の関係でまず学校利用者
IF 学校利用者:0 != ""
	業務別報告文字列:7 += "■本日の学校利用状況<br>"
	LOCAL:2 = 0
	FOR LOCAL, 1, 100
		SIF ERDNAME(知識素質, LOCAL) == ""
			BREAK
		IF 学校利用者:(LOCAL + 100) != ""
			VARSET 学校利用者配列
			業務別報告文字列:7 += @"　　%ERDNAME(知識素質, LOCAL), 9, LEFT%の教師："
			SPLIT 学校利用者:(LOCAL + 100), "_", 学校利用者配列
			LOCAL:1 = RESULT
			FOR LCOUNT, 0, LOCAL:1 - 1
				SIF ISNUMERIC(学校利用者配列:LCOUNT) == 0
					CONTINUE
				業務別報告文字列:7 += @"%CALLNAME:TOINT(学校利用者配列:LCOUNT)%"
				SIF LCOUNT + 1 < LOCAL:1 - 1
					業務別報告文字列:7 += "、"
			NEXT
			業務別報告文字列:7 += "<br>"
			LOCAL:2 = 1
		ENDIF
	NEXT
	SIF LOCAL:2
		業務別報告文字列:7 += "<br>"
	FOR LOCAL, 1, 100
		SIF ERDNAME(知識素質, LOCAL) == ""
			BREAK
		IF 学校利用者:LOCAL != ""
			VARSET 学校利用者配列
			業務別報告文字列:7 += @"　　%ERDNAME(知識素質, LOCAL), 9, LEFT%の生徒："
			SPLIT 学校利用者:LOCAL, "_", 学校利用者配列
			LOCAL:1 = RESULT
			FOR LCOUNT, 0, LOCAL:1 - 1
				SIF ISNUMERIC(学校利用者配列:LCOUNT) == 0
					CONTINUE
				業務別報告文字列:7 += @"%CALLNAME:TOINT(学校利用者配列:LCOUNT)%"
				SIF LCOUNT + 1 < LOCAL:1 - 1
					業務別報告文字列:7 += "、"
			NEXT
			業務別報告文字列:7 += "<br>"
			LOCAL:2 = 1
		ENDIF
	NEXT
	業務別報告文字列:7 += "<br><br>"
ENDIF
;教師の方でも一覧を表示するのでそのままコピー
業務別報告文字列:8 = %業務別報告文字列:7%

;各業務の詳細、処理中はPRINTしないのでSKIPDISP
SKIPDISP 1
固有業務人数記録 = 0
FOR LOCAL, 0, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0 && LOCAL != MASTER
		CONTINUE
	SIF TCVAR:LOCAL:休暇中
		CONTINUE
	仕事変数 = 現在仕事:LOCAL:0
	SELECTCASE 仕事変数
		CASE 1
			仕事人数表示用:雑務担当人数 ++
			業務量 = (仕事レベル:LOCAL:仕事変数 + 1) * (仕事レベル:LOCAL:仕事変数 + 1) * RAND(95, 106)
			業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%が{業務量, 5}の雑務をこなしました。<br>"
			雑務ゲージ減少量 += 業務量
			;経験値取得とレベル上昇
			仕事経験値:LOCAL:仕事変数 += 1
			IF 仕事レベル:LOCAL:仕事変数 < 5
				IF 仕事経験値:LOCAL:仕事変数 >= 仕事レベル経験値テーブル:(仕事レベル:LOCAL:仕事変数 + 1)
					仕事レベル:LOCAL:仕事変数 += 1
					業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の雑務レベルがLv{仕事レベル:LOCAL:仕事変数}に上昇した！<br>"
					IF 仕事レベル:LOCAL:仕事変数 >= 5
						;仕事レベルは減少しないのでこれでいいはず
						CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, @"仕事：雑務レベルがLv5になった","リゾート")
					ENDIF
					;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：雑務)
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:1 = 1
				ENDIF
			ENDIF
		CASE 2
			仕事人数表示用:常駐業務担当人数 ++
			LOCALS = %MAP_GET("常駐業務名", REPLACE(GETPLACENAME(現在仕事:LOCAL:1 / 10000, 現在仕事:LOCAL:1 % 10000), "『.*』", ""))%
			
			RESULTS = 
			CALLFORM 常駐業務効果_%LOCALS%(LOCAL, "ルピ補正")
			SIF 現在仕事:LOCAL:1 / 10000 == 1 && 現在仕事:LOCAL:1 % 10000 >= 3 && 現在仕事:LOCAL:1 % 10000 <= 8
				港商店常駐_収支報告文字列:(現在仕事:LOCAL:1 % 10000) += @"%RESULTS%"
			売上分ルピ += RESULT
			CALLFORM 常駐業務効果_%LOCALS%(LOCAL, "経験値取得")
			IF RESULT
				;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：常駐業務)
				レポート強調フラグ:従業員業務報告 = 1
				フォント色変更:2 = 1
			ENDIF
		CASE 3
			仕事人数表示用:秘書担当人数 ++
			業務量 = (仕事レベル:LOCAL:仕事変数 + 1) * (仕事レベル:LOCAL:仕事変数 + 1) * RAND(190, 211)
			NOWEX:LOCAL:信頼度上昇格納 += 業務量
			;干渉効果適用のために関数呼び
			CALL FAVOR_CALC(LOCAL, 1)
			業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の信頼度が{NOWEX:LOCAL:信頼度上昇格納, 5}上昇しました。"
			;変なところで誤爆しないよう変数リセット
			NOWEX:LOCAL:友情度上昇格納 = 0
			NOWEX:LOCAL:信頼度上昇格納 = 0
			NOWEX:LOCAL:恋心度上昇格納 = 0
			NOWEX:LOCAL:性愛度上昇格納 = 0

			IF CFLAG:LOCAL:信頼度 >= 100500
				業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の信頼度は既に最大値です。<br>"
			ELSEIF 干渉強化変数:LOCAL:干渉種類 == 2
				業務別報告文字列:仕事変数 += @"<font color='#%カラーパレット_HTML("黄")%'>　干渉効果発動中！</font>"
			ENDIF
			業務別報告文字列:仕事変数 += "<br>"
			;経験値取得とレベル上昇
			仕事経験値:LOCAL:仕事変数 += 1
			IF 仕事レベル:LOCAL:仕事変数 < 5
				IF 仕事経験値:LOCAL:仕事変数 >= 仕事レベル経験値テーブル:(仕事レベル:LOCAL:仕事変数 + 1)
					仕事レベル:LOCAL:仕事変数 += 1
					業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の秘書レベルがLv{仕事レベル:LOCAL:仕事変数}に上昇した！<br>"
					IF 仕事レベル:LOCAL:仕事変数 >= 5
						;仕事レベルは減少しないのでこれでいいはず
						CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, @"仕事：秘書レベルがLv5になった","リゾート")
					ENDIF
					;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：秘書)
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:3 = 1
				ENDIF
			ENDIF
		CASE 4
			仕事人数表示用:宣伝担当人数 ++
			CALL 人気上昇処理((仕事レベル:LOCAL:仕事変数 + 1) * (仕事レベル:LOCAL:仕事変数 + 1) * 1000)
			人気度変動一時保存 += RESULT
			業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の宣伝でリゾートの人気度が%DECIMAL_STRING(RESULT, 3)%上昇しました。<br>"
			;経験値取得とレベル上昇
			仕事経験値:LOCAL:仕事変数 += 1
			IF 仕事レベル:LOCAL:仕事変数 < 5
				IF 仕事経験値:LOCAL:4 >= 仕事レベル経験値テーブル:(仕事レベル:LOCAL:仕事変数 + 1)
					仕事レベル:LOCAL:仕事変数 += 1
					業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の宣伝レベルがLv{仕事レベル:LOCAL:仕事変数}に上昇した！<br>"
					IF 仕事レベル:LOCAL:仕事変数 >= 5
						;仕事レベルは減少しないのでこれでいいはず
						CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, @"仕事：宣伝レベルがLv5になった","リゾート")
					ENDIF
					;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：宣伝)
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:4 = 1
				ENDIF
			ENDIF
		CASE 5
			仕事人数表示用:訓練担当人数 ++
			TCVAR:LOCAL:摂取カロリー -= 500
			IF 基礎BATTLE_STATE:LOCAL:Lv < 最大レベル
				IF 基礎BATTLE_STATE:LOCAL:蓄積EXP > 戦闘経験値テーブル:(MAX(最大レベル - 2, 0))
					;最大Lv到達EXPより現在のEXPが高いならスキップ
					業務別報告文字列:仕事変数 += @"今のリゾートの環境では、訓練しても%CALLNAME:LOCAL, 18, LEFT%はEXPを入手出来ないようだ……。<br>"
					;レポート強調用記憶：従業員業務報告(－訓練：レベル上限)
					SIF レポート強調フラグ:従業員業務報告 == 0
						レポート強調フラグ:従業員業務報告 = -1
					フォント色変更:5 = -1
					CONTINUE
				ENDIF
				LOCAL:1 = MAX(戦闘経験値テーブル:(MAX(最大レベル - 2, 0)) / 15, 500)
				;EXPは最大レベル到達EXPより上には行かない
				業務量 = MIN(LOCAL:1, 戦闘経験値テーブル:(MAX(最大レベル - 2, 0)) - 基礎BATTLE_STATE:LOCAL:蓄積EXP)
				業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%は訓練を通して{業務量, 6}EXPを入手した。<br>"
				基礎BATTLE_STATE:LOCAL:蓄積EXP = 基礎BATTLE_STATE:LOCAL:蓄積EXP + 業務量
				CALL 口上変数初期化
				CALL レベルアップ処理(LOCAL)
				;レベルアップ時の簡易お知らせ
				IF RESULT > 0
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:5 = 1
				ENDIF
				;KSTR系列に入ってるものを変換、特殊な動作は入れてないはずなので特に処理は通さない
				FOR 口上番号,0,1000
					SIF !口上有無確認(口上番号,1)
						BREAK
					IF !(WSTR:口上番号 == "")
						業務別報告文字列:仕事変数 += @"%WSTR:口上番号%<br>"
					ELSEIF !(KSTR:口上番号 == "")
						業務別報告文字列:仕事変数 += @"%KSTR:口上番号%<br>"
					ELSEIF !(NSTR:口上番号 == "")
						業務別報告文字列:仕事変数 += @"%NSTR:口上番号%"
					ELSEIF !(WNSTR:口上番号 == "")
						業務別報告文字列:仕事変数 += @"%WNSTR:口上番号%"
					ENDIF
				NEXT
				CALL 口上変数初期化
			ELSE
				;既に最大レベル到達済のキャラ
				業務別報告文字列:仕事変数 += @"今のリゾートの環境では、訓練しても%CALLNAME:LOCAL, 18, LEFT%はEXPを入手出来ないようだ……。<br>"
				;レポート強調用記憶：従業員業務報告(－訓練：レベル上限)
				SIF レポート強調フラグ:従業員業務報告 == 0
					レポート強調フラグ:従業員業務報告 = -1
				SIF フォント色変更:5 == 0
					フォント色変更:5 = -1
			ENDIF
		CASE 6
			仕事人数表示用:自動探索担当人数 ++
			TCVAR:LOCAL:摂取カロリー -= 400
		CASE 7
			仕事人数表示用:知識習得担当人数 ++
			IF 知識素質:LOCAL:(現在仕事:LOCAL:1) > 1
				; 仕事による知識習得はLv2まで。以降の学習は無意味になるので仕事内容から外す
				業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の%ERDNAME(知識素質, 現在仕事:LOCAL:1)%はこれ以上学校で学ぶことは出来ないようだ。知識習得業務を解除します。<br>"
				現在仕事:LOCAL:0 = 0
				現在仕事:LOCAL:1 = 0
				;レポート強調用記憶：従業員業務報告(－知識習得：レベル上限)
				SIF レポート強調フラグ:従業員業務報告 == 0
					レポート強調フラグ:従業員業務報告 = -1
				フォント色変更:7 = -1
			ENDIF
		CASE 8
			仕事人数表示用:教師担当人数 ++
			;経験値取得とレベル上昇
			仕事経験値:LOCAL:仕事変数 += 1
			IF 仕事レベル:LOCAL:仕事変数 < 5
				IF 仕事経験値:LOCAL:仕事変数 >= 仕事レベル経験値テーブル:(仕事レベル:LOCAL:仕事変数 + 1)
					仕事レベル:LOCAL:仕事変数 += 1
					業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の教師レベルがLv{仕事レベル:LOCAL:仕事変数}に上昇した！<br>"
					IF 仕事レベル:LOCAL:仕事変数 >= 5
						;仕事レベルは減少しないのでこれでいいはず
						CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, @"仕事：教師レベルがLv5になった","リゾート")
					ENDIF
					;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：教師)
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:8 = 1
				ENDIF
			ENDIF
		CASE 9
			仕事人数表示用:調理師担当人数 ++
			IF 知識素質:LOCAL:料理人 >= 0
				業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%は%TEXTR("頑張って料理を作っている。/忙しそうに料理を作っている。/美味しそうな料理を作っている。")%<br>"
			ELSE
				業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%は%TEXTR("フライパンで/鍋で/オーブンで")%奇妙な物体を錬成している……。<br>"
			ENDIF
			;経験値取得とレベル上昇
			IF 知識素質:LOCAL:料理人 >= 0
				知識素質:LOCAL:料理人経験値 += LIMIT(モブ観光客数 / 20, 1, 10)
				IF 知識素質:LOCAL:料理人 < 2
					料理人レベル退避 = 知識素質:LOCAL:料理人
					SKIPDISP 1
					CALL 知識系素質の取得(LOCAL)
					SKIPDISP 0
					IF 知識素質:LOCAL:料理人 > 料理人レベル退避
						;料理人レベル上昇時
						業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の料理人レベルがLv{知識素質:LOCAL:料理人}に上昇した！<br>"
						;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：料理人)
						レポート強調フラグ:従業員業務報告 = 1
						フォント色変更:9 = 1
					ENDIF
				ENDIF
				料理ZP補正保存 += (知識素質:LOCAL:料理人 + 1) * 5
			ENDIF
		CASE 10
			仕事人数表示用:給仕担当人数 ++
			業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%は%TEXTR("頑張って給仕をしている。/忙しそうに給仕をしている。/愛想よく給仕をしている。")%<br>"
			;経験値取得とレベル上昇
			仕事経験値:LOCAL:仕事変数 += 1
			IF 仕事レベル:LOCAL:仕事変数 < 5
				IF 仕事経験値:LOCAL:仕事変数 >= 仕事レベル経験値テーブル:(仕事レベル:LOCAL:仕事変数 + 1)
					仕事レベル:LOCAL:仕事変数 += 1
					業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の給仕レベルがLv{仕事レベル:LOCAL:仕事変数}に上昇した！<br>"
					IF 仕事レベル:LOCAL:仕事変数 >= 5
						;仕事レベルは減少しないのでこれでいいはず
						CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, @"仕事：給仕レベルがLv5になった","リゾート")
					ENDIF
					;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：給仕)
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:10 = 1
				ENDIF
			ENDIF
			料理ルピ補正保存 += (仕事レベル:LOCAL:仕事変数 + 1) * 5
		CASE 11
			仕事人数表示用:高速船員担当人数 ++
			業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%は%高速船記録:派遣先登録%への航路を往復している。<br>"
		CASE 12
			仕事人数表示用:警備員担当人数 ++
			業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%は%TEXTR("真面目に警備をしている/こまめに見回りをしている/不審者への対応をしている")%。<br>"
			撃退値 += 仕事レベル:LOCAL:仕事変数 + 1
			;経験値取得とレベル上昇
			仕事経験値:LOCAL:仕事変数 += 1
			IF 仕事レベル:LOCAL:仕事変数 < 5
				IF 仕事経験値:LOCAL:仕事変数 >= 仕事レベル経験値テーブル:(仕事レベル:LOCAL:仕事変数 + 1)
					仕事レベル:LOCAL:仕事変数 += 1
					業務別報告文字列:仕事変数 += @"%CALLNAME:LOCAL, 18, LEFT%の警備員レベルがLv{仕事レベル:LOCAL:仕事変数}に上昇した！<br>"
					IF 仕事レベル:LOCAL:仕事変数 >= 5
						;仕事レベルは減少しないのでこれでいいはず
						CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, @"仕事：警備員レベルがLv5になった","リゾート")
					ENDIF
					;レポート強調用記憶：従業員業務報告(＋仕事レベル上昇：警備員)
					レポート強調フラグ:従業員業務報告 = 1
					フォント色変更:12 = 1
				ENDIF
			ENDIF
		CASE IS >= 100000
			CALLFORM 固有仕事効果_一日終了時_{NO:LOCAL}_{仕事変数 - 100000}(LOCAL)
			固有業務人数記録 ++
	ENDSELECT
NEXT
SKIPDISP 0

;雑務ゲージ減少の適用
FOR LOCAL, 1, マップ部屋番号上限
	IF 存在しない移動先(0, LOCAL) == 0
		雑務ゲージ:LOCAL = MAX(雑務ゲージ:LOCAL - 雑務ゲージ減少量, 0)
	ENDIF
NEXT

MONEY += 売上分ルピ

;レポート簡易表示用
RSTR:従業員業務報告_記録文字列 += "■従業員業務報告レポート<br>"
RSTR:従業員業務報告_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
IF 仕事人数表示用:雑務担当人数 > 0
	SIF フォント色変更:1 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:雑務担当人数}名の従業員の働きにより、各場所の雑務ゲージが{雑務ゲージ減少量}減少しました。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='101'>[詳細を表示]</button>"
	SIF フォント色変更:1
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:常駐業務担当人数 > 0
	SIF フォント色変更:2 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:常駐業務担当人数}名の従業員の働きにより、%NUM_FORMAT(売上分ルピ)%ルピの利益が上がりました。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='102'>[詳細を表示]</button>"
	SIF フォント色変更:2
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:秘書担当人数 > 0
	SIF フォント色変更:3 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:秘書担当人数}名の従業員は、秘書業務を通じて%CALLNAME:PLAYER%への信頼を高めました。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='103'>[詳細を表示]</button>"
	SIF フォント色変更:3
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:宣伝担当人数 > 0
	SIF フォント色変更:4 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:宣伝担当人数}名の従業員は、宣伝業務を通じてリゾートの人気度を%DECIMAL_STRING(人気度変動一時保存, 3)%高めました。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='104'>[詳細を表示]</button>"
	SIF フォント色変更:4
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:訓練担当人数 > 0
	SIF フォント色変更:5 < 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
	SIF フォント色変更:5 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:訓練担当人数}名の従業員は、訓練業務を通じて経験値を得た。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='105'>[詳細を表示]</button>"
	SIF フォント色変更:5
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:自動探索担当人数 > 0
	SIF フォント色変更:6 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:自動探索担当人数}名の従業員は、各ダンジョンを探索した。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='106'>[詳細を表示]</button>"
	SIF フォント色変更:6
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
	;自動探索の実処理
	CALL ダンジョン自動探索処理()
	業務別報告文字列:6 += @"%RESULTS%"
ENDIF
IF 仕事人数表示用:知識習得担当人数 > 0
	SIF フォント色変更:7 < 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:知識習得担当人数}名の従業員は、頑張って学習に励んでいる。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='107'>[詳細を表示]</button>"
	SIF フォント色変更:7
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:教師担当人数 > 0
	SIF フォント色変更:8 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:教師担当人数}名の従業員は、教師として講義を行っている。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='108'>[詳細を表示]</button>"
	SIF フォント色変更:8
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:調理師担当人数 > 0
	SIF フォント色変更:9 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:調理師担当人数}名の従業員は、頑張って料理を作っている。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='109'>[詳細を表示]</button>"
	SIF フォント色変更:9
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:給仕担当人数 > 0
	SIF フォント色変更:10 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:給仕担当人数}名の従業員は、愛想よく給仕を行っている。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='110'>[詳細を表示]</button>"
	SIF フォント色変更:10
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:高速船員担当人数 > 0
	SIF フォント色変更:11 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	RSTR:従業員業務報告_記録文字列 += @"%RSTR:高速船初派遣報告_記録文字列%"
	RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:高速船員担当人数}名の従業員は、%高速船記録:派遣先登録%への航路を往復している。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='111'>[詳細を表示]</button>"
	SIF フォント色変更:11
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 仕事人数表示用:警備員担当人数 > 0
	CALL 警備員イベント解消判定(撃退値)
	SIF RESULT == 1
		フォント色変更:12 = 1
	SIF フォント色変更:12 > 0
		RSTR:従業員業務報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
	SELECTCASE RESULT
		CASE 0
			RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:警備員担当人数}名の従業員は、リゾートの周辺を警備している。", 80, LEFT%"
		CASE 1
			RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:警備員担当人数}名の従業員は、不審者の撃退に成功した！", 80, LEFT%"
		CASE -1
			RSTR:従業員業務報告_記録文字列 += @"%@"・{仕事人数表示用:警備員担当人数}名の従業員は、不審者の対応に追われている。", 80, LEFT%"
	ENDSELECT
	RSTR:従業員業務報告_記録文字列 += "<button value='112'>[詳細を表示]</button>"
	SIF フォント色変更:12
		RSTR:従業員業務報告_記録文字列 += "</font>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
IF 業務別報告文字列:49 != ""
	RSTR:従業員業務報告_記録文字列 += @"%@"・{固有業務人数記録}名の従業員は、特技を活かした業務を行っている。", 80, LEFT%"
	RSTR:従業員業務報告_記録文字列 += "<button value='149'>[詳細を表示]</button>"
	RSTR:従業員業務報告_記録文字列 += "<br><br>"
ENDIF
;人数再セット
VARSET 仕事人数表示用
LOCAL:1 = 1
仕事人数表示用:(現在仕事:0:0) += 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	SIF 現在仕事:LOCAL:0 < 0
		CONTINUE
	SIF 現在仕事:LOCAL:0 >= 100000
		CONTINUE
	仕事人数表示用:(現在仕事:LOCAL:0) += 1
	LOCAL:1 += 1
NEXT


@一日終了時レポート表示_リゾート収支計算(ボタン番号)
#DIM ボタン番号
DRAWLINE

PRINTL 
HTML_PRINT リゾート収支報告文字列:(ボタン番号 - 100)

WAIT

@一日終了時レポート処理_リゾート収支計算()
#DIM 業務量
#DIM DYNAMIC 長期雇用者保存配列, 2000
#DIM 給料基礎値
#DIM 口上番号
#DIM 給料合計値
#DIM ループ用
#DIM 消費ZP
#DIM 長期雇用者人数
#DIM 食材ID
#DIM 表示フラグ
#DIM 施設維持費
#DIM 合計ルピ収入
#DIM 合計ZP収入
#DIM 一時ルピ計算
#DIM 一時ZP計算
#DIM フォント色変更
#DIMS 料理名

;記録用文字列リセット
RSTR:リゾート収支計算_記録文字列 = 
フォント色変更 = 0
VARSET リゾート収支報告文字列
;:0:長期雇用者EXP表示　1:従業員雇用費詳細　2:施設効果収入詳細
合計ルピ収入 = 0
合計ZP収入 = 0

RSTR:リゾート収支計算_記録文字列 += "■リゾート収支計算報告レポート<br>"
RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
IF 滞在キャラ数算出関数() > 0
	RSTR:リゾート収支計算_記録文字列 += @"{滞在キャラ数算出関数()}人の観光客により、以下の利益を得た。<br>"
	;金払っているキャラのみ算出
	LOCAL = 滞在キャラ数算出関数() * 300 * RAND(95,106) * RAND(95, 106) / 10000
	SIF エンディングイベント完了フラグ確認("億万長者経営者") > 0
		LOCAL = LOCAL * 12 / 10
	MONEY += LOCAL
	合計ルピ収入 += LOCAL
	RSTR:リゾート収支計算_記録文字列 += @"ルピ利益：%NUM_FORMAT(LOCAL)%<br>"
	;リゾートにいるキャラ全員算出
	LOCAL = 滞在キャラ数算出関数(1)
	RSTR:リゾート収支計算_記録文字列 += @"{LOCAL}人の滞在者により、以下のＺＰを得た。<br>"
	LOCAL = LOCAL * 5 * RAND(95,106) * RAND(95, 106) / 10000
	SIF エンディングイベント完了フラグ確認("億万長者経営者") > 0
		LOCAL = LOCAL * 12 / 10
	ZP小数点以下所持量 += LOCAL
	合計ZP収入 += LOCAL
	RSTR:リゾート収支計算_記録文字列 += @"発生ＺＰ：{LOCAL / 1000}.{(LOCAL % 1000) / 100}{(LOCAL % 100) / 10}{(LOCAL % 10)}<br>"
	IF ZP小数点以下所持量 > 999
		FLAG:ZP所持量 += ZP小数点以下所持量 / 1000
		ZP小数点以下所持量 = ZP小数点以下所持量 % 1000
	ENDIF
	RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"

	LOCAL = 0
	LOCAL:1 = 0
	FOR LOCAL:2, 1, 6
		IF 現在設定メニュー:(LOCAL:2)
			LOCAL += DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:(LOCAL:2), "ルピ収入補正", 1) * 滞在キャラ数算出関数() / 5
			食材ID = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:(LOCAL:2), "メイン食材ID", 1)
			LOCAL -= DT_CELL_GET("所持素材データベース", 食材ID, "素材ランク", 1) * 滞在キャラ数算出関数() / 5 * DT_CELL_GET("所持素材データベース", 食材ID, "素材値相場", 1) / 100
			食材ID = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:(LOCAL:2), "追加食材１ID", 1)
			SIF 食材ID
				LOCAL -= DT_CELL_GET("所持素材データベース", 食材ID, "素材ランク", 1) * 滞在キャラ数算出関数() / 5 * DT_CELL_GET("所持素材データベース", 食材ID, "素材値相場", 1) / 100
			食材ID = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:(LOCAL:2), "追加食材２ID", 1)
			SIF 食材ID
				LOCAL -= DT_CELL_GET("所持素材データベース", 食材ID, "素材ランク", 1) * 滞在キャラ数算出関数() / 5 * DT_CELL_GET("所持素材データベース", 食材ID, "素材値相場", 1) / 100
			食材ID = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:(LOCAL:2), "追加食材３ID", 1)
			SIF 食材ID
				LOCAL -= DT_CELL_GET("所持素材データベース", 食材ID, "素材ランク", 1) * 滞在キャラ数算出関数() / 5 * DT_CELL_GET("所持素材データベース", 食材ID, "素材値相場", 1) / 100
			LOCAL:1 += DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:(LOCAL:2), "ZP収入補正", 1) * 滞在キャラ数算出関数() / 5
		ENDIF
	NEXT
	IF LOCAL || LOCAL:1
		RSTR:リゾート収支計算_記録文字列 += @"リゾートの料理メニューの効果により、以下の利益を得た。<br>"
		;乱数
		LOCAL = LOCAL * RAND(95,106) * RAND(95, 106) / 10000
		LOCAL:1 = LOCAL:1 * RAND(95,106) * RAND(95, 106) / 10000
		;補正による増加
		IF GROUPMATCH(発動中コンボ名, "バランスの良い食事処", "新鮮海鮮レストラン", "肉食べ放題レストラン", "健康野菜レストラン", "満腹穀物レストラン")
			LOCAL = LOCAL * 11 / 10
			RSTR:リゾート収支計算_記録文字列 += @"『%発動中コンボ名%』の効果でルピ収入UP！<br>"
		ENDIF
		;料理セットによる補正
		一時ルピ計算 = 0
		一時ZP計算 = 0
		FOR LOCAL:2, 1, 6
			VARSET RESULT
			料理名 '= DT_CELL_GETS("料理メニューデータベース",  現在設定メニュー:(LOCAL:2), "メニュー名", 1)
			TRYCALLFORM 料理効果_メニュー効果収入補正_%料理名%(LOCAL, LOCAL:1)
			一時ルピ計算 += RESULT:0
			一時ZP計算 += RESULT:1
		NEXT
		LOCAL = MAX(0, 一時ルピ計算 + LOCAL)
		LOCAL:1 = MAX(0, 一時ZP計算 + LOCAL:1)
		;従業員の効果
		LOCAL = LOCAL * (MIN(滞在キャラ数算出関数(1) / 10, 料理ルピ補正保存) + 100) / 100
		LOCAL:1 = LOCAL:1 * (MIN(滞在キャラ数算出関数(1) / 10, 料理ZP補正保存) + 100) / 100
		IF エンディングイベント完了フラグ確認("億万長者経営者") > 0
			LOCAL = LOCAL * 12 / 10
			LOCAL:1 = LOCAL:1 * 12 / 10
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += @"ルピ利益：%NUM_FORMAT(LOCAL)%<br>"
		RSTR:リゾート収支計算_記録文字列 += @"ＺＰ利益：{LOCAL:1 / 1000}.{(LOCAL:1 % 1000) / 100}{(LOCAL:1 % 100) / 10}{(LOCAL:1 % 10)}<br>"
		MONEY += LOCAL
		合計ルピ収入 += LOCAL
		ZP小数点以下所持量 += LOCAL:1
		合計ZP収入 += LOCAL:1
		IF ZP小数点以下所持量 > 999
			FLAG:ZP所持量 += ZP小数点以下所持量 / 1000
			ZP小数点以下所持量 = ZP小数点以下所持量 % 1000
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
	ENDIF

	LOCAL = 0
	LOCAL:1 = 0
	FOR LOCAL:2, 3, 9
		VARSET RESULT
		表示フラグ = 0
		TRYCALLFORM 空艇港店舗効果_{空艇港店舗状況:(LOCAL:2):0}("一日終了時のルピ収入")
		LOCAL += RESULT
		IF RESULT
			リゾート収支報告文字列:2 += @"%GETPLACENAME(1, LOCAL:2), 26, LEFT%の効果：%NUM_FORMAT(RESULT), 9%ルピ<br>"
			表示フラグ ++
		ENDIF
		TRYCALLFORM 空艇港店舗効果_{空艇港店舗状況:(LOCAL:2):0}("一日終了時のZP発生")
		LOCAL:1 += RESULT
		IF RESULT
			LOCALS = {RESULT / 1000}.{RESULT % 1000}
			リゾート収支報告文字列:2 += @"%GETPLACENAME(1, LOCAL:2), 26, LEFT%の効果：%LOCALS, 9%ZP<br>"
			表示フラグ ++
		ENDIF
		SIF 表示フラグ == 0
			リゾート収支報告文字列:2 += @"%GETPLACENAME(1, LOCAL:2), 26, LEFT%の効果：なし<br>"
		SIF 港商店常駐_収支報告文字列:(LOCAL:2) != ""
			リゾート収支報告文字列:2 += @"%港商店常駐_収支報告文字列:(LOCAL:2)%"
		リゾート収支報告文字列:2 += "<br>"
	NEXT
	IF LOCAL || LOCAL:1
		IF エンディングイベント完了フラグ確認("億万長者経営者") > 0
			リゾート収支報告文字列:2 += @"エンディング『億万長者経営者』の効果：ルピ・ZP１．２倍<br>"
			LOCAL = LOCAL * 12 / 10
			LOCAL:1 = LOCAL:1 * 12 / 10
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += @"%@"施設の効果により、以下の利益を得た。", 80, LEFT%"
		RSTR:リゾート収支計算_記録文字列 += "<button value='102'>[詳細を表示]</button>"
		RSTR:リゾート収支計算_記録文字列 += "<br>"
	ENDIF
	IF LOCAL + 施設ルピ補正保存
		RSTR:リゾート収支計算_記録文字列 += @"ルピ利益：%NUM_FORMAT(LOCAL + 施設ルピ補正保存)%<br>"
		MONEY += LOCAL + 施設ルピ補正保存
		合計ルピ収入 += LOCAL + 施設ルピ補正保存
	ENDIF
	IF LOCAL:1
		RSTR:リゾート収支計算_記録文字列 += @"発生ＺＰ：{LOCAL:1 / 1000}.{(LOCAL:1 % 1000) / 100}{(LOCAL:1 % 100) / 10}{(LOCAL:1 % 10)}<br>"
		ZP小数点以下所持量 += LOCAL:1
		合計ZP収入 += LOCAL:1
		IF ZP小数点以下所持量 > 999
			FLAG:ZP所持量 += ZP小数点以下所持量 / 1000
			ZP小数点以下所持量 = ZP小数点以下所持量 % 1000
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
	ENDIF
	
	;商業区収入
	IF 港改造度:0:0
		LOCAL = 0
		表示フラグ = 0
		FOR LOCAL:2, 0, VARSIZE("商業区店割り配列", 1)
			IF 商業区店割り配列:(LOCAL:2):店舗名 != ""
				IF GETBIT(TOINT(商業区店割り配列:(LOCAL:2):定休日), DAY % 7)
					;店休日のビットが立ってると店休なので店効果スキップ
				ELSE
					TRYCCALLFORM 特殊店舗_一日終了時処理_%商業区店割り配列:(LOCAL:2):店舗名%
						表示フラグ ++
					CATCH
						LOCAL ++
					ENDCATCH
				ENDIF
			ENDIF
		NEXT
		IF LOCAL
			LOCAL = 滞在キャラ数算出関数() * LOCAL * 5 * RAND(95, 106) / 100
			IF 表示フラグ
				RSTR:リゾート収支計算_記録文字列 += @"商業区のその他店舗より、以下の場所代を得た。<br>"
			ELSE
				RSTR:リゾート収支計算_記録文字列 += @"商業区の店舗より、以下の場所代を得た。<br>"
			ENDIF
			RSTR:リゾート収支計算_記録文字列 += @"ルピ利益：%NUM_FORMAT(LOCAL)%<br>"
			MONEY += LOCAL
			合計ルピ収入 += LOCAL
			RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
		ENDIF
	ENDIF

	施設維持費 = 0
	IF 施設改造度:7:0 > 0
		施設維持費 += 5000
	ENDIF
	IF 施設改造度:7:1 > 0
		施設維持費 += 2000
	ENDIF
	IF 施設改造度:7:2 > 0
		施設維持費 += 10000
	ENDIF
	IF 施設改造度:7:3 > 0
		施設維持費 += 1000
	ENDIF
	IF 施設維持費
		RSTR:リゾート収支計算_記録文字列 += @"施設維持費として以下のルピを消費した。<br>"
		RSTR:リゾート収支計算_記録文字列 += @"維持費合計：%NUM_FORMAT(施設維持費)%<br>"
		MONEY -= 施設維持費
		合計ルピ収入 -= 施設維持費
		RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
	ENDIF

	消費ZP = 0
	FOR LOCAL, 0, 50
		SIF ERDNAME(フィールド展開, LOCAL) == ""
			BREAK
		SIF フィールド展開:LOCAL <= 0
			CONTINUE
		CALLFORM フィールド効果解説_%ERDNAME(フィールド展開, LOCAL)%
		消費ZP += RESULT
		IF 消費ZP > FLAG:ZP所持量
			RSTR:リゾート収支計算_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
			RSTR:リゾート収支計算_記録文字列 += @"ZPが足りないため、%ERDNAME(フィールド展開, LOCAL)%効果を停止しました。</font><br>"
			フィールド展開:LOCAL = 0
			;レポート強調用記憶：リゾート収支計算(－ZP不足によるフィールド停止)
			SIF レポート強調フラグ:リゾート収支計算 == 0
				レポート強調フラグ:リゾート収支計算 = -1
		ENDIF
	NEXT
	IF 消費ZP
		RSTR:リゾート収支計算_記録文字列 += @"フィールド維持のために{MIN(消費ZP, FLAG:ZP所持量), 3}ZPを消費した。<br>"
		IF 消費ZP > FLAG:ZP所持量
			合計ZP収入 -= FLAG:ZP所持量 * 1000 + ZP小数点以下所持量
			FLAG:ZP所持量 = 0
			ZP小数点以下所持量 = 0
		ELSE
			合計ZP収入 -= 消費ZP * 1000
			FLAG:ZP所持量 -= 消費ZP
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
	ENDIF

	長期雇用者人数 = 0
	FOR ループ用, 0, CHARANUM
		IF CFLAG:ループ用:長期雇用
			長期雇用者保存配列:長期雇用者人数 = ループ用
			長期雇用者人数 ++
		ENDIF
	NEXT
	IF 長期雇用者人数 > 0
		FOR ループ用, 0, 長期雇用者人数
			SIF 長期雇用者保存配列:ループ用 > 0
				リゾート収支報告文字列:0 += @"%CALLNAME:(長期雇用者保存配列:ループ用)%雇用費：1000ルピ<br>"
		NEXT
		RSTR:リゾート収支計算_記録文字列 += @"{長期雇用者人数}名の長期雇用により、%NUM_FORMAT(長期雇用者人数 * 1000), 9%ルピを消費した。<br>"
		MONEY -= 長期雇用者人数 * 1000
		合計ルピ収入 -= 長期雇用者人数 * 1000
		IF 施設改造度:3:8 > 0
			LOCAL = 最大レベル算出()
			CALL 口上変数初期化
			FOR ループ用, 0, 長期雇用者人数
				;非戦闘キャラはスキップ
				IF TALENT:(長期雇用者保存配列:ループ用):非戦闘キャラ
					CONTINUE
				ENDIF
				IF 長期雇用者保存配列:ループ用 > 0 && 基礎BATTLE_STATE:(長期雇用者保存配列:ループ用):Lv < LOCAL
					IF 基礎BATTLE_STATE:(長期雇用者保存配列:ループ用):蓄積EXP >  戦闘経験値テーブル:(MAX(LOCAL - 2, 0))
						;最大Lv到達EXPより現在のEXPが高いならスキップ
						リゾート収支報告文字列:0 += @"%CALLNAME:(長期雇用者保存配列:ループ用), 18, LEFT%は今は訓練でEXPを入手できないようだ。<br>"
						CONTINUE
					ENDIF
					LOCAL:1 = MIN(戦闘経験値テーブル:(MAX(LOCAL - 2, 0)) / 10, 500)

					;EXPは最大レベル到達EXPより上には行かない
					業務量 = MIN(LOCAL:1, 戦闘経験値テーブル:(MAX(LOCAL - 2, 0)) - 基礎BATTLE_STATE:(長期雇用者保存配列:ループ用):蓄積EXP)
					リゾート収支報告文字列:0 += @"%CALLNAME:(長期雇用者保存配列:ループ用), 18, LEFT%は訓練を通して{業務量, 6}EXPを入手した。<br>"
					基礎BATTLE_STATE:(長期雇用者保存配列:ループ用):蓄積EXP = 基礎BATTLE_STATE:(長期雇用者保存配列:ループ用):蓄積EXP + 業務量

					CALL 口上変数初期化
					CALL レベルアップ処理(長期雇用者保存配列:ループ用)
					;レベルアップ時の簡易お知らせ
					IF RESULT > 0
						レポート強調フラグ:リゾート収支計算 = 1
						フォント色変更 = 1
					ENDIF
					;KSTR系列に入ってるものを変換、特殊な動作は入れてないはずなので特に処理は通さない
					FOR 口上番号,0,1000
						SIF !口上有無確認(口上番号,1)
							BREAK
						IF !(WSTR:口上番号 == "")
							リゾート収支報告文字列:0 += @"%WSTR:口上番号%<br>"
						ELSEIF !(KSTR:口上番号 == "")
							リゾート収支報告文字列:0 += @"%KSTR:口上番号%<br>"
						ELSEIF !(NSTR:口上番号 == "")
							リゾート収支報告文字列:0 += @"%NSTR:口上番号%"
						ELSEIF !(WNSTR:口上番号 == "")
							リゾート収支報告文字列:0 += @"%WNSTR:口上番号%"
						ENDIF
					NEXT
					CALL 口上変数初期化
				ENDIF
			NEXT
		ENDIF
		IF フォント色変更
			RSTR:リゾート収支計算_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += @"%@"訓練室の効果により、長期雇用者{長期雇用者人数, 2}名の経験値が上昇した。", 80, LEFT%"
		RSTR:リゾート収支計算_記録文字列 += "<button value='100'>[詳細を表示]</button>"
		IF フォント色変更
			RSTR:リゾート収支計算_記録文字列 += "</font>"
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += "<br>"
		RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
		フォント色変更 = 0
	ENDIF
	IF 従業員人数 > 0
		IF SAVESTR:あなたスタイル == "スタートダッシュモード" && DAY < 30
			RSTR:リゾート収支計算_記録文字列 += @"※スタートダッシュ時は最初の一ヶ月は給料無料<br>"
		ELSE
			給料合計値 = 0
			FOR ループ用, 1, CHARANUM
				SIF TALENT:ループ用:従業員 == 0
					CONTINUE
				IF TALENT:ループ用:恋慕
					給料基礎値 = 1000
				ELSE
					給料基礎値 = 2000
				ENDIF
				LOCALS = 
				LOCALS += @"%CALLNAME:ループ用, 18, LEFT%は"
				IF TCVAR:ループ用:休暇中
					LOCALS += "休暇中だ。"
				ELSE
					SELECTCASE RAND:5
						CASE 0
							;給料変動なし
							LOCALS += "頑張って働いている。"
						CASE 1
							;１割引
							給料基礎値 = 給料基礎値 * 9 / 10
							LOCALS += "幾つか失敗してしまった……。"
						CASE 2
							;１割増
							給料基礎値 = 給料基礎値 * 11 / 10
							LOCALS += "とても良く働いている！"
						CASE 3
							;２割引
							給料基礎値 = 給料基礎値 * 8 / 10
							LOCALS += "大きなミスをしてしまった！"
						CASE 4
							;２割増
							給料基礎値 = 給料基礎値 * 12 / 10
							LOCALS += "素晴らしい働きをしている！"
					ENDSELECT
					給料基礎値 = 給料基礎値 * RAND(95, 105) / 100
					給料合計値 += 給料基礎値
					リゾート収支報告文字列:1 += @"%LOCALS, 50, LEFT%　　雇用費：%NUM_FORMAT(給料基礎値), 7%ルピ<br>"
					MONEY -= 給料基礎値
					合計ルピ収入 -= 給料基礎値
				ENDIF
			NEXT
			RSTR:リゾート収支計算_記録文字列 += @"%@"{従業員人数}人の従業員により、%NUM_FORMAT(給料合計値), 7%ルピを消費した。", 80, LEFT%"
			RSTR:リゾート収支計算_記録文字列 += "<button value='101'>[詳細を表示]</button>"
			RSTR:リゾート収支計算_記録文字列 += "<br>"
		ENDIF
		RSTR:リゾート収支計算_記録文字列 += "-------------------------------------------------------------------------------------------------------------------<br>"
	ENDIF
ENDIF
IF その他ルピ補正保存 != 0 || その他ZP補正保存 != 0
	リゾート収支報告文字列:9 '= RSTR:その他収支報告文字列
	RSTR:リゾート収支計算_記録文字列 += @"%@"その他ルピ変動：%NUM_FORMAT(その他ルピ補正保存)%ルピ", 80, LEFT%"
	RSTR:リゾート収支計算_記録文字列 += "<button value='109'>[詳細を表示]</button><br>"
	LOCALS = {ABS(その他ZP補正保存 / 1000)}.{ABS(その他ZP補正保存 % 1000)}
	IF その他ZP補正保存 >= 0
		RSTR:リゾート収支計算_記録文字列 += @"その他ZP変動：%LOCALS, 9%ZP<br>"
	ELSE
		RSTR:リゾート収支計算_記録文字列 += @"その他ZP変動：－%LOCALS, 9%ZP</font><br>"
	ENDIF
ENDIF


IF 合計ルピ収入 >= 0
	;基本方針
	IF 条例発布:0 == 2
		RSTR:リゾート収支計算_記録文字列 += @"<font color='#%カラーパレット_HTML("真っ赤")%'>商業都市効果！　収入１．２倍</font><br>"
		MONEY += 合計ルピ収入 * 2 / 10
		合計ルピ収入 += 合計ルピ収入 * 2 / 10
	ENDIF
	RSTR:リゾート収支計算_記録文字列 += @"%@"ルピ収支：＋%NUM_FORMAT(ABS(合計ルピ収入)), 9%ルピ", 100%<br>"
ELSE
	RSTR:リゾート収支計算_記録文字列 += @"%@"<font color='#%カラーパレット_HTML("赤")%'>ルピ収支：－%NUM_FORMAT(ABS(合計ルピ収入)), 9%ルピ</font>", 129%<br>"
ENDIF
LOCALS = {ABS(合計ZP収入 / 1000)}.{ABS(合計ZP収入 % 1000)}
IF 合計ZP収入 >= 0
	RSTR:リゾート収支計算_記録文字列 += @"%@"ZP収支：＋%LOCALS, 9%ZP", 98%<br>"
ELSE
	RSTR:リゾート収支計算_記録文字列 += @"%@"<font color='#%カラーパレット_HTML("赤")%'>ZP収支：－%LOCALS, 9%ZP</font>", 127%<br>"
ENDIF




@一日終了時レポート表示_リゾート人気変動(ボタン番号)
#DIM ボタン番号

@一日終了時レポート処理_リゾート人気変動()
#DIM 人気度変動一時保存
#DIM ルピ減数
#DIM バッドイベ判定値
#DIM ランダムセット

LOCALS:1 = 
LOCALS:2 = <br>
RSTR:リゾート人気変動_記録文字列 = 
人気度変動一時保存 = 0
IF モブ観光客数 > 0
	LOCALS:1 +=  "■本日のリゾート人気度変動<br>"
	IF 人気度別施設充足度() > 0
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「遊ぶ場所が十分あって楽しい！」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += "人気度UP！<br>"
		人気度変動一時保存 += 人気度別施設充足度() * 1000
	ELSEIF 人気度別施設充足度() < 0
		RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「遊ぶ場所が足りないよ……」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += "人気度DOWN…<br>"
		RSTR:リゾート人気変動_記録文字列 +=  "</font>"
		人気度変動一時保存 -= 人気度別施設充足度() * 500
		;レポート強調用記憶：リゾート人気変動(－施設不足による人気ダウン)
		SIF レポート強調フラグ:リゾート人気変動 == 0
			レポート強調フラグ:リゾート人気変動 = -1
	ENDIF

	VARSET LOCAL
	;雑務ゲージ平均値算出
	FOR LOCAL, 1, マップ部屋番号上限
		SIF LOCAL == 20
			CONTINUE
		SIF LOCAL > 2000
			CONTINUE
		IF 存在しない移動先(0, LOCAL) == 0
			LOCAL:1 += 雑務ゲージ:LOCAL
			LOCAL:2 += 1
		ENDIF
	NEXT
	LOCAL:1 = LOCAL:1 / LOCAL:2
	IF LOCAL:1 < 500
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「雑務の対応が早くて快適だね」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += "人気度UP！<br>"
		人気度変動一時保存 += 1000
	ELSEIF LOCAL:1 < 2000
	ELSEIF LOCAL:1 < 4000
		RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「ちょっと雑務が溜まってるかな」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += @"人気度DOWN…<br>"
		RSTR:リゾート人気変動_記録文字列 +=  "</font>"
		人気度変動一時保存 -= 1000
		;レポート強調用記憶：リゾート人気変動(－雑務不足による人気ダウン)
		SIF レポート強調フラグ:リゾート人気変動 == 0
			レポート強調フラグ:リゾート人気変動 = -1
	ELSEIF LOCAL:1 < 6000
		RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「雑務が遅いのが気になるかも」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += @"人気度DOWN…<br>"
		RSTR:リゾート人気変動_記録文字列 +=  "</font>"
		人気度変動一時保存 -= 3000
		;レポート強調用記憶：リゾート人気変動(－雑務不足による人気ダウン)
		SIF レポート強調フラグ:リゾート人気変動 == 0
			レポート強調フラグ:リゾート人気変動 = -1
	ELSEIF LOCAL:1 < 8000
		RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「雑務、溜まりすぎじゃない？」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += @"人気度DOWN…<br>"
		RSTR:リゾート人気変動_記録文字列 +=  "</font>"
		人気度変動一時保存 -= 5000
		;レポート強調用記憶：リゾート人気変動(－雑務不足による人気ダウン)
		SIF レポート強調フラグ:リゾート人気変動 == 0
			レポート強調フラグ:リゾート人気変動 = -1
	ELSE
		RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「雑務が何も対応されてない！」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += @"人気度DOWN…<br>"
		RSTR:リゾート人気変動_記録文字列 +=  "</font>"
		人気度変動一時保存 -= 10000
		;レポート強調用記憶：リゾート人気変動(－雑務不足による人気ダウン)
		SIF レポート強調フラグ:リゾート人気変動 == 0
			レポート強調フラグ:リゾート人気変動 = -1
	ENDIF

	;各施設イベント、モブが100人超えてから発生
	IF モブ観光客数 >= 100
		;数値が高いほど悪いイベが発生
		バッドイベ判定値 = RAND:100
	ENDIF
	
	VARSET LOCAL
	;魔物襲撃が残ってるかどうか
	LOCAL:1 = DT_ROW_LENGTH("各イベント用変数配列")
	FOR LOCAL, 0, LOCAL:1
		IF DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名") == "魔物襲撃"
			IF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") && DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント完了フラグ") == 0
				;出現しているかつクリアしてない
				RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
				RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「魔物が出てきて危ないぞ！！」", 80, LEFT%"
				LOCALS:1 += "モブ観光客「魔物が出てきて危ないぞ！！」<br>"
				RSTR:リゾート人気変動_記録文字列 += @"人気度DOWN…<br>"
				RSTR:リゾート人気変動_記録文字列 += "</font>"
				人気度変動一時保存 -= 10000
				;レポート強調用記憶：リゾート人気変動(－魔物による人気ダウン)
				SIF レポート強調フラグ:リゾート人気変動 == 0
					レポート強調フラグ:リゾート人気変動 = -1
			ELSEIF DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT") == -2
				;再発生中
				RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
				RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「魔物が出てきて危ないぞ！！」", 80, LEFT%"
				RSTR:リゾート人気変動_記録文字列 += @"人気度DOWN…<br>"
				RSTR:リゾート人気変動_記録文字列 += "</font>"
				人気度変動一時保存 -= 10000
				;レポート強調用記憶：リゾート人気変動(－魔物による人気ダウン)
				SIF レポート強調フラグ:リゾート人気変動 == 0
					レポート強調フラグ:リゾート人気変動 = -1
			ENDIF
		ENDIF
		IF DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名") == "ならず者の来訪"
			IF (DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") && DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント完了フラグ") == 0) || DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT") == -2
				RSTR:リゾート人気変動_記録文字列 += @"<font color='#%カラーパレット_HTML("赤")%'>"
				ランダムセット = RAND:3
				SELECTCASE ランダムセット
					CASE 2
						RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「ならず者が出てきて危ないんですけど！！」", 80, LEFT%"
						RSTR:リゾート人気変動_記録文字列 += "人気度DOWN…<br>"
						人気度変動一時保存 -= 10000
					CASE 1
						ルピ減数 = RAND(5000, 10001)
						MONEY -= ルピ減数
						RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「変な人が従業員室に入っていったの見たんですが…」", 80, LEFT%<br>"
						RSTR:リゾート人気変動_記録文字列 += @"%@"調べたら金庫に入っていた{ルピ減数}ルピがなくなっていた", 80, LEFT%"
						RSTR:リゾート人気変動_記録文字列 += "人気度DOWN…<br>"
						人気度変動一時保存 -= 1000
						;収支報告に記載
						その他ルピ補正保存 -= ルピ減数
						RSTR:その他収支報告文字列 += @"%"ならず者による被害", 50, LEFT%：%NUM_FORMAT(ルピ減数), 9%ルピ<br>"
					CASE 0
						ルピ減数 = RAND(2500, 5001)
						MONEY -= ルピ減数
						RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「部屋に戻ってきたら荒らされてたんですけど！！」", 80, LEFT%<br>"
						RSTR:リゾート人気変動_記録文字列 += @"%@"補償として{ルピ減数}ルピを支払った", 80, LEFT%<br>"
						RSTR:リゾート人気変動_記録文字列 += "人気度DOWN…<br>"
						人気度変動一時保存 -= 5000
						;収支報告に記載
						その他ルピ補正保存 -= ルピ減数
						RSTR:その他収支報告文字列 += @"%"ならず者による被害", 50, LEFT%：%NUM_FORMAT(ルピ減数), 9%ルピ<br>"
				ENDSELECT
				RSTR:リゾート人気変動_記録文字列 += "</font>"
				;レポート強調用記憶：リゾート人気変動(－ならず者による人気ダウン)
				SIF レポート強調フラグ:リゾート人気変動 == 0
					レポート強調フラグ:リゾート人気変動 = -1
			ENDIF
		ENDIF
	NEXT

	;基本方針
	IF 条例発布:0 == 1
		RSTR:リゾート人気変動_記録文字列 += @"%"モブ観光客「素晴らしい街並みだよ！　リゾート地はやはり違うなぁ」", 80, LEFT%"
		RSTR:リゾート人気変動_記録文字列 += "人気度UP！<br>"
		人気度変動一時保存 += モブ観光客数 * 5
	ENDIF

	RSTR:リゾート人気変動_記録文字列 += @"--------------------------------------------------------------------------------------<br>"
	SKIPDISP 1
	CALL 人気上昇処理(人気度変動一時保存)
	SKIPDISP 0
	RSTR:リゾート人気変動_記録文字列 += @"リゾート人気度　　%リゾート人気度全文字列()%"
	CALL 人気度上限算出()
	IF リゾート人気度 >= RESULT / 1000
		RSTR:リゾート人気変動_記録文字列 += "　　　<font color='#C0C070'>上限到達</font>"
	ENDIF
ENDIF


@一日終了時レポート表示_依頼報告(ボタン番号)
#DIM ボタン番号
DRAWLINE

PRINTL 
HTML_PRINT 依頼報告文字列:(ボタン番号 - 100)

WAIT


@一日終了時レポート処理_依頼報告()

VARSET 依頼報告文字列

LOCAL:1 = 0
RSTR:依頼報告_記録文字列 = 
FOR LOCAL, 0, 10
	IF 遂行中依頼:LOCAL == ""
		CONTINUE
	ENDIF
	IF 依頼受注者名:LOCAL == ""
		;未受注なので受注者をランダムで決定
		CALL 依頼受注者決定(LOCAL)
		RSTR:依頼報告_記録文字列 += @"%遂行中依頼:LOCAL% の依頼を %依頼受注者名:LOCAL% が受注したようだ。<br>"
		LOCAL:1 = 1
	ELSE
		;受注済みなので依頼の終了判定
		;依頼残り期間が存在する場合はその間絶対に終了しない
		IF 依頼残り期間:LOCAL > 0
			RSTR:依頼報告_記録文字列 += @"%依頼受注者名:LOCAL% は %遂行中依頼:LOCAL% のために移動中だ。<br>"
			依頼残り期間:LOCAL -= 1
		ELSE
			LOCAL:10 = RAND:100
			IF LOCAL:10 < 依頼完了確率_初期値:LOCAL
				;依頼完了
				SIF LOCAL:1
					PRINTL
				RSTR:依頼報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
				RSTR:依頼報告_記録文字列 += @"%@"%依頼受注者名:LOCAL% が %遂行中依頼:LOCAL% の依頼を達成したようだ！", 80, LEFT%"
				RSTR:依頼報告_記録文字列 += @"<button value='{LOCAL + 100}'>[詳細を表示]</button>"
				RSTR:依頼報告_記録文字列 +=  "</font><br>"
				CALLFORM 依頼完了時処理_%遂行中依頼:LOCAL%(LOCAL)
				;登録情報をリセット
				遂行中依頼:LOCAL = 
				遂行中依頼_対象名:LOCAL = 
				依頼受注者名:LOCAL = 
				依頼残り期間:LOCAL = 0
				依頼完了確率_初期値:LOCAL = 0
				依頼完了確率_進数:LOCAL = 0
				LOCAL:1 = 1
				;レポート強調用記憶：依頼報告(＋依頼達成)
				レポート強調フラグ:依頼報告 = 1
			ELSE
				;まだ完了しないので確率を上昇させておしまい
				RSTR:依頼報告_記録文字列 += @"%依頼受注者名:LOCAL% は %遂行中依頼:LOCAL% の依頼を遂行中……<br>"
				依頼完了確率_初期値:LOCAL += 依頼完了確率_進数:LOCAL
			ENDIF
		ENDIF
	ENDIF
NEXT


@一日終了時レポート表示_その他報告(ボタン番号)
#DIM ボタン番号

@一日終了時レポート処理_その他報告()

CSTR:LOCAL:その他報告_キャラ別記録文字列 = 
RSTR:その他報告_記録文字列 = 

IF 海改造度:6:0
	CALL 海エリア漁処理()
ENDIF

;[SKIPEND]






@レポート後全体処理()
#DIM ループ用

;--------------------------------------------------------------------------------
;キャラ個別処理
;--------------------------------------------------------------------------------
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	TRYCALLFORM DAYEND_CHARAEVENT_{NO:LOCAL}
	;滞在中は信頼度がじわじわ上昇（初対面だと上昇しない）
	IF 初体験日:LOCAL:初対面
		SKIPDISP 1
		CFLAG:LOCAL:信頼度 += 500
		CALL FAVOR_LvUPCheck(LOCAL)
		SKIPDISP 0
	ENDIF
	CALL 処女童貞適用処理(LOCAL)

	;性癖開拓のリセット
	IF OPTION変数:性癖開拓自動継続 == 0
		性癖素質:LOCAL:性癖開発用フラグ変数 = 0
	ELSEIF RCVAR:LOCAL:性癖開拓結果保存 == 1
		性癖素質:LOCAL:性癖開発用フラグ変数 = 0
	ENDIF

	;合計滞在日数カウント
	EXP:LOCAL:滞在日数 ++
	SIF TALENT:LOCAL:従業員
		EXP:LOCAL:従業員日数 ++
	SIF TALENT:LOCAL:定住者
		EXP:LOCAL:定住者日数 ++
NEXT

;--------------------------------------------------------------------------------
;突発イベント再発生CT減少＆再発生判定
;--------------------------------------------------------------------------------
ループ用 = DT_ROW_LENGTH("各イベント用変数配列")

FOR LOCAL, 0, ループ用
	LOCAL:1 = DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT")
	LOCAL:2 = DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生確率")
	LOCAL:3 = DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント消滅CT")
	
	RESULT = 0
	;再発生確率は消失時にしか入らないはずなので、そこで条件を読むか見る
	SIF LOCAL:2 > 0
		TRYCALLFORM イベント時限消失_再発生条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
	IF RESULT == -1
		;再発生させない
		IF LOCAL:1 > 0
			;CTが残ってる場合は１減らす
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", LOCAL:1 - 1
		ENDIF
	ELSEIF RESULT == 1
		;即座に再発生させる
		DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", -1
	ELSE
		;以降通常処理
		IF LOCAL:1 > 0
			;CTが残ってる場合は１減らす
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", LOCAL:1 - 1
		ELSEIF LOCAL:3 == -2
			;消失状態、かつ発生条件を満たす場合はRANDで再発生させる
			RESULT = 0
			TRYCALLFORM イベント発生条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
			IF RESULT
				LOCAL:10 = RAND:100
				IF LOCAL:2 > LOCAL:10
					DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント再発生CT", -1
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	LOCAL:1 = DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント消滅CT")
	LOCAL:2 = DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント消滅確率")

	RESULT = 0
	;消滅確率は消失時にしか入らないはずなので、そこで条件を読むか見る
	SIF LOCAL:2 > 0
		TRYCALLFORM イベント時限消失_再発生条件_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベントカテゴリ")%_%DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名")%
	IF RESULT == -1
		;消滅させない
		IF LOCAL:1 > 0
			;CTが残ってる場合は１減らす
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", LOCAL:1 - 1
		ENDIF
	ELSEIF RESULT == 1
		;即座に消滅させる
		DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", -1
	ELSE
		;以降通常処理
		IF LOCAL:1 > 0
			;CTが残ってる場合は１減らす
			DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", LOCAL:1 - 1
		ELSEIF LOCAL:2 > 0
			;現在出現中のものだけ消滅判定
			IF (DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント発生フラグ") == 1 && DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント完了フラグ") == 0) || DT_CELL_GET("各イベント用変数配列", LOCAL, "イベント再発生CT") == -2
				;消失確率がある場合はRANDで消失させる
				LOCAL:10 = RAND:100
				IF LOCAL:2 > LOCAL:10
					DT_CELL_SET "各イベント用変数配列", LOCAL, "イベント消滅CT", -1
				ENDIF
			ENDIF
		ENDIF
	ENDIF
NEXT

;--------------------------------------------------------------------------------
;輸入品店品揃え変動
;--------------------------------------------------------------------------------
CALL 輸入品店品揃え変動処理

;変数初期化
VARSET 解放キャラ招待
モブ観光客数 = 0
集客人数 = 0


@人気度別施設充足度()
#FUNCTION

SELECTCASE リゾートランク
	CASE 0
		;誰も知らないリゾート
		;一個でも施設改造していればOK、してないなら不満小
		IF 施設改造度:3:0 || 施設改造度:5:0 || 施設改造度:15:0
			RETURNF 施設改造度:3:0 + 施設改造度:5:0 + 施設改造度:15:0 + 施設改造度:3:8 + 施設改造度:5:6
		ELSE
			RETURNF -1
		ENDIF
	CASE 1
		;無名リゾート
		;前段階の改造は全部必須
		IF 施設改造度:3:0 && 施設改造度:5:0 && 施設改造度:15:0
			IF 施設改造度:3:8 || 施設改造度:5:6
				;追加施設の数に応じて＋補正
				RETURNF 施設改造度:3:8 + 施設改造度:5:6
			ELSE
				RETURNF 0
			ENDIF
		ELSE
			RETURNF -4 + 施設改造度:3:0 + 施設改造度:5:0 + 施設改造度:15:0
		ENDIF
ENDSELECT

RETURNF 0


@学習判定_一日終了時()
#DIM 対象キャラ
#DIM スケジュール番号

;学習が終わる前に寝ると学習判定がスキップされてしまうので、それの補填処理

FOR 対象キャラ, 1, CHARANUM
	;寝た瞬間以降のスケジュールを全部見る
	FOR スケジュール番号, スケジュール状態:LOCAL:現在スケジュール, 最大スケジュール登録数
		SIF 移動スケジュール文字列:対象キャラ:スケジュール番号:スケジュール == ""
			BREAK
		IF 移動スケジュール文字列:対象キャラ:スケジュール番号:スケジュール == "学校で勉強"
			CALL 自動学習処理(対象キャラ)
		ENDIF
	NEXT
NEXT


