@アビテンプレ変数リセット
アビテンプレート用_アビ名 = 
VARSET アビテンプレート用_対象保存, -1
VARSET アビテンプレート用_表示メッセージ格納:0:0
VARSET A_M_NUM
アビテンプレート用_キャンセルフラグ = 0
アビテンプレート用_全体フラグ = 0
アビテンプレート用_アビ名表示済フラグ = 0
アビテンプレート用_アビ封印用フラグ = 0
アビテンプレート用_行動消費無しフラグ = 0
アビテンプレート用_回避フラグ = 0
VARSET アビテンプレート用_行動中敵対心, 0
VARSET アビテンプレート用_奥義使用先保存, -1

アビテンプレート用_反応口上モード記録 = 
VARSET アビテンプレート用_反応口上対象記録, -1
アビテンプレート用_反応口上管理番号 = 0

行動口上呼び出し用文字列 = 
口上用技名保存 = 
アビテンプレート用_付与バフ保存 = 
アビテンプレート用_付与デバフ保存 = 

CALL アビ汎用変数文字列リセット
CALL 口上変数初期化


@アビ汎用変数文字列リセット
VARSET アビ汎用変数:0
VARSET アビ汎用文字列:0
アビ汎用変数:数値乱数幅 = -1
アビ汎用変数:クリティカル率計算値保存 = -1
アビ汎用変数:クリティカルダメージ計算値保存 = -1


@アビテンプレート用_表示メッセージ変換処理
#DIM 対象番号
#DIM メッセージ番号
;まず実行時メッセージを表示
FOR メッセージ番号, 0, メッセージ格納最大行数
	SIF アビテンプレート用_表示メッセージ格納:システムメッセージ番号:メッセージ番号 == ""
		BREAK
	WSTR:(K++) = %アビテンプレート用_表示メッセージ格納:システムメッセージ番号:メッセージ番号%
NEXT

FOR 対象番号, 0, アビ対象最大数
	FOR メッセージ番号, 0, メッセージ格納最大行数
		SIF アビテンプレート用_表示メッセージ格納:対象番号:メッセージ番号 == ""
			BREAK
		WSTR:(K++) = %アビテンプレート用_表示メッセージ格納:対象番号:メッセージ番号%
	NEXT
NEXT

VARSET アビテンプレート用_表示メッセージ格納:0:0
VARSET A_M_NUM


@アビテンプレート_対象指定共通処理()
CALL アビテンプレート用_表示メッセージ変換処理

VARSET アビテンプレート用_対象保存, -1
SIF アビテンプレート用_キャンセルフラグ
	RETURN -1


@アビテンプレート_効果共通処理(対象なし = 0)
#DIM 対象なし
#DIM メッセージカスタムフラグ

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

IF アビテンプレート用_対象保存:0 < 0 && 対象なし == 0
	PRINTW 対象が不正です
	アビテンプレート用_キャンセルフラグ = 1
	RETURN -1
ENDIF

;使用時文章表示
IF アビテンプレート用_アビ名表示済フラグ == 0
	IF 戦闘行動内容 == 101
		;奥義の時に奥義カットイン
		CALL 画面再描画
		TRYCCALLFORM %行動口上呼び出し用文字列%(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号, 口上用技名保存)
			口上なしフラグ = 0
			CALL 口上変数初期化
		CATCH
			口上なしフラグ = 1
		ENDCATCH
		CALL カットイン呼出("奥義")
		IF RESULT != -1 || 口上なしフラグ == 0
			WAIT
			CALL カットイン継承(1)
			CALL カットイン継承終了(1)
		ENDIF
	ELSEIF 行動口上呼び出し用文字列 != ""
		TRYCALLFORM %行動口上呼び出し用文字列%(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号, 口上用技名保存)
		CALL 口上変数初期化
	ENDIF

	メッセージカスタムフラグ = 0
	IF アビ汎用文字列:実行時メッセージ１ != ""
		アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:実行時メッセージ１%
		メッセージカスタムフラグ = 1
	ENDIF
	IF アビ汎用文字列:実行時メッセージ２ != ""
		アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:実行時メッセージ２%
		メッセージカスタムフラグ = 1
	ENDIF
	IF アビ汎用文字列:実行時メッセージ３ != ""
		アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:実行時メッセージ３%
		メッセージカスタムフラグ = 1
	ENDIF
	IF アビ汎用文字列:実行時メッセージ４ != ""
		アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:実行時メッセージ４%
		メッセージカスタムフラグ = 1
	ENDIF
	IF メッセージカスタムフラグ || アビテンプレート用_アビ名 == ""
	ELSEIF 戦闘行動キャラ < 10
		アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%の%アビテンプレート用_アビ名%！
	ELSE
		アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の%アビテンプレート用_アビ名%！
	ENDIF
	アビテンプレート用_アビ名表示済フラグ = 1
ENDIF

IF アビ汎用文字列:途中表示メッセージ１ != ""
	アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:途中表示メッセージ１%
	アビ汎用文字列:途中表示メッセージ１ = 
ENDIF
IF アビ汎用文字列:途中表示メッセージ２ != ""
	アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:途中表示メッセージ２%
	アビ汎用文字列:途中表示メッセージ２ = 
ENDIF
IF アビ汎用文字列:途中表示メッセージ３ != ""
	アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:途中表示メッセージ３%
	アビ汎用文字列:途中表示メッセージ３ = 
ENDIF
IF アビ汎用文字列:途中表示メッセージ４ != ""
	アビテンプレート用_表示メッセージ格納:システムメッセージ番号:(A_M_NUM:システムメッセージ番号++) = %アビ汎用文字列:途中表示メッセージ４%
	アビ汎用文字列:途中表示メッセージ４ = 
ENDIF


@アビ対象選択テンプレート_自己のみ
CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

アビテンプレート用_対象保存:0 = 戦闘行動キャラ


@アビ対象選択テンプレート_指定(隊列番号, 指定対象番号 = -1, リセットなし = 0)
#DIM 隊列番号
#DIM 指定対象番号
#DIM リセットなし

IF 指定対象番号 < 0 || (指定対象番号 == 0 && !リセットなし)
	CALL アビテンプレート_対象指定共通処理
	SIF RESULT == -1
		RETURN -1
	指定対象番号 = 0
ELSE
	SIF アビテンプレート用_キャンセルフラグ
		RETURN -1
	SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
		RETURN 0
ENDIF

アビテンプレート用_対象保存:指定対象番号 = 隊列番号
IF INRANGE(隊列番号, 10, 19)
	SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
		アビテンプレート用_奥義使用先保存:指定対象番号 = アビテンプレート用_対象保存:指定対象番号
ENDIF


@アビ対象選択テンプレート_敵単体(ARG)
CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1
SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

SIF ARG
	GOTO INPUT_LOOP
SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = 誰に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
IF 戦闘行動キャラ < 9
	CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔", @"%CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%",,0,1)
ELSE
	CALL メッセージ欄表示用関数(, @"%CALLNAME:PLAYER%",,0,1)
ENDIF
CALL 口上変数初期化
$INPUT_LOOP
BINPUTS
IF STRCOUNT(RESULTS, "サイド領域") > 0
	CALL サイド領域_実行処理_戦闘中
	RESTART
ENDIF
RESULT = TOINT(RESULTS)
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		アビテンプレート用_対象保存:0 = RESULT
		SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
			アビテンプレート用_奥義使用先保存:0 = アビテンプレート用_対象保存:0
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT


@アビ対象選択テンプレート_敵横列
CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1
SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = どの横列に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
IF 戦闘行動キャラ < 9
	CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔", @"%CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%",,0,1)
ELSE
	CALL メッセージ欄表示用関数(, @"%CALLNAME:PLAYER%",,0,1)
ENDIF
CALL 口上変数初期化
$INPUT_LOOP
BINPUTS
IF STRCOUNT(RESULTS, "サイド領域") > 0
	CALL サイド領域_実行処理_戦闘中
	RESTART
ENDIF
RESULT = TOINT(RESULTS)
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		アビテンプレート用_対象保存:0 = RESULT
		SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
			アビテンプレート用_奥義使用先保存:0 = アビテンプレート用_対象保存:0
		IF アビテンプレート用_対象保存:0 % 2
			IF 敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 11):エネミー名 == ""
			ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 11):ＨＰ < 1
			ELSE
				アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 - 1
				SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
					アビテンプレート用_奥義使用先保存:1 = アビテンプレート用_対象保存:1
			ENDIF
		ELSE
			IF 敵BATTLE_STATE_STR:(アビテンプレート用_対象保存:0 - 9):エネミー名 == ""
			ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 9):ＨＰ < 1
			ELSE
				アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 + 1
				SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
					アビテンプレート用_奥義使用先保存:1 = アビテンプレート用_対象保存:1
			ENDIF
		ENDIF
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT


@アビ対象選択テンプレート_敵縦列
#DIM 敵番号
CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1
SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = どの縦列に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
IF 戦闘行動キャラ < 9
	CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔", @"%CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%",,0,1)
ELSE
	CALL メッセージ欄表示用関数(, @"%CALLNAME:PLAYER%",,0,1)
ENDIF
CALL 口上変数初期化
$INPUT_LOOP
BINPUTS
IF STRCOUNT(RESULTS, "サイド領域") > 0
	CALL サイド領域_実行処理_戦闘中
	RESTART
ENDIF
RESULT = TOINT(RESULTS)
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		LOCAL = 0
		IF RESULT % 2
			FOR 敵番号, 11, 21, 2
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE
				アビテンプレート用_対象保存:LOCAL = 敵番号
				SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
					アビテンプレート用_奥義使用先保存:LOCAL = アビテンプレート用_対象保存:LOCAL
				LOCAL += 1
			NEXT
		ELSE
			FOR 敵番号, 10, 20, 2
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE
				アビテンプレート用_対象保存:LOCAL = 敵番号
				SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
					アビテンプレート用_奥義使用先保存:LOCAL = アビテンプレート用_対象保存:LOCAL
				LOCAL += 1
			NEXT
		ENDIF
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT


@アビ対象選択テンプレート_敵全体(生者死者 = 0, 自己除外 = 0, デバフ条件 = "")
#DIM 生者死者
#DIM 自己除外
#DIMS デバフ条件
#DIM 敵番号
#DIM 対象数

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

対象数 = 0
FOR 敵番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
		CONTINUE
	IF 生者死者 == 1
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
			CONTINUE
	ELSEIF 生者死者 == 2
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ > 0
			CONTINUE
	ENDIF
	IF 自己除外
		SIF 敵番号 == 戦闘行動キャラ
			CONTINUE
	ENDIF
	IF デバフ条件 != ""
		SIF デバフ番号取得(デバフ条件, 敵番号) < 0
			CONTINUE
	ENDIF
	アビテンプレート用_対象保存:対象数 = 敵番号
	SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
		アビテンプレート用_奥義使用先保存:対象数 = アビテンプレート用_対象保存:対象数
	対象数 += 1
NEXT
アビテンプレート用_全体フラグ = 1

;対象が無い場合は-1を返す
SIF 対象数 == 0
	RETURN -1

@アビ対象選択テンプレート_敵全体_生者のみ
JUMP アビ対象選択テンプレート_敵全体(1, 0, "")

@アビ対象選択テンプレート_敵全体_死者のみ
JUMP アビ対象選択テンプレート_敵全体(2, 0, "")

@アビ対象選択テンプレート_敵全体_生者のみ_自己以外
JUMP アビ対象選択テンプレート_敵全体(1, 1, "")


@アビ対象選択テンプレート_敵ランダムＸ体(ランダム回数, 重複無しフラグ = 0, デバフ条件 = "")
#DIM ランダム回数
#DIM 重複無しフラグ
#DIMS デバフ条件
#DIM 対象数
#DIM 敵番号
#DIM 敵対心合計
#DIM 敵対心, 10
#DIM ランダム候補, 100
#DIM ランダム幅, 10

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

VARSET ランダム候補, -1
対象数 = 0
VARSET ランダム幅, 0

IF 戦闘行動キャラ >= 10 || アビ汎用変数:敵対心無視オプション
	;敵が敵を選ぶ場合は敵対心処理は挟まない
	FOR 敵番号, 10, 20
		SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
			CONTINUE
		IF デバフ条件 != ""
			SIF デバフ番号取得(デバフ条件, 敵番号) < 0
				CONTINUE
		ENDIF
		ランダム候補:対象数 = 敵番号
		対象数 += 1
		ランダム幅:(敵番号 - 10) = 1
	NEXT
	;対象が無い場合は-1を返す
	SIF 対象数 == 0
		RETURN -1
ELSE
	VARSET 敵対心, -1
	敵対心合計 = 0
	FOR 敵番号, 10, 20
		SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
			CONTINUE
		IF デバフ条件 != ""
			SIF デバフ番号取得(デバフ条件, 敵番号) < 0
				CONTINUE
		ENDIF
		IF アビテンプレート用_行動中敵対心:敵番号 <= 0
			CALL バフ・デバフ測定(敵番号, "敵対心")
			アビテンプレート用_行動中敵対心:敵番号 = MAX(RESULT, 1)
		ENDIF
		敵対心:(敵番号 - 10) = アビテンプレート用_行動中敵対心:敵番号
		敵対心合計 += 敵対心:(敵番号 - 10)
	NEXT
	;対象が無い場合は-1を返す
	SIF 敵対心合計 == 0
		RETURN -1
	VARSET LOCAL
	FOR 敵番号, 10, 20
		SIF 敵対心:(敵番号 - 10) < 0
			CONTINUE
		LOCAL += 敵対心:(敵番号 - 10) * 100 / 敵対心合計
		LOCAL = MIN(LOCAL, 100)
		VARSET ランダム候補, 敵番号, LOCAL:1, LOCAL
		ランダム幅:(敵番号 - 10) = LOCAL - LOCAL:1
		LOCAL:1 = MIN(LOCAL, 99)
	NEXT
	対象数 = LOCAL
ENDIF

FOR 敵番号, 0, ランダム回数
	アビテンプレート用_対象保存:敵番号 = ランダム候補:(RAND:対象数)
	SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
		アビテンプレート用_奥義使用先保存:敵番号 = アビテンプレート用_対象保存:敵番号
	IF 重複無しフラグ
		ARRAYSHIFT ランダム候補, -1 * ランダム幅:(アビテンプレート用_対象保存:敵番号 - 10), -1, SUMARRAY(ランダム幅, 0, アビテンプレート用_対象保存:敵番号 - 10)
		対象数 -= ランダム幅:(アビテンプレート用_対象保存:敵番号 - 10)
		ランダム幅:(アビテンプレート用_対象保存:敵番号 - 10) = 0
		SIF 対象数 <= 0
			BREAK
	ENDIF
NEXT

@アビ対象選択テンプレート_敵ランダムＸ体_重複無し(ランダム回数)
#DIM ランダム回数
JUMP アビ対象選択テンプレート_敵ランダムＸ体(ランダム回数, 1, "")


@アビ対象選択テンプレート_敵単体_最大ステータス(ステータス名)
#DIMS ステータス名
#DIM 敵番号
#DIM 比較敵番号
#DIM 比較数値保存

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

IF GETNUM(敵BATTLE_STATE, ステータス名) < 0
	PRINTW 指定されたステータスが不正です。開発者に連絡してください。
	RETURN -1
ENDIF


比較敵番号 = 0
比較数値保存 = 0
;ステータス依存なので敵対心は挟まない
FOR 敵番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
		CONTINUE
	IF 比較数値保存 < 敵BATTLE_STATE:(敵番号 - 10):ステータス名
		比較敵番号 = 敵番号
		比較数値保存 = 敵BATTLE_STATE:(敵番号 - 10):ステータス名
	ENDIF
NEXT

アビテンプレート用_対象保存:0 = 比較敵番号
SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
		アビテンプレート用_奥義使用先保存:0 = アビテンプレート用_対象保存:0


@アビ対象選択テンプレート_敵単体_最小ステータス(ステータス名)
#DIMS ステータス名
#DIM 敵番号
#DIM 比較敵番号
#DIM 比較数値保存

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

IF GETNUM(敵BATTLE_STATE, ステータス名) < 0
	PRINTW 指定されたステータスが不正です。開発者に連絡してください。
	RETURN -1
ENDIF


比較敵番号 = 0
比較数値保存 = 0
;ステータス依存なので敵対心は挟まない
FOR 敵番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
		CONTINUE
	IF 比較数値保存 > 敵BATTLE_STATE:(敵番号 - 10):ステータス名
		比較敵番号 = 敵番号
		比較数値保存 = 敵BATTLE_STATE:(敵番号 - 10):ステータス名
	ENDIF
NEXT

アビテンプレート用_対象保存:0 = 比較敵番号
SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
	アビテンプレート用_奥義使用先保存:0 = アビテンプレート用_対象保存:0

@アビ対象選択テンプレート_敵単体_最小ＨＰ
JUMP アビ対象選択テンプレート_敵単体_最小ステータス("ＨＰ")

@アビ対象選択テンプレート_敵単体_最大ＨＰ
JUMP アビ対象選択テンプレート_敵単体_最大ステータス("ＨＰ")


@アビ対象選択テンプレート_味方単体(生者死者 = 0, 自己除外 = 0)
#DIM 生者死者
#DIM 自己除外

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1
SIF アビテンプレート用_味方全滅フラグ
	RETURN 0

SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = 誰に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
キャラ画像ボタン化フラグ = 1
IF 戦闘行動キャラ < 9
	CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔", @"%CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%",,0,1)
ELSE
	CALL メッセージ欄表示用関数(, @"%CALLNAME:PLAYER%",,0,1)
ENDIF
CALL 口上変数初期化
$INPUT_LOOP
BINPUTS
IF STRCOUNT(RESULTS, "サイド領域") > 0
	CALL サイド領域_実行処理_戦闘中
	RESTART
ENDIF
RESULT = TOINT(RESULTS)
SELECTCASE RESULT
	CASE 0 TO 3
		IF BATTLE_STATE:RESULT:キャラ登録番号 < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		IF 生者死者 == 1
			;生者のみ
			IF BATTLE_STATE:RESULT:ＨＰ < 1
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		ELSEIF 生者死者 == 2
			;死者のみ
			IF BATTLE_STATE:RESULT:ＨＰ > 0
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		ENDIF
		IF 自己除外
			IF RESULT == 戦闘行動キャラ
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		ENDIF
		アビテンプレート用_対象保存:0 = RESULT
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@アビ対象選択テンプレート_味方単体_生者のみ
JUMP アビ対象選択テンプレート_味方単体(1, 0)

@アビ対象選択テンプレート_味方単体_死者のみ
JUMP アビ対象選択テンプレート_味方単体(2, 0)

@アビ対象選択テンプレート_味方単体_生者のみ_自己以外
JUMP アビ対象選択テンプレート_味方単体(1, 1)


@アビ対象選択テンプレート_味方単体_最大ステータス(ステータス名)
#DIMS ステータス名
#DIM 味方番号
#DIM 比較味方番号
#DIM 比較数値保存

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

IF GETNUM(BATTLE_STATE, ステータス名, 2) < 0
	PRINTW 指定されたステータスが不正です。開発者に連絡してください。
	RETURN -1
ENDIF

比較味方番号 = 0
比較数値保存 = 0
;ステータス依存なので敵対心は挟まない
FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:味方番号:ＨＰ < 1
		CONTINUE
	IF 比較数値保存 < BATTLE_STATE:味方番号:ステータス名
		比較味方番号 = 味方番号
		比較数値保存 = BATTLE_STATE:味方番号:ステータス名
	ENDIF
NEXT

アビテンプレート用_対象保存:0 = 比較味方番号
SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
	アビテンプレート用_奥義使用先保存:0 = アビテンプレート用_対象保存:0


@アビ対象選択テンプレート_味方単体_最小ステータス(ステータス名)
#DIMS ステータス名
#DIM 味方番号
#DIM 比較味方番号
#DIM 比較数値保存

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

IF GETNUM(BATTLE_STATE, ステータス名, 2) < 0
	PRINTW 指定されたステータスが不正です。開発者に連絡してください。
	RETURN -1
ENDIF

比較味方番号 = 0
比較数値保存 = 0
;ステータス依存なので敵対心は挟まない
FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:味方番号:ＨＰ < 1
		CONTINUE
	IF 比較数値保存 > BATTLE_STATE:味方番号:ステータス名
		比較味方番号 = 味方番号
		比較数値保存 = BATTLE_STATE:味方番号:ステータス名
	ENDIF
NEXT

アビテンプレート用_対象保存:0 = 比較味方番号
SIF アビ汎用文字列:ダメージ種類 == "奥義ダメージ"
	アビテンプレート用_奥義使用先保存:0 = アビテンプレート用_対象保存:0

@アビ対象選択テンプレート_味方単体_最小ＨＰ
JUMP アビ対象選択テンプレート_味方単体_最小ステータス("ＨＰ")

@アビ対象選択テンプレート_味方単体_最大ＨＰ
JUMP アビ対象選択テンプレート_味方単体_最大ステータス("ＨＰ")


@アビ対象選択テンプレート_味方全体(生者死者 = 0, 自己除外 = 0, デバフ条件 = "")
#DIM 生者死者
#DIM 自己除外
#DIMS デバフ条件
#DIM 味方番号
#DIM 対象数
CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

対象数 = 0
FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
		CONTINUE
	IF 生者死者 == 1
		;生者のみ
		SIF BATTLE_STATE:味方番号:ＨＰ < 1
			CONTINUE
	ELSEIF 生者死者 == 2
		;死者のみ
		SIF BATTLE_STATE:味方番号:ＨＰ > 0
			CONTINUE
	ENDIF
	IF 自己除外
		SIF 味方番号 == 戦闘行動キャラ
			CONTINUE
	ENDIF
	IF デバフ条件 != ""
		SIF デバフ番号取得(デバフ条件, 味方番号) < 0
			CONTINUE
	ENDIF
	アビテンプレート用_対象保存:対象数 = 味方番号
	対象数 += 1
NEXT
アビテンプレート用_全体フラグ = 1

;対象が無い場合は-1を返す
SIF 対象数 == 0
	RETURN -1

@アビ対象選択テンプレート_味方全体_生者のみ
JUMP アビ対象選択テンプレート_味方全体(1, 0, "")

@アビ対象選択テンプレート_味方全体_死者のみ
JUMP アビ対象選択テンプレート_味方全体(2, 0, "")

@アビ対象選択テンプレート_味方全体_生者のみ_自己以外
JUMP アビ対象選択テンプレート_味方全体(1, 1, "")

@アビ対象選択テンプレート_味方全体_特定デバフ(デバフ種類)
;特定デバフが掛かっている味方すべてが対象に
;性質上、対象がいないことがある。その場合は-1が帰ってくる
;必ず例外処理を行うこと
#DIMS デバフ種類
JUMP アビ対象選択テンプレート_味方全体(1, 0, デバフ種類)


@アビ対象選択テンプレート_味方ランダムＸ体(ランダム回数, 重複無しフラグ = 0, デバフ条件 = "")
#DIM ランダム回数
#DIM 重複無しフラグ
#DIMS デバフ条件
#DIM 対象数
#DIM 味方番号
#DIM 敵対心合計
#DIM 敵対心, 4
#DIM ランダム候補, 100
#DIM ランダム幅, 4

CALL アビテンプレート_対象指定共通処理
SIF RESULT == -1
	RETURN -1

VARSET ランダム候補, -1
対象数 = 0
VARSET ランダム幅, 0

IF 戦闘行動キャラ < 10 || アビ汎用変数:敵対心無視オプション
	;味方が味方を選ぶ場合は敵対心処理は挟まない
	FOR 味方番号, 0, 4
		SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
			CONTINUE
		SIF BATTLE_STATE:味方番号:ＨＰ < 1
			CONTINUE
		IF デバフ条件 != ""
			SIF デバフ番号取得(デバフ条件, 味方番号) < 0
				CONTINUE
		ENDIF
		ランダム候補:対象数 = 味方番号
		対象数 += 1
		ランダム幅:味方番号 = 1
	NEXT
	;対象が無い場合は-1を返す
	SIF 対象数 == 0
		RETURN -1
ELSE
	VARSET 敵対心, -1
	敵対心合計 = 0
	FOR 味方番号, 0, 4
		SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
			CONTINUE
		SIF BATTLE_STATE:味方番号:ＨＰ < 1
			CONTINUE
		IF デバフ条件 != ""
			SIF デバフ番号取得(デバフ条件, 味方番号) < 0
				CONTINUE
		ENDIF
		IF アビテンプレート用_行動中敵対心:味方番号 <= 0
			CALL バフ・デバフ測定(味方番号, "敵対心")
			アビテンプレート用_行動中敵対心:味方番号 = MAX(RESULT, 1)
		ENDIF
		敵対心:味方番号 = アビテンプレート用_行動中敵対心:味方番号
		敵対心合計 += 敵対心:味方番号
	NEXT
	;対象が無い場合は-1を返す
	SIF 敵対心合計 == 0
		RETURN -1
	VARSET LOCAL
	FOR 味方番号, 0, 4
		SIF 敵対心:味方番号 < 0
			CONTINUE
		LOCAL += 敵対心:味方番号 * 100 / 敵対心合計
		LOCAL = MIN(LOCAL, 100)
		VARSET ランダム候補, 味方番号, LOCAL:1, LOCAL
		ランダム幅:味方番号 = LOCAL - LOCAL:1
		LOCAL:1 = MIN(LOCAL, 99)
	NEXT
	
	対象数 = LOCAL
ENDIF

FOR 味方番号, 0, ランダム回数
	アビテンプレート用_対象保存:味方番号 = ランダム候補:(RAND:対象数)
	IF 重複無しフラグ
		ARRAYSHIFT ランダム候補, -1 * ランダム幅:(アビテンプレート用_対象保存:味方番号), -1, SUMARRAY(ランダム幅, 0, アビテンプレート用_対象保存:味方番号)
		対象数 -= ランダム幅:(アビテンプレート用_対象保存:味方番号)
		ランダム幅:(アビテンプレート用_対象保存:味方番号) = 0
		SIF 対象数 <= 0
			BREAK
	ENDIF
NEXT

@アビ対象選択テンプレート_味方ランダムＸ体_重複無し(ランダム回数)
#DIM ランダム回数
JUMP アビ対象選択テンプレート_味方ランダムＸ体(ランダム回数, 1, "")

@アビ対象選択テンプレート_味方ランダム１体_特定デバフ(デバフ種類)
;特定デバフが掛かっている味方のうち、一人が対象に
;性質上、対象がいないことがある。その場合は-1が帰ってくる
;必ず例外処理を行うこと
#DIMS デバフ種類
JUMP アビ対象選択テンプレート_味方ランダムＸ体(1, 0, デバフ種類)
