;時間経過処理
@CHARA_MOVEMENT(追従モード = 0)
#DIM DYNAMIC 追従モード
#DIM 追い出し先
#DIM モード実行中フラグ
#DIM モード番号
#DIM あなた追従中

FOR LOCAL,0,CHARANUM
	;滞在してないキャラはスルー
	SIF CFLAG:LOCAL:滞在期間 == -1
		CONTINUE
	;追従モード時、追従じゃないキャラはスルー
	SIF 追従モード && RCVAR:LOCAL:追従ルーチン中 == 0
		CONTINUE
	;マップ間移動処理
	CALL マップ遷移判定処理(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置)
	IF RESULT
		CFLAG:LOCAL:現在位置 = RESULT
	ENDIF
	; SIF LOCAL == MASTER
	; 	CALL マップ種別変更(LOCAL)
	
;	;睡眠中、MASTERと同室なら追い出す
;	IF LOCAL > 0 && CFLAG:LOCAL:睡眠 && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && !TALENT:LOCAL:恋慕 && FLAG:(300 + CFLAG:MASTER:現在位置)
;		IF CFLAG:LOCAL:衰弱
;			PRINTFORML %CALLNAME:LOCAL%は疲れて眠ってしまった…
;		ELSE
;			PRINTFORML %CALLNAME:LOCAL%に追い出された…
;		ENDIF
;		;追い出し先
;		FOR 追い出し先,0,20
;			;移動可能な部屋が見つかるとそこに移動
;			IF CAN_MOVE_1(CFLAG:MASTER:現在位置, 追い出し先) & 1
;				CFLAG:MASTER:現在位置 = 追い出し先
;				BREAK
;			ENDIF
;		NEXT
;		;うふふリセット
;		CVARSET CFLAG, 317
;		TARGET = 0
;	ENDIF
	
	;BASEの自然変動
	;精力剤
	IF TCVAR:LOCAL:精力剤フラグ
		TCVAR:LOCAL:精液濃縮 = MIN(10, TCVAR:LOCAL:精液濃縮)
	ENDIF
	;MASTERと同じ場所にいないとき
	IF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置 || CFLAG:LOCAL:現在マップ種別 != CFLAG:PLAYER:現在マップ種別
		BASE:LOCAL:ムード = 0
		BASE:LOCAL:理性 = BASE:LOCAL:理性 + TIME_PROGRESS(TFLAG:行動前時刻) * (MAXBASE:LOCAL:理性 + 1000) / ((BASE:LOCAL:理性 + 1000) * 2)
		BASE:LOCAL:理性 = MIN(理性キャップ算出(LOCAL), BASE:LOCAL:理性)
	ENDIF
	;怒りは常に減少する
	BASE:LOCAL:怒り = BASE:LOCAL:怒り - TIME_PROGRESS(TFLAG:行動前時刻) * 5 / (CFLAG:LOCAL:怒り + 1)
	SIF BASE:LOCAL:怒り <= 0
		CFLAG:LOCAL:怒り = 0
	BASE:LOCAL:怒り = MAX(0,BASE:LOCAL:怒り)
	;不機嫌
	SIF CFLAG:LOCAL:不機嫌
		BASE:LOCAL:怒り = MAXBASE:LOCAL:怒り
	;勃起度の自然減少・増加
	CALL 勃起度自然変動(LOCAL)
	;陥没乳首勃起の自然減少
	SIF TCVAR:LOCAL:陥没乳首露出フラグ && !CFLAG:LOCAL:うふふ
		TCVAR:LOCAL:陥没乳首露出フラグ = MAX(0, TCVAR:LOCAL:陥没乳首露出フラグ - TIME_PROGRESS(TFLAG:行動前時刻))
	;常時勃起時、0未満なら入れ直す
	IF (素質切り替えOPTION記憶:陥没乳首_なし == 0 && GETBIT(TALENT:LOCAL:発情期あり, 7) && CFLAG:LOCAL:発情期フラグ < 0) ||  TEQUIP:LOCAL:上半身下着あり < -1
		SIF TCVAR:LOCAL:陥没乳首露出フラグ < 1
			TCVAR:LOCAL:陥没乳首露出フラグ = 150
	ENDIF
	;酩酊の処理
	IF BASE:LOCAL:酩酊 > 0
		BASE:LOCAL:酩酊 = BASE:LOCAL:酩酊 - TIME_PROGRESS(TFLAG:行動前時刻)
		BASE:LOCAL:酩酊 = MAX(0, BASE:LOCAL:酩酊)
	ELSEIF BASE:LOCAL:酩酊 < 0
		BASE:LOCAL:酩酊 = BASE:LOCAL:酩酊 + TIME_PROGRESS(TFLAG:行動前時刻)
		BASE:LOCAL:酩酊 = MIN(0, BASE:LOCAL:酩酊)
	ENDIF
	SIF BASE:LOCAL:酩酊 == 0
		CFLAG:LOCAL:酔いすぎ = 0
	IF TALENT:LOCAL:飲酒拒否 < 0 && INRANGE(BASE:LOCAL:酩酊, 0, 100)
		BASE:LOCAL:酩酊 = 100
	ENDIF
	IF TALENT:LOCAL:飲食不可
		BASE:LOCAL:酩酊 = 0
		CFLAG:LOCAL:酔いすぎ = 0
	ENDIF
	;トランスの処理
	SIF TCVAR:LOCAL:トランス残り時間 > 0
		TCVAR:LOCAL:トランス残り時間 = MAX(0, TCVAR:LOCAL:トランス残り時間 - TIME_PROGRESS(TFLAG:行動前時刻))

	;移動インターバルの減少
	TCVAR:LOCAL:移動インターバル時間 = MAX(TCVAR:LOCAL:移動インターバル時間 - TIME_PROGRESS(TFLAG:行動前時刻), 0)

	;ショッピングインターバルの減少
	TCVAR:LOCAL:移動ルーチンインターバル時間 = MAX(TCVAR:LOCAL:移動ルーチンインターバル時間 - TIME_PROGRESS(TFLAG:行動前時刻), 0)

	;会話累積値　100を超えると会話不能フラグ
	SIF TCVAR:LOCAL:会話累積値 > 100
		TCVAR:LOCAL:話題無しフラグ = 1
	;減衰
	TCVAR:LOCAL:会話累積値 = MAX(TCVAR:LOCAL:会話累積値 - TIME_PROGRESS(TFLAG:行動前時刻) , 0)
	;0になると会話不能解除
	SIF TCVAR:LOCAL:会話累積値 == 0
		TCVAR:LOCAL:話題無しフラグ = 0

	;デートちょっかいの時間経過
	IF TCVAR:LOCAL:デートちょっかい値 > 0
		TCVAR:LOCAL:ちょっかい経過時間 += TIME_PROGRESS(TFLAG:行動前時刻)
		IF TCVAR:LOCAL:ちょっかい経過時間 >= 120 && TCVAR:LOCAL:デートちょっかい値 >= 120
			;ちょっかい時間が2時間を過ぎたらデート中止
			TCVAR:LOCAL:ちょっかい経過時間 = -2
		ELSEIF TCVAR:LOCAL:ちょっかい経過時間 >= TCVAR:LOCAL:デートちょっかい値
			;ちょっかい時間が基準値を過ぎたらデート再開
			TCVAR:LOCAL:ちょっかい経過時間 = -1
		ENDIF
	ENDIF
	
	;身体の清潔度
	LOCAL:1 = CFLAG:LOCAL:風呂
	CFLAG:LOCAL:風呂 += TIME_PROGRESS(TFLAG:行動前時刻)
	IF BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置)
		SIF LOCAL == 0 && LOCAL:1
			PRINTFORMW %CALLNAME:MASTER%は風呂に浸かった
		CFLAG:LOCAL:風呂 = 0
	ENDIF

	IF LOCAL == MASTER
		CALL 精力ゲージ_日中回復処理()
		CONTINUE
	ELSEIF TCVAR:LOCAL:精力剤フラグ
		;精力剤中は精力回復
		BASE:LOCAL:精力 = MAX(BASE:LOCAL:精力 + 100, MAXBASE:LOCAL:精力)
	ENDIF

	;同行フラグ（1時間持続）
	;うふふ中・隠密中・マッサージモードは同行フラグ消えない
	IF CFLAG:LOCAL:うふふ || (CFLAG:LOCAL:隠密 && CFLAG:LOCAL:同行 && CFLAG:MASTER:隠密) || (SAVESTR:ゲームフェイズ管理 == "マッサージモード" && MATCH(TARGET, LOCAL))
		CFLAG:LOCAL:同行 = MAX(CFLAG:LOCAL:同行 - TIME_PROGRESS(TFLAG:行動前時刻),0)
		SIF !CFLAG:LOCAL:同行
			CFLAG:LOCAL:同行 = 1
	ELSE
		CFLAG:LOCAL:同行 = MAX(CFLAG:LOCAL:同行 - TIME_PROGRESS(TFLAG:行動前時刻),0)
	ENDIF
	;デート残り時間は関係なく時間経過
	IF CFLAG:LOCAL:デート残り時間 > 0
		CFLAG:LOCAL:デート残り時間 = MAX(CFLAG:LOCAL:デート残り時間 - TIME_PROGRESS(TFLAG:行動前時刻),0)
		IF !CFLAG:LOCAL:デート残り時間 || TIME >= 1260
			;デート採点処理
			CALL デート採点処理(LOCAL)
			;うふふなどの途中の場合、同行をセット
			SIF CFLAG:LOCAL:うふふ || (CFLAG:LOCAL:隠密 && CFLAG:LOCAL:同行 && CFLAG:MASTER:隠密) || (SAVESTR:ゲームフェイズ管理 == "マッサージモード" && MATCH(TARGET, LOCAL))
				CFLAG:LOCAL:同行 = 1
		ENDIF
	ENDIF

	あなた追従中 = 0
	IF 追従中キャラ(LOCAL)
		CFLAG:LOCAL:現在位置 = CFLAG:MASTER:現在位置
		CFLAG:LOCAL:現在マップ種別 = CFLAG:PLAYER:現在マップ種別
		あなた追従中 = 1
	ENDIF

	;うふふ中ではない、かつ同行終わったら隠密消去
	SIF !CFLAG:LOCAL:同行 && !CFLAG:LOCAL:うふふ && LOCAL > 0 && 連れ込みパターン == ""
		CFLAG:LOCAL:隠密 = 0

	;性欲
	SIF !CFLAG:LOCAL:睡眠
		CALL 性欲ゲージ増加(LOCAL)

	;現在何らかのモードに入っているか
	モード実行中フラグ = 0
	FOR モード番号, 0, DT_ROW_LENGTH("体位モードデータベース")
		SIF 系統リスト_アイテム系(DT_CELL_GETS("体位モードデータベース", モード番号, "モード名"))
			CONTINUE
		IF DT_CELL_GET("体位モードデータベース", モード番号, "実行キャラ") == LOCAL || DT_CELL_GET("体位モードデータベース", モード番号, "対象キャラ") == LOCAL
			モード実行中フラグ = 1
			BREAK
		ENDIF
	NEXT

	;遊びに誘ったフラグ（１時間持続）
	IF CFLAG:LOCAL:一緒に遊ぶ
		CFLAG:LOCAL:一緒に遊ぶ = MAX(CFLAG:LOCAL:一緒に遊ぶ - TIME_PROGRESS(TFLAG:行動前時刻),0)
		SIF CFLAG:LOCAL:うふふ || モード実行中フラグ
			;うふふ中、あるいは何らかのモード実行中は遊ぶが途切れない
			CFLAG:LOCAL:一緒に遊ぶ = MAX(CFLAG:LOCAL:一緒に遊ぶ, 1)
		IF !CFLAG:LOCAL:一緒に遊ぶ
			PRINTFORML %CALLNAME:LOCAL%は遊びに満足したようだ。
		ENDIF
	ENDIF

	;Add ahyanosia
	;ゆきずりセックス処理
	IF 性癖素質:LOCAL:ゆきずりセックス > 0 && !CFLAG:LOCAL:うふふ
		CALL ゆきずりセックス判定(LOCAL)
		;性行為中は移動しない
		SIF IS_ゆきずりセックス中(LOCAL)
			CONTINUE
	ENDIF

	;野外オナニー処理
	IF 性癖素質:LOCAL:野外オナニー > 0 && !CFLAG:LOCAL:うふふ
		CALL 野外オナニー判定(LOCAL)
		;オナニー中は移動しない
		SIF IS_野外オナニー中(LOCAL)
			CONTINUE
	ENDIF

	IF CFLAG:LOCAL:酔いすぎ == 1 && !CFLAG:LOCAL:睡眠 && !TCVAR:LOCAL:泥酔眠姦
		;泥酔時は該当処理に飛ばすため分岐
		CALL CHARA_MOVEMENT_T(LOCAL, 追従モード)
		CONTINUE
	ENDIF
	SIF あなた追従中
		CONTINUE
	SIF CFLAG:LOCAL:うふふ
		CONTINUE
	SIF CFLAG:LOCAL:飲み会
		CONTINUE
	SIF CFLAG:LOCAL:一緒に遊ぶ
		CONTINUE
	SIF モード実行中フラグ && !CFLAG:LOCAL:睡眠
		CONTINUE
	SIF CFLAG:LOCAL:添い寝フラグ
		CONTINUE
	SIF FLAG:イベントうふふ中フラグ
		CONTINUE
	SIF TCVAR:LOCAL:デートちょっかい値 > 0
		CONTINUE
	;部屋移動
	CALL CHARA_MOVEMENT_T(LOCAL, 追従モード)
NEXT

;追従キャラを動かす
IF 追従モード == 0
	CALL CHARA_MOVEMENT(1)
ELSE
	RETURN 0
ENDIF

;ここでキャラ同士うふふの判定を行う
;これ汎用化したほうがいいんだけどとりあえずキャラ同士うふふでしか使わないのでベタ打ち
FOR LOCAL,1,CHARANUM
	IF 移動スケジュール文字列:LOCAL:(スケジュール状態:LOCAL:現在スケジュール):スケジュール == "自室でうふふ"
		IF CFLAG:LOCAL:デート
			IF CFLAG:LOCAL:現在位置 == CFLAG:(ABS(CFLAG:LOCAL:デート) - 100):現在位置 && CFLAG:LOCAL:現在マップ種別 == CFLAG:(ABS(CFLAG:LOCAL:デート) - 100):現在マップ種別
				CFLAG:LOCAL:キャラ同士うふふ = 1
				CALL CLOTHES_CHANGE(LOCAL, "全裸")
			ENDIF
		ENDIF
	ENDIF
NEXT

LOCAL:1 = 滞在キャラ数算出関数(2)

FOR LOCAL, 1, マップ部屋番号上限
	SIF LOCAL == 20
		CONTINUE
	SIF LOCAL > 2000
		CONTINUE
	IF 存在しない移動先(0, LOCAL) == 0
		雑務ゲージ:LOCAL = MIN(雑務ゲージ:LOCAL + (TIME_PROGRESS(TFLAG:行動前時刻) * LOCAL:1 / 120), 10000)
	ENDIF
NEXT

@CHARA_MOVEMENT_ACTIVETIME(ARG)
#DIM 寝る
#DIM 起きる

CALL CHARA_ROUTINE_STATUS(ARG, "就寝時刻")
寝る = RESULT
CALL CHARA_ROUTINE_STATUS(ARG, "起床時刻")
起きる = RESULT

;時間設定ない場合は22時寝の8時起き
SIF !寝る
	寝る = 1320 + TCVAR:ARG:行動時間ゆらぎ
SIF !起きる
	起きる = 480 + TCVAR:ARG:行動時間ゆらぎ

;泥酔時は常時寝る
SIF CFLAG:ARG:酔いすぎ == 1
	寝る = 0
;体力限界時は眠る
SIF CFLAG:ARG:体力限界 == 1
	寝る = 0
;泥酔眠姦とかに使う分岐
SIF TCVAR:ARG:言いなり == 1
	寝る = 0


;PLAYERがうふふ、同じ部屋にいる、隠密状態が同じ場合は寝ない
SIF CFLAG:MASTER:うふふ && CFLAG:MASTER:隠密 == CFLAG:ARG:隠密 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:ARG:現在マップ種別
	RETURN 0

RETURN TIME < 起きる || TIME > 寝る


;@CHARA_MOVEMENT_MOVERATE(ARG)
;;キャラごとの移動率補正呼び出し
;RETURN 90 + CFLAG:ARG:移動率補正


@CHARA_MOVEMENT_KOUHO(ARG,ARG:1)
#DIM 移動先候補
#DIM 移動キャラ

移動先候補 = ARG
移動キャラ = ARG:1

;よく行く場所はあらゆる条件を無視して確率上昇

SIF CFLAG:移動キャラ:よく行く場所 == 移動先候補
	RETURN 200

SELECTCASE 移動先候補
	CASE 7,9,10,13
		;男利用なら通す
		IF 施設利用性別(移動キャラ) == 性別_男性
			RETURN 100
		ELSE
			RETURN 0
		ENDIF
	CASE 8,11,12,14
		;女利用なら通す
		IF 施設利用性別(移動キャラ) == 性別_女性
			RETURN 100
		ELSE
			RETURN 0
		ENDIF
	CASE 20, 2000 TO 2020
		;基本自室には来ない（後々恋慕状態だと来るように再設定すること）
		RETURN 0
	;ほかはまんべんなく移動する
	CASEELSE
		RETURN 100
ENDSELECT

;ゲーム内現在時刻をゲーム開始時からの経過した分で返す
@NOW()
#FUNCTION
RETURNF TIME + DAY * 24 * 60

@TIME_PROGRESS(ARG)
#FUNCTION
RETURNF TIME + DAY * 1440 - ARG

@CHARA_SLEEP, ARG
#DIM 睡眠場所指定
#DIM 睡眠マップ
#DIM 睡眠場所
#DIM 部屋割
#DIM TARGET保存
#DIM COM保存
#DIM 泥酔勘違い

VARSET LOCAL

泥酔勘違い = 0

;現在、キャラごとの個室がないのでまるごとオミット、寝る時にどっか行くだけ

;IF (CAN_MOVE_1(CFLAG:MASTER:現在位置, CFLAG:ARG:現在位置) == 2 || CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置) && !CFLAG:MASTER:睡眠
;	IF CFLAG:ARG:現在位置 == CFLAG:ARG:311
;		IF TALENT:ARG:恋慕 == 1
;			PRINTFORMW %CALLNAME:ARG%は眠っ\@CFLAG:ARG:睡眠 ? ている # た\@
;			LOCAL = 2
;		ELSE
;			PRINTFORMW %CALLNAME:ARG%は眠そうにしている
;			LOCAL = 1
;		ENDIF
;	ELSE
;		IF CFLAG:ARG:衰弱 && TALENT:ARG:恋慕 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
;			PRINTFORMW %CALLNAME:ARG%を%GETPLACENAME(CFLAG:ARG:現在位置)%から%GETPLACENAME(CFLAG:ARG:311)%へ運びました
;			LOCAL = 3
;		ELSE
;			PRINTFORMW %CALLNAME:ARG%は%GETPLACENAME(CFLAG:ARG:現在位置)%から%GETPLACENAME(CFLAG:ARG:311)%に戻りました
;			LOCAL = 0
;		ENDIF
;	ENDIF
;ENDIF

IF !CFLAG:ARG:睡眠
	CALL CHARA_ROUTINE_STATUS(ARG, "就寝場所")
	睡眠場所指定 = RESULT
	IF 睡眠場所指定
		睡眠マップ = RESULT:1
		睡眠場所 = RESULT:2
	ENDIF
	CFLAG:ARG:同行 = 0
	CFLAG:ARG:睡眠 = 1
	CFLAG:ARG:うふふ = 0
	CALL 性欲ゲージ減少処理(ARG)
	TCVAR:ARG:初うふふフラグ = 0

	IF CFLAG:ARG:デート == MASTER + 100
		CALL デート採点処理(ARG)
	ENDIF

	IF CFLAG:ARG:酔いすぎ == 1
		IF CFLAG:MASTER:酔いすぎ != 1 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
			;あなたがいる場合は部屋へと運ぶ
			LOCAL = キャラクター部屋検索(ARG)
			IF 自由利用BEDROOM(CFLAG:ARG:現在マップ種別, CFLAG:ARG:現在位置)
				PRINTFORMW %CALLNAME:MASTER%は疲れ果ててしまった%CALLNAME:ARG%をベッドに寝かせ、布団を掛けた。
			ELSEIF CFLAG:ARG:現在位置 == LOCAL && CFLAG:ARG:現在マップ種別 == 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:MASTER%は酔いつぶれてしまった%CALLNAME:ARG%をベッドに寝かせ、布団を掛けた。
			ELSEIF GETBIT(TALENT:ARG:性別, 0) && TCVAR:ARG:レディキラー >= 1
				;書いた人は男あなたで男が酔いつぶれた時にも選択肢が出てしまうのが嫌で女に限定した。あなたの性別嗜好が取れる手段があるならそれで。
				;女前提なら地の文でも女性的な表現使えて楽！
				CFLAG:ARG:現在位置 = LOCAL
				CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:ARG%は酔いつぶれてしまったようだ……。
				PRINTL
				PRINTBUTTONLC "[0] 紳士的に部屋まで送る", 0
				PRINTBUTTONLC "[1] 送り狼になる", 1
				BINPUT
				IF RESULT == 0
					PRINTL
					PRINTFORMW %CALLNAME:MASTER%は酔いつぶれてしまった%CALLNAME:ARG%を部屋へと送ってあげた……。
				ELSE
				;泥酔レイプ！野獣と化したオーナー
					CALL 飲み会終了処理()
					PRINTL
					PRINTFORMW %CALLNAME:MASTER%は酔いつぶれてしまった%CALLNAME:ARG%に肩を貸し、部屋まで送ってあげることにした。
					PRINTL
					PRINTFORMW 酔いに火照った体に腕を回し、半身に%CALLNAME:ARG%の柔らかさを感じながらゆっくりと部屋へ歩みを進める……。
					PRINTL
					IF TALENT:ARG:恋慕 >= 1
						PRINTFORML %CALLNAME:PLAYER%の前で油断しきり、ベロベロになるまで酔いつぶれた%CALLNAME:ARG%は、%CALLNAME:PLAYER%が体を抱くと嬉しそうに体を摺り寄せてくる。
					ELSEIF TALENT:ARG:恋人持ち == -1 || TALENT:ARG:恋人持ち ==1 ||TALENT:ARG:恋人持ち == 2
						IF TALENT:ARG:恋人持ち == -1
							PRINTFORM 想い人
						ELSEIF TALENT:ARG:恋人持ち == 1
							PRINTFORM 恋人
						ELSEIF TALENT:ARG:恋人持ち == 2
							PRINTFORM 伴侶
						ENDIF
						PRINTFORML がいるというのに、%CALLNAME:MASTER%の前で酔いつぶれるとはいささか不用心だ。
					ELSEIF 知識素質:高貴 > 0
						PRINTFORML やんごとない身分でありながら、%CALLNAME:MASTER%の前で酔いつぶれるとはいささか不用心だ。
					ELSEIF TALENT:ARG:性別嗜好 == 1 && TALENT:MASTER:性別 == 2
						PRINTFORML %CALLNAME:ARG%が男嫌いと言えど、こうして酔いつぶれてしまっては可愛いものだ。
					ELSEIF TALENT:ARG:年齢層 == 10 || (BASE:ARG:年齢 <= 15 && BASE:ARG:年齢 != 0)
						PRINTFORML 年齢的にダメだと分かっているだろうに、勧めるままに酒を飲んでしまうなんて……%TEXTR("悪い/いけない")%子だ。
					ELSE
						PRINTFORML 酔いに浮かされ、%CALLNAME:PLAYER%に体を預けて夢心地な%CALLNAME:ARG%は、
					ENDIF
					PRINTFORMW 「転ばないように」などと理由をつけて%CALLNAME:MASTER%が%TEXTR("際どい部分を触っ/抱き寄せる腕でこっそり胸を揉みしだい/腰に回した手でこっそり尻を触っ")%ても%TEXTR("、何をされているのかすらわかっていない様子だ/嫌がる様子はない")%。
					PRINTL
					IF (TALENT:ARG:恋人持ち == -1 || TALENT:ARG:恋人持ち ==1 || TALENT:ARG:恋人持ち == 2 || TALENT:ARG:恋人持ち == 3) && TALENT:ARG:恋慕 < 1 && RAND:3 == 0
						PRINTFORML 手のひらで介抱の役得を楽しみながら鍵を開けて薄暗い部屋に入り、%CALLNAME:ARG%をベッドの上へと横たえると、
						PRINTFORMW %CALLNAME:ARG%はぼんやりとした目で%CALLNAME:MASTER%に甘えてくる。
						PRINTL
						PRINTFORML どうやら目の前の相手が誰なのかもわからないほどに酔いつぶれた%CALLNAME:ARG%は、
						PRINTFORM 介抱している%CALLNAME:MASTER%を
						IF TALENT:ARG:恋人持ち == -1
							PRINTFORM 意中の相手
						ELSEIF TALENT:ARG:恋人持ち == 1
							PRINTFORM 恋人
						ELSEIF TALENT:ARG:恋人持ち == 2
							PRINTFORM 伴侶
						ELSEIF TALENT:ARG:恋人持ち == 3
							PRINTFORM 既にいないはずのかつての伴侶
						ENDIF
						PRINTFORMW と勘違いしているようだ……。
						PRINTL
						PRINTFORMW 前後不覚に酔いつぶれたうえ、思い込みでガードがゆるゆるになった%CALLNAME:ARG%の姿に思わず%CALLNAME:MASTER%の口角が上がる。
						PRINTL
						PRINTFORMW ──またとない機会だ。利用させてもらおう。
						PRINTL
						PRINTFORM %CALLNAME:MASTER%は%CALLNAME:ARG%の、寝言にも似た胡乱な言葉に
						IF TALENT:ARG:恋人持ち == -1
							PRINTFORM 意中の相手のふりをして
						ELSEIF TALENT:ARG:恋人持ち == 1
							PRINTFORM 恋人のふりをして
						ELSEIF TALENT:ARG:恋人持ち == 2
							PRINTFORM 伴侶のふりをして
						ENDIF
						PRINTFORML 適当に相槌を打ちながら、
						PRINTFORMW ひとときのラブラブ%TEXTR("気分/交尾/セックス")%を楽しませてもらうべく%CALLNAME:ARG%をそっと押し倒した……。
						CFLAG:MASTER:現在位置 = LOCAL
						CFLAG:MASTER:現在マップ種別 = 部屋検索_マップ種別
						CFLAG:ARG:現在位置 = LOCAL
						CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
						CALL TARGET_SET()
						;EVENTCOMENDからそのままもってきた。正直よくわかってない。
						LOCALS '= CSTR:ARG:コスプレ前の服
						LOCALS:1 '= CSTR:ARG:着せ替え服
						CSTR:ARG:コスプレ前の服 '= LOCALS
						CALL CLOTHES_CHANGE(ARG, LOCALS:1)
						;うふふに移行
						CFLAG:ARG:泥酔ックス回数 += 1
						泥酔勘違い = 1
						TCVAR:ARG:泥酔眠姦 = 1
						TCVAR:ARG:言いなり = 1
						CFLAG:MASTER:うふふ = 1
						CALL うふふ開始キャラ処理(ARG, "泥酔姦")
;						TSTR:むりやりごっこ時シチュ保存 '= CSTR:TARGET:うふふシチュ記録
						CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>酔いつぶれた%CALLNAME:ARG%の勘違いに付け込み、想い人のふりをしてその体をこっそり楽しませてもらった</font><img src='えっちハート'>","うふふ")
						CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>酔いつぶれて%CALLNAME:MASTER%を想い人と勘違いし、体を許してしまった</font><img src='えっちハート'>","うふふ")
						TIME += 10
					ELSE
						PRINTFORML 手のひらで介抱の役得を楽しみながら鍵を開けて部屋に入り、%CALLNAME:ARG%をベッドの上へと横たえる。
						IF 知識素質:高貴 > 0
							PRINTW
							PRINTFORML アルコールの匂いを振りまきながらシーツの上に力なくごろんと転がる、本来の身分では触れることすら許されないはずの、%TEXTR("高貴な/貴い")%血筋に連なる%CALLNAME:ARG%の玉体。
						ELSEIF アイドル判定(ARG)
							PRINTW
							PRINTFORML アルコールの匂いを振りまきながらシーツの上に力なくごろんと転がる、アイドルとして数多のファンを魅了すべく磨かれ、しかしその誰も触れることすら叶わない%CALLNAME:ARG%の体。
						ELSEIF TALENT:ARG:恋人持ち == -1 || TALENT:ARG:恋人持ち ==1 || TALENT:ARG:恋人持ち == 2 || TALENT:ARG:恋人持ち == 3
							PRINTFORM アルコールの匂いを振りまきながらシーツの上に力なくごろんと転がる、
							IF TALENT:ARG:恋人持ち == -1
								PRINTFORML 意中の相手に捧げたかっただろう%CALLNAME:ARG%のカラダ。
							ELSEIF TALENT:ARG:恋人持ち == 1
								PRINTFORML 本来恋人%TEXTR("だけが味わえる/にしか許すつもりなどない/しか楽しめない")%はずの%CALLNAME:ARG%のカラダ。
							ELSEIF TALENT:ARG:恋人持ち == 2 && TALENT:ARG:恋慕 != 2
								PRINTFORML 伴侶にだけ捧げることを誓ったはずの人妻のカラダ。
							ELSEIF TALENT:ARG:恋人持ち == 3
							;未亡人なら熟れとるやろ(適当)
								PRINTFORML 男日照りだっただろう未亡人の熟れたカラダ。
							ENDIF
						ELSEIF 十天衆判定(ARG)
							PRINTW
							PRINTFORML アルコールの匂いを振りまきながらシーツの上に力なくごろんと転がる、十天衆の一人に数えられる%CALLNAME:ARG%の、全空最強の力を秘めた体。
						ENDIF
						PRINTFORMW 横になるや気持ちよさそうにすやすやと寝息をたてはじめた%CALLNAME:ARG%の無防備な姿は、まるで%CALLNAME:MASTER%を誘っているようだ……。
						PRINTL
						SELECTCASE RAND:4
						CASE 0
							PRINTFORMW 酔いつぶれて当分は目覚めない今なら、この無防備な体を%TEXTR("少々/こっそり")%%TEXTR("楽しませて/つまみ食いさせて")%貰ったとしてもきっとバレやしないだろう。
							PRINTL
							PRINTFORMW %CALLNAME:MASTER%は寝込みを襲うスリルに胸を躍らせながら、寝息を乱さないよう静かに%CALLNAME:ARG%に覆いかぶさった……。
						CASE 1
							PRINTFORMW %CALLNAME:MASTER%の前で酔いつぶれた%CALLNAME:ARG%が悪いのだ。これでは何をされようとも文句は言えまい。
							PRINTL
							PRINTFORMW 脳裏で身勝手に欲望を正当化しながら%CALLNAME:MASTER%はほくそ笑み、油断して酔いつぶれるとどうなるか、その体にたっぷりと%TEXTR("教えて/刻み付けて")%やるべく%CALLNAME:ARG%に覆いかぶさった……。
						CASE 2
							PRINTFORMW ──きっとこのままでは寝苦しいだろう。服を脱がしてやるのは、あくまで善意の「介抱」だ。
							;卑猥は一切ない。いいね？
							PRINTL
							PRINTFORMW 脳裏でわざとらしく理由をつけながら%CALLNAME:MASTER%はそっと%CALLNAME:ARG%の服に手をかけ、その下にある柔らかな肢体へと滾る欲望を向けた……。
						CASE 3
							PRINTFORMW ──いや、これはもう誘っているに違いない。ならばこの据え膳、手を出さないほうがかえって失礼というものだろう。
							PRINTL
							PRINTFORMW 脳裏で身勝手な解釈をつけた%CALLNAME:MASTER%はほくそ笑み、寝息を乱さないよう静かに%CALLNAME:ARG%に覆いかぶさった……。
						ENDSELECT
						CFLAG:MASTER:現在位置 = LOCAL
						CFLAG:MASTER:現在マップ種別 = 部屋検索_マップ種別
						CFLAG:ARG:現在位置 = LOCAL
						CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
						CALL TARGET_SET()
						;EVENTCOMENDからそのままもってきた。正直よくわかってない。
						;むりやりうふふごっこのとこ
						LOCALS '= CSTR:ARG:コスプレ前の服
						LOCALS:1 '= CSTR:ARG:着せ替え服
						CALL ゲームフェイズ変更("むりやりうふふモード")
						CSTR:ARG:コスプレ前の服 '= LOCALS
						CALL CLOTHES_CHANGE(ARG, LOCALS:1)
						;うふふに移行
						CFLAG:ARG:泥酔ックス回数 += 1
						TCVAR:ARG:泥酔眠姦 = 1
						TCVAR:ARG:言いなり = 1
						CFLAG:ARG:睡眠 = 1
						CALL うふふ開始キャラ処理(ARG, "泥酔眠姦")
;						TSTR:むりやりごっこ時シチュ保存 '= CSTR:TARGET:うふふシチュ記録
						CFLAG:MASTER:うふふ = 1
						CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>酔いつぶれた%CALLNAME:ARG%を部屋に送り、無防備に眠るその体をこっそり楽しんだ</font><img src='えっちハート'>","うふふ")
						CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>酔いつぶれて寝ているあいだに%CALLNAME:MASTER%に襲われてしまった</font><img src='えっちハート'>","うふふ")
						TIME += 10
					ENDIF
				ENDIF
			ELSE
				CFLAG:ARG:現在位置 = LOCAL
				CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:MASTER%は酔いつぶれてしまった%CALLNAME:ARG%を部屋へと送りました。
			ENDIF
		ENDIF
	ELSEIF CFLAG:ARG:体力限界 && TALENT:ARG:分身 == 0
		;部屋へ戻る、分身は移動しない
		IF CFLAG:MASTER:酔いすぎ != 1 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
			;あなたがいる場合は部屋へと運ぶ
			LOCAL = キャラクター部屋検索(ARG)
			IF 自由利用BEDROOM(CFLAG:ARG:現在マップ種別, CFLAG:ARG:現在位置)
				PRINTFORMW %CALLNAME:MASTER%は疲れ果ててしまった%CALLNAME:ARG%をベッドに寝かせ、布団を掛けた。
			ELSEIF CFLAG:ARG:現在位置 == LOCAL && CFLAG:ARG:現在マップ種別 == 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:MASTER%は疲れ果ててしまった%CALLNAME:ARG%をベッドに寝かせ、布団を掛けた。
			ELSE
				CFLAG:ARG:現在位置 = LOCAL
				CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:MASTER%は疲れ果ててしまった%CALLNAME:ARG%を部屋へと運びました。
			ENDIF
		ELSE
			;同室じゃない場合は勝手に戻る
			CFLAG:ARG:現在位置 = キャラクター部屋検索(ARG)
			CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
		ENDIF
	ELSEIF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:ARG:現在マップ種別
		IF 睡眠場所指定
			IF CFLAG:MASTER:現在位置 == 睡眠場所 && CFLAG:MASTER:現在マップ種別 == 睡眠マップ
				PRINTFORMW %CALLNAME:ARG%はこのまま%GETPLACENAME(睡眠マップ, 睡眠場所)%で眠るようだ。
				TRYCALLFORM KOJO_EVENT_この場おやすみ_{NO:ARG}(ARG)
			ELSE
				PRINTFORMW %CALLNAME:ARG%は%GETPLACENAME(睡眠マップ, 睡眠場所)%で眠るようだ。
				TRYCALLFORM KOJO_EVENT_移動おやすみ_{NO:ARG}(ARG)
			ENDIF
		ELSE
			部屋割 = キャラクター部屋検索(ARG)
			IF CFLAG:MASTER:現在位置 == 部屋割 && CFLAG:MASTER:現在マップ種別 == 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:ARG%はこのまま部屋で眠るようだ。
				TRYCALLFORM KOJO_EVENT_この場おやすみ_{NO:ARG}(ARG)
			ELSE
				PRINTFORMW %CALLNAME:ARG%は部屋に戻って眠るようだ。
				TRYCALLFORM KOJO_EVENT_移動おやすみ_{NO:ARG}(ARG)
			ENDIF
		ENDIF
	ENDIF
	
	IF CFLAG:ARG:酔いすぎ == 1 || CFLAG:ARG:体力限界
		;泥酔時は基本そのまま
		;体力限界時はあなたが同じ部屋にいるはずなので運んだ後のはず
		GOTO リセット位置
	ELSEIF 睡眠場所指定
		;寝る場所指定がある場合はそこへ
		CFLAG:ARG:現在位置 = 睡眠場所
		CFLAG:ARG:現在マップ種別 = 睡眠マップ
		GOTO リセット位置
	ELSE
		;指定なしの場合、場所を宿泊エリアの個室に設定する
		CFLAG:ARG:現在位置 = キャラクター部屋検索(ARG)
		CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
	ENDIF

	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:ARG:現在マップ種別
		;もし同じ部屋にあなたがいるなら追い出される
		IF 陥落チェック(ARG) == 0
			PRINTFORMW %CALLNAME:MASTER%は追い出されてしまった……
			;モードもリセット
			CALL 特定キャラモードリセット(MASTER)
			IF CFLAG:ARG:現在位置 == 303
				CFLAG:MASTER:現在位置 = 307
			ELSEIF 部屋検索_マップ種別 == 999
				CFLAG:PLAYER:現在マップ種別 = 0
				IF 施設改造度:20:0
					CFLAG:MASTER:現在位置 = 2001
				ELSE
					CFLAG:MASTER:現在位置 = 20
				ENDIF
				宿泊エリア表示種別 = 0
				CALL 宿泊エリア処理
			ELSEIF 部屋検索_マップ種別 == 2
				CFLAG:PLAYER:現在マップ種別 = 2
				CFLAG:MASTER:現在位置 = 1
				宿泊エリア表示種別 = 2
				CALL 宿泊エリア処理
			ENDIF
		ENDIF
	ENDIF

	$リセット位置
	;服をリセット
	IF !CFLAG:ARG:体力限界
		CALL CLOTHES_RESET(ARG)
		CALL CLOTHES_RESET_TRAIN(ARG)
	ENDIF
	;二日酔い処理のため、酩酊が残ってたら絶対値を負に
	BASE:ARG:酩酊 = BASE:ARG:酩酊 * -1
	CALL 特定キャラモードリセット(ARG)
ENDIF

CFLAG:ARG:同行 = 0
IF 泥酔勘違い == 1
	CFLAG:ARG:睡眠 = 0
ELSEIF TCVAR:ARG:泥酔眠姦 == 1
	CFLAG:ARG:睡眠 = 1
ELSE
	CFLAG:ARG:睡眠 = 1
	CFLAG:ARG:うふふ = 0
	TCVAR:ARG:初うふふフラグ = 0
ENDIF
CFLAG:ARG:飲み会 = 0
CALL 性欲ゲージ減少処理(ARG)


;SIF LOCAL == 3
;	CFLAG:MASTER:現在位置 = CFLAG:ARG:311
;施錠
;FLAG:(300 + CFLAG:ARG:現在位置) = 1

@CHARA_AWAKE, ARG


IF CFLAG:ARG:睡眠 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:ARG:現在マップ種別 == CFLAG:MASTER:現在マップ種別
	IF ARG > 0
		IF MODE_存在判定_完全一致("ＶＡ挿入系", PLAYER, ARG)
			;挿入時に目覚めたらうふふへ派生
			DRAWLINE
			PRINTFORML 挿入中に%CALLNAME:ARG%が目を覚ましてしまった……！
			PRINTFORML %CALLNAME:PLAYER%は寝起きで混乱する%CALLNAME:ARG%相手に腰を動かし、なし崩しでセックスに持ち込んだ…
			CFLAG:ARG:睡眠 = 0
			IF GET_TARGETNUM() > 1
				CFLAG:PLAYER:隠密 = 1
				CFLAG:ARG:隠密 = 1
			ENDIF
			CFLAG:PLAYER:うふふ = 1
			TCVAR:ARG:挨拶フラグ = 1
			CALL うふふ開始キャラ処理(ARG, "睡姦からなし崩し")
			FLAG:モード管理 = 0
			DRAWLINE
			FORCEWAIT
			RETURN 0
		ELSEIF MODE_存在判定_ターゲット側("ALL", ARG)
			PRINTFORML %CALLNAME:ARG%が目を覚ましそうだ……！
			PRINTFORML %CALLNAME:PLAYER%は慌てて%CALLNAME:ARG%から離れた……
			FORCEWAIT
		ENDIF
		PRINTFORML %CALLNAME:ARG%が目を覚ました
		WAIT
	ENDIF
	;同じ場所で起きた時は３分のインターバル
	TCVAR:ARG:移動インターバル時間 = 3
	;おはよう口上
	TRYCALLFORM KOJO_EVENT_おはよう_{NO:ARG}(ARG)
	CALL 特定キャラモードリセット(ARG)
ENDIF

IF CFLAG:ARG:睡眠
	CALL CHARA_MOVEMENT_ACTIVETIME(ARG)
	;睡眠時間
	IF RESULT
		CALL CHARA_SLEEP, ARG
		RETURN 0
	ENDIF
	IF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置 || CFLAG:ARG:現在マップ種別 != CFLAG:MASTER:現在マップ種別
		PRINTFORML %CALLNAME:ARG%が目を覚ましたようだ。
		CALL CHARA_ROUTINE_STATUS(ARG, "初期位置")
	ENDIF
	;起きても酩酊が残ってるなら二日酔い
	SIF BASE:ARG:酩酊
		CFLAG:ARG:酔いすぎ = 2
	CALL 特定キャラモードリセット(ARG)
ENDIF
CFLAG:ARG:睡眠 = 0
RETURN 1



;ARG 移動させるキャラ番号
@CHARA_MOVEMENT_T(ARG, 追従モード = 0)
#DIM 追従モード
;活動時間　睡眠時間ならRESULTに真、活動時間ならRESULTに偽を代入
CALL CHARA_MOVEMENT_ACTIVETIME(ARG)

;睡眠時間
IF RESULT
	CALL CHARA_SLEEP, ARG
	RETURN 0
;活動時間、CFLAG:睡眠が立っていれば起床処理
ELSE
	;日中睡眠フラグが立ってると覚醒時間でも寝てる
	SIF TCVAR:ARG:日中睡眠
		RETURN 0
	CALL CHARA_AWAKE, ARG
	SIF RESULT == 0
		RETURN 0
ENDIF

;スケジュールに基づいた移動処理
IF TCVAR:ARG:移動インターバル時間 == 0 || 追従モード
	CALL MOVE_ON_SCHEDULE(ARG, 追従モード)
	IF RESULT > 0
		;RETURNが１＝移動したという意味なので3分のインターバル
		TCVAR:ARG:移動インターバル時間 = 3
	ENDIF
ENDIF

RETURN 1
