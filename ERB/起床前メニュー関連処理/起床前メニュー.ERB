@SHOW_SHOP
#DIM コマンド表示数
#DIM 消費ZP
;奴隷購入ページフラグ
SAVESTR:ゲームフェイズ管理 = 
TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000
CALL モブ観光客算出


DO

CALL リゾートランク上昇チェック()
CALL イベント発生条件チェック()
CALL 従業員人数チェック()
CALL ユニーク客人数チェック()

DRAWLINE
LOCALS = %月算出()%
PRINTFORM {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)
PRINTFORML （所持金：%NUM_FORMAT(MONEY)%%MONEYLABEL% 所持ZP：%ZP所持量全文字列()%）
PRINTFORM %リゾート名称%人気度：%リゾート人気度全文字列()%
CALL 人気度上限算出()
IF リゾート人気度 >= RESULT / 1000
	SETCOLOR 0xC0C070
	PRINT 　　　上限到達
	RESETCOLOR
ENDIF
PRINTL
PRINTFORML リゾートランク：%リゾートランク文字列:リゾートランク%
SETCOLOR カラーパレット("えっちな色")
PRINT フィールド効果：
RESETCOLOR
LOCALS = <font color='#%カラーパレット_HTML("えっちな色")%'>
LOCAL:1 = 0
消費ZP = 0
FOR LOCAL, 0, 50
	SIF ERDNAME(フィールド展開, LOCAL) == ""
		BREAK
	SIF フィールド展開:LOCAL <= 0
		CONTINUE
	CALLFORM フィールド効果解説_%ERDNAME(フィールド展開, LOCAL)%
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += @"<nonbutton title='%TSTR:ツールチップ受渡%'>[%RESULTS%]</nonbutton>"
	ELSE
		LOCALS += @"<nonbutton>[%RESULTS%]</nonbutton>"
	ENDIF
	CALLFORM フィールド効果解説_%ERDNAME(フィールド展開, LOCAL)%
	消費ZP += RESULT
	LOCAL:1 ++
NEXT
IF LOCAL:1 == 0
	LOCALS = なし
ELSE
	LOCALS += @"　消費ZP/1日：{消費ZP, 3}ZP"
	LOCALS += "</font>"
ENDIF
HTML_PRINT LOCALS, 1
PRINTL
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	CALL スリーサイズ変動適用(LOCAL)
	
	SIF CFLAG:LOCAL:長期雇用
		CONTINUE
	SIF CFLAG:LOCAL:滞在期間 == 1
		LOCAL:1 += 1
NEXT
PRINTFORML ネームドキャラ滞在人数：%NUM_FORMAT(滞在キャラ数), 3%人 / 最大%NUM_FORMAT(滞在キャラ上限), 3%人　　　本日帰宅予定者：%NUM_FORMAT(LOCAL:1), 3%人
PRINTFORM 今日のモブ観光客人数：%NUM_FORMAT(モブ観光客数), 5%人　　　　
PRINTFORML 雇用中従業員キャラ人数：%NUM_FORMAT(従業員人数), 3%人
DRAWLINE
LOCALS = <div rect='94, 0, 5937, 1062' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156, 94, 5812, 937'>"
LOCALS += 起床前メニュー上部分表示文章()
LOCALS += "</div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 10
	PRINTL 
NEXT
LOCALS = <font color='#f39800'>-----[ゲーム進行]-------------------------------------------------------------------------------------------</font><br>
LOCALS += "<button value='100'>[100] - 起床</button>　　　　　　　　　　　　　　"
IF STRFIND(TSTR:NEWイベントフラグ, "EVENT") >= 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='120'>[120] - イベントリスト</button></font><font color='#%カラーパレット_HTML("真っ赤")%'> NEW!</font> 　　　　　　"
ELSEIF チュートリアルフラグ:イベントリスト == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='120'>[120] - イベントリスト</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　"
ELSE
	LOCALS += "<button value='120'>[120] - イベントリスト</button>　　　　　　　　　"
ENDIF
IF チュートリアルフラグ:探索へ出かける == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='101'>[101] - 探索へ出かける</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　"
ELSE
	LOCALS += "<button value='101'>[101] - 探索へ出かける</button>　　　　　　　　　"
ENDIF
LOCALS += "<br>"
IF 0
	;実行条件まだ未実装
ELSE
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'><nonbutton title='未実装です'>[102] - 外出する</nonbutton></font>　　　　　　　　　　　　"
	ELSE
		LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[102] - 外出する</font>　　　　　　　　　　　　"
	ENDIF
ENDIF
IF リゾートランク >= 2
	IF STRFIND(TSTR:NEWイベントフラグ, "ENDHING") >= 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='300'>[300] - エンディングイベント</button></font> <font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font> 　　"
	ELSE
		LOCALS += "<button value='300'>[300] - エンディングイベント</button>　　　　　　"
	ENDIF
ELSE
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'><nonbutton title='実行条件：リゾートランクＤ以上'>[300] - エンディングイベント</nonbutton></font>　　　　　　"
	ELSE
		LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[300] - エンディングイベント</font>　　　　　　"
	ENDIF
ENDIF
IF DT_SELECT("エンディング用変数配列", "イベント完了フラグ > 0")
	LOCALS += "<button value='301'>[301] - 達成済みエンド視聴</button>"
ENDIF

LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[リゾート]---------------------------------------------------------------------------------------------</font><br>"
IF 滞在キャラ数 >= 滞在キャラ上限
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>滞在人数上限<br>'>[110] - リゾートの広告を出す</nonbutton></font>　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[110] - リゾートの広告を出す</nonbutton></font>　　　　　　"
	ENDIF
ELSE
	IF チュートリアルフラグ:最初の広告 == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='110'>[110] - リゾートの広告を出す</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　"
	ELSE
		LOCALS += "<button value='110'>[110] - リゾートの広告を出す</button>　　　　　　"
	ENDIF
ENDIF

LOCALS += "<button value='111'>[111] - 経営計画</button>　　　　　　　　　　　　"

IF ダンジョンクリアフラグ_沿岸部の森
	IF チュートリアルフラグ:従業員設定 == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='113'>[113] - 従業員設定</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　"
	ELSE
		LOCALS += "<button value='113'>[113] - 従業員設定</button>　　　　　　　　　　　"
	ENDIF
ELSE
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>未開放機能<br>ダンジョン「沿岸部の森」クリアで解放<br>'>[113] - 従業員設定</nonbutton></font>　　　　　　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[113] - 従業員設定</nonbutton></font>　　　　　　　　　　　"
	ENDIF
ENDIF
LOCALS += "<br>"
IF ダンジョンクリアフラグ_沿岸部の森
	IF チュートリアルフラグ:雇用レポート == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='130'>[130] - 雇用レポート</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　"
	ELSE
		LOCALS += "<button value='130'>[130] - 雇用レポート</button>　　　　　　　　　　"
	ENDIF
ELSE
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>未開放機能<br>ダンジョン「沿岸部の森」クリアで解放<br>'>[130] - 雇用レポート</nonbutton></font>　　　　　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[130] - 雇用レポート</nonbutton></font>　　　　　　　　　　"
	ENDIF
ENDIF
LOCALS += "<button value='131'>[131] - 宿泊エリア確認</button>　　　　　　　　　"




LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[キャラクター]-----------------------------------------------------------------------------------------</font><br>"
IF チュートリアルフラグ:能力表示 == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='121'>[121] - 能力表示</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　　"
ELSE
	LOCALS += "<button value='121'>[121] - 能力表示</button>　　　　　　　　　　　　"
ENDIF
IF チュートリアルフラグ:探索系情報 == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='122'>[122] - 探索系情報一覧</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　"
ELSE
	LOCALS += "<button value='122'>[122] - 探索系情報一覧</button>　　　　　　　　　"
ENDIF
IF 施設改造度:5:6
	IF チュートリアルフラグ:性癖開拓 == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='123'>[123] - 性癖開拓</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　　"
	ELSE
		LOCALS += "<button value='123'>[123] - 性癖開拓</button>　　　　　　　　　　　　"
	ENDIF
ELSE
	IF OPTION変数:ツールチップ切り替え == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>未開放機能<br>施設改造「遊戯室系列：図書室」達成で解放<br>'>[123] - 性癖開拓</nonbutton></font>　　　　　　　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[123] - 性癖開拓</nonbutton></font>　　　　　　　　　　　　"
	ENDIF
ENDIF
LOCALS += "<br>"
IF チュートリアルフラグ:干渉力強化 == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='124'>[124] - 干渉力強化</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　"
ELSE
	LOCALS += "<button value='124'>[124] - 干渉力強化</button>　　　　　　　　　　　"
ENDIF
IF チュートリアルフラグ:縁結び == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='125'>[125] - 縁結び</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　"
ELSE
	LOCALS += "<button value='125'>[125] - 縁結び</button>　　　　　　　　　　　　　"
ENDIF
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[ショップ類]-------------------------------------------------------------------------------------------</font><br>"
IF チュートリアルフラグ:通信販売 == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='114'>[114] - 通信販売</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　　"
ELSE
	LOCALS += "<button value='114'>[114] - 通信販売</button>　　　　　　　　　　　　"
ENDIF
IF チュートリアルフラグ:ZPショップ == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='115'>[115] - ZPショップ</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　"
ELSE
	LOCALS += "<button value='115'>[115] - ZPショップ</button>　　　　　　　　　　　"
ENDIF
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[リゾート内施設]---------------------------------------------------------------------------------------</font><br>"
コマンド表示数 = 0
IF イベント完了フラグ確認("大拡張！空艇港") > 0
	IF チュートリアルフラグ:よろず屋依頼 == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='116'>[116] - よろず屋で依頼を出す</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　"
	ELSE
		LOCALS += @"<button value='116'>[116] - よろず屋で依頼を出す</button>　　　　　　"
	ENDIF
	IF チュートリアルフラグ:輸入品店 == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='117'>[117] - 輸入品店</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　　"
	ELSE
		LOCALS += @"<button value='117'>[117] - 輸入品店</button>　　　　　　　　　　　　"
	ENDIF
	IF イベント完了フラグ確認("異世界の衣服？") > 0
		IF チュートリアルフラグ:異世界裁店 == 0
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='118'>[118] - 異世界裁店</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　"
		ELSE
			LOCALS += @"<button value='118'>[118] - 異世界裁店</button>　　　　　　　　　　　"
		ENDIF
	ENDIF
	LOCALS += "<br>"
ENDIF
FOR LOCAL, 3, 9
	CALLF GETPLACENAME_1_0(LOCAL)
	IF 空艇港店舗状況:LOCAL:0 == 1
		LOCALS += @"<button value='{140 + LOCAL}'>[{140 + LOCAL}] - %TSTR:場所名受渡, 32, LEFT%</button>"
		コマンド表示数 += 1
	ELSEIF 空艇港店舗状況:LOCAL:0 == 2
		LOCALS += @"<button value='{140 + LOCAL}'>[{140 + LOCAL}] - %TSTR:場所名受渡, 32, LEFT%</button>"
		コマンド表示数 += 1
	ELSEIF 空艇港店舗状況:LOCAL:0 == 4
		LOCALS += @"<button value='{140 + LOCAL}'>[{140 + LOCAL}] - %TSTR:場所名受渡, 32, LEFT%</button>"
		コマンド表示数 += 1
	ENDIF
	IF コマンド表示数 > 2
		LOCALS += "<br>"
		コマンド表示数 = 0
	ENDIF
NEXT
IF 港改造度:11:0
	SIF コマンド表示数
		LOCALS += "<br>"
	LOCALS += @"<button value='150'>[150] - 乗機作成</button>　　　　　　　　　　　　"
	LOCALS += @"<button value='151'>[151] - 搭載兵器作成</button>　　　　　　　　　　"
	LOCALS += @"<button value='152'>[152] - 古ぼけた装備再生</button>　　　　　　　　"
	LOCALS += "<br>"
ENDIF
IF 高速船作成済フラグ
	IF チュートリアルフラグ:高速船派遣 == 0
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='153'>[153] - 高速船派遣</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　"
	ELSE
		LOCALS += @"<button value='153'>[153] - 高速船派遣</button>　　　　　　　　　　　"
	ENDIF
ENDIF
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[アイテム類]-------------------------------------------------------------------------------------------</font><br>"
IF チュートリアルフラグ:写真一覧 == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='135'>[135] - 写真一覧</button></font>　<font color='#%カラーパレット_HTML("真っ赤")%'>NEW!</font>　　　　　　　　　"
ELSE
	LOCALS += @"<button value='135'>[135] - 写真一覧</button>　　　　　　　　　　　　"
ENDIF
IF DT_ROW_LENGTH("ミルクデータベース") > 0
	LOCALS += @"<button value='136'>[136] - ミルク一覧</button>　　　　　　　　　　　"
ENDIF
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[オプション]-------------------------------------------------------------------------------------------</font><br>"
LOCALS += "<button value='200'>[200] - OPTION</button>　　　　　　　　　　　　　"
LOCALS += "<button value='201'>[201] - SAVE</button>　　　　　　　　　　　　　　"
LOCALS += "<button value='202'>[202] - LOAD</button>　　　　　　　　　　　　　　"
LOCALS += "<br>"
LOCALS += "<button value='203'>[203] - ゲームヘルプ</button>　　　　　　　　　　"
LOCALS += "<br>"
SIF FLAG:エンディング後フラグ
	LOCALS += "<button value='900'>[900] - 強くてNEWGAME</button>　　　　　　　　　　"
LOCALS += "<br><br>"
[IF_DEBUG]
LOCALS += "<button value='9999'>[9999] - デバッグ用</button>　　　　　　　　　　"
LOCALS += "<br><br>"
[ENDIF]
HTML_PRINT LOCALS
BINPUT

SELECTCASE RESULT
	CASE 800 TO 899
		CALL 起床前メニュー上部分文章切り替え(RESULT)
	CASE 100
		IF チュートリアルフラグ:最初の広告 == 0
			PRINTW チュートリアル中のため、広告チュートリアルを見るまで起床することが出来ません。
		ELSE
			SAVESTR:ゲームフェイズ管理 = 通常モード

			;念の為イベントうふふ用のフラグを消しとく
			FLAG:イベントうふふ中フラグ = 0
			FLAG:イベントうふふベッドルーム = 0

			;念の為イベント再視聴用の（ｒｙ
			FLAG:クリア済み視聴フラグ = 0

			CALL TRAIN_MAIN
			RETURN 1
		ENDIF
	CASE 101
		CALL COM601
		IF RESULT == 1
			CALL TURN_RESET
			LOCALS = %月算出()%
			SAVEDATA 99, @"%GETTIMES()% {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)"
		ENDIF
	CASE 110
		SIF 滞在キャラ数 < 滞在キャラ上限
			CALL キャラクター招待
	CASE 111
		CALL 経営計画メニュー
	CASE 113
		SIF ダンジョンクリアフラグ_沿岸部の森
			CALL 従業員メインメニュー
	CASE 114
		CALL 通信販売_BUY
	CASE 115
		CALL ZP_SHOP
	CASE 116
		CALL よろず屋依頼発行処理
	CASE 117
		CALL よろず屋輸入品店処理
	CASE 118
		CALL よろず屋異世界裁店処理
	CASE 120
		CALL 起床前イベントリスト
	CASE 121
		TFLAG:調教モード = -1
		CALL SHOW_CHARADATA
		TFLAG:調教モード = 0
	CASE 122
		IF チュートリアルフラグ:探索系情報 == 0
			CALL 探索系情報チュートリアル()
		ENDIF
		表示切り替え文字列 = 探索集約_探索メンバー
		CALL 探索系集約表示
	CASE 123
		SIF 施設改造度:5:6
			CALL 性癖開拓処理
	CASE 124
		CALL 干渉強化メニュー
	CASE 125
		CALL 縁結びメニュー
	CASE 130
		CALL 雇用レポート表示
	CASE 131
		CALL 宿泊エリア処理(1)
	CASE 135
		CALL 所持写真一覧表示
	CASE 136
		CALL 所持ミルク一覧表示
	CASE 143 TO 148
		IF 空艇港店舗状況:(RESULT - 140):0 == 1
			CALL 空艇港店舗用コマンド_COM_1
		ELSEIF 空艇港店舗状況:(RESULT - 140):0 == 2
			CALL 空艇港店舗用コマンド_COM_2
		ELSEIF 空艇港店舗状況:(RESULT - 140):0 == 4
			CALL 空艇港店舗用コマンド_COM_4
		ENDIF
	CASE 150
		CALL 所持乗機リスト表示_作成時
	CASE 151
		CALL 所持兵器リスト表示_作成時
	CASE 152
		CALL 古ぼけた装備_再生処理
	CASE 153
		CALL 高速船派遣処理
	CASE 200
		CALL OPTION
	CASE 201
		SAVEGAME
	CASE 202
		LOADGAME
	CASE 203
		CALL チュートリアル_MAIN
	CASE 300
		CALL エンディングイベントリスト(0)
		SIF RESULT == -1
			BEGIN FIRST
	CASE 301
		CALL エンディングイベントリスト(1)
	CASE 900
		IF FLAG:エンディング後フラグ
			CALL 周回引き継ぎ処理()
			SIF RESULT == -1
				BEGIN FIRST
		ENDIF
	CASE 9999
		CALL 起床前デバッグ処理()

ENDSELECT

LOOP 1

@USERSHOP

@起床時処理
#DIM EXP番号
#DIM シャッフル数
#DIM DYNAMIC 順番シャッフル用配列, キャラクタ数上限

IF モブ観光客数 > 0
	DRAWLINE
	PRINTFORMW 今日のモブ観光客数は{モブ観光客数, 4}人です。
	DRAWLINE
ENDIF

CALL キャラ番号配列_初期化_シャッフル(順番シャッフル用配列, 0)
シャッフル数 = RESULT

LOCALS = 
LOCAL:1 = 0
VARSET 教師レベル保存
FOR LOCAL, 0, シャッフル数
	SIF CFLAG:(順番シャッフル用配列:LOCAL):滞在期間 < 0
		CONTINUE
	IF TALENT:(順番シャッフル用配列:LOCAL):従業員
		IF 従業員休暇判定(順番シャッフル用配列:LOCAL)
			TCVAR:(順番シャッフル用配列:LOCAL):休暇中 = 1
			LOCALS += @"・%CALLNAME:(順番シャッフル用配列:LOCAL), 20, LEFT%"
			LOCAL:1 ++
			SIF LOCAL:1 % 3 == 0
				LOCALS += "<br>"
		ELSEIF 現在仕事:(順番シャッフル用配列:LOCAL):0 == 8
			;教師数をチェックしておく
			教師レベル保存:(現在仕事:(順番シャッフル用配列:LOCAL):1) += 知識素質:(順番シャッフル用配列:LOCAL):(現在仕事:(順番シャッフル用配列:LOCAL):1) * (仕事レベル:(順番シャッフル用配列:LOCAL):8 + 1) / 2
			;表示用に保存
			学校利用者:(現在仕事:(順番シャッフル用配列:LOCAL):1 + 100) += @"{(順番シャッフル用配列:LOCAL)}_"
			学校利用者:0 = 表示
		ENDIF
	ENDIF
	CALL キャラ就寝場所セット(順番シャッフル用配列:LOCAL)

	;行動時間に±60分のゆらぎが発生する（大体は±15分に収まるはず）
	;業務ルーチンでは無視される
	TCVAR:(順番シャッフル用配列:LOCAL):行動時間ゆらぎ = (RAND:9 + RAND:9 + RAND:9) * 5 - 60
	CALL 移動ルーチンセット決定(順番シャッフル用配列:LOCAL)

	;一日開始時のEXP数値を記録
	FOR EXP番号, 0, 200
		EXP記録_一日開始時:(順番シャッフル用配列:LOCAL):EXP番号 = EXP:(順番シャッフル用配列:LOCAL):EXP番号
	NEXT
NEXT
;移動ルーチン終わった後にデートセット
CALL デートルーチンセット決定()
CALL 関係性同行決定()

IF LOCALS != ""
	HTML_PRINT @"本日は以下の従業員が休暇中です。<br>%LOCALS%"
	DRAWLINE
	WAIT
ENDIF

;クイック表示用に種別セット
TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
