
;---------------------------------------------------------------
;マップエリア名一覧
;---------------------------------------------------------------
@マップエリア名取得(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 0
		RETURNF "リゾートエリア"
	CASE 1
		RETURNF "空艇港エリア"
	CASE 2
		RETURNF "居住区エリア"
	CASE 3
		RETURNF "海エリア"
	CASE 100
		RETURNF @"%SUBSTRING(開催予定祭り名, 9)%マップ"
	CASE 999
		RETURNF "宿泊エリア"
ENDSELECT

;---------------------------------------------------------------
;キャラの居場所再設定
;---------------------------------------------------------------
@派生マップ番号取得(マップ番号, 現在地番号)
#FUNCTION
#DIM マップ番号
#DIM 現在地番号

SELECTCASE マップ番号
	CASE 0
		;リゾートマップ
		SELECTCASE 現在地番号
			CASE 1 TO 20
				RETURNF 0
			CASE 302 TO 308
				RETURNF 3
			CASE 402 TO 406
				RETURNF 4
			CASE 502 TO 506
				RETURNF 5
			CASE 701 TO 711
				RETURNF 7
			CASE 801 TO 811
				RETURNF 8
			CASE 1503 TO 1515
				RETURNF 15
			CASE 2001 TO 2020
				RETURNF 20
		ENDSELECT
	CASE 2
		;居住区
		SELECTCASE 現在地番号
			CASE 1 TO 20
				RETURNF 0
			CASE 101 TO 104
				RETURNF 1
		ENDSELECT
	CASE 3
		;海
		SELECTCASE 現在地番号
			CASE 1 TO 20
				RETURNF 0
			CASE 101 TO 105
				RETURNF 1
		ENDSELECT
ENDSELECT

@マップ遷移判定処理(マップ番号, 現在地番号)
#DIM マップ番号
#DIM 現在地番号

SELECTCASE マップ番号
	CASE 0
		;基本→キャンプ
		IF 現在地番号 == 3 && 施設改造度:3:0
			RETURN 303
		ENDIF
		;基本→レストラン
		IF 現在地番号 == 4 && 施設改造度:4:0
			RETURN 403
		ENDIF
		;基本→遊戯
		IF 現在地番号 == 5 && 施設改造度:5:0
			RETURN 503
		ENDIF
		;基本→男湯
		IF 現在地番号 == 7 && 施設改造度:7:0
			RETURN 702
		ENDIF
		;基本→女湯
		IF 現在地番号 == 8 && 施設改造度:8:0
			RETURN 802
		ENDIF
		;基本→プール
		IF 現在地番号 == 15 && 施設改造度:15:0
			RETURN 1503
		ENDIF
		;基本→自宅
		IF 現在地番号 == 20 && 施設改造度:20:0
			RETURN 2001
		ENDIF
		;キャンプ→基本
		IF 現在地番号 == 302
			RETURN 2
		ENDIF
		;レストラン→基本
		IF 現在地番号 == 402
			RETURN 2
		ENDIF
		;遊戯→基本
		IF 現在地番号 == 502
			RETURN 2
		ENDIF
		;男湯→基本
		IF 現在地番号 == 701
			RETURN 6
		ENDIF
		;男湯→女湯
		IF 現在地番号 == 709
			RETURN 810
		ENDIF
		;女湯→基本
		IF 現在地番号 == 801
			RETURN 6
		ENDIF
		;女湯→男湯
		IF 現在地番号 == 809
			RETURN 710
		ENDIF
		;プール→基本
		IF 現在地番号 == 1513
			RETURN 13
		ENDIF
		IF 現在地番号 == 1514
			RETURN 14
		ENDIF
		;自宅→基本
		IF 現在地番号 == 2010
			RETURN 2
		ENDIF
	CASE 2
		;一丁目-二丁目
		IF 現在地番号 == 14
			RETURN 101
		ENDIF
		IF 現在地番号 == 104
			RETURN 1
		ENDIF
	CASE 3
		;海水浴場＞沖合
		IF 現在地番号 == 8
			RETURN 102
		ENDIF
		;沖合＞海水浴場
		IF 現在地番号 == 101
			RETURN 6
		ENDIF
ENDSELECT

RETURN 0

;-----------------------------------------------------------
;遠距離移動処理
;-----------------------------------------------------------
@SKIP_MOVE(マップ種別, 目的地, 移動キャラ = 0)
#DIM 目的地
#DIM 移動キャラ
#DIM マップ種別
;戻り値は目的地に直接行ける場合は目的地
;行けない場合は経由地

SIF (移動キャラ == 0 || 移動キャラ == MASTER) && あなた特殊能力:縦横無尽 && あなた単独()
	RETURN 目的地
JUMPFORM SKIP_MOVE_{マップ種別}(目的地, 移動キャラ)

;-----------------------------------------------------------
;地点関連性応答処理
;-----------------------------------------------------------
@CAN_MOVE(ARG:2, ARG, ARG:1)
#DIM RESULT保存
;部屋の構造
;ARG 移動元
;ARG:1 移動先
;戻り値はビット管理 & 1で移動可能、 & 2で見通し可能
;RETURNF 1 移動可能
;RETURNF 2 移動不可だが見通せる
;RETURNF 3 直通（移動可能＆見通せる）
#FUNCTION
SELECTCASE ARG:2
	CASE 0
		RETURNF CAN_MOVE_0(ARG, ARG:1)
	CASE 1
		RETURNF CAN_MOVE_1(ARG, ARG:1)
	CASE 2
		RETURNF CAN_MOVE_2(ARG, ARG:1)
	CASE 3
		RETURNF CAN_MOVE_3(ARG, ARG:1)
	CASE 100
		RETURNF CAN_MOVE_100(ARG, ARG:1)
	CASE 200 TO 499
		RESULT保存 = RESULT
		RESULT = 1
		TRYCALLFORMF CAN_MOVE_{ARG:2}(ARG, ARG:1)
		SWAP RESULT, RESULT保存
		RETURNF RESULT保存
ENDSELECT

;---------------------------------------------------------------
;マップ種別による移動先可否判定
;---------------------------------------------------------------
@存在しない移動先(ARG:1, ARG)
#FUNCTION
#DIM RESULT保存

SELECTCASE ARG:1
	CASE 0
		RETURNF 存在しない移動先_0(ARG)
	CASE 1
		RETURNF 存在しない移動先_1(ARG)
	CASE 2
		RETURNF 存在しない移動先_2(ARG)
	CASE 3
		RETURNF 存在しない移動先_3(ARG)
	CASE 100
		RESULT保存 = RESULT
		RESULT = 1
		TRYCALLFORMF 存在しない移動先_%開催予定祭り名%(ARG)
		SWAP RESULT, RESULT保存
		RETURNF RESULT保存
	CASE 200 TO 499
		RESULT保存 = RESULT
		RESULT = 1
		TRYCALLFORMF 存在しない移動先_{ARG:1}(ARG)
		SWAP RESULT, RESULT保存
		RETURNF RESULT保存
ENDSELECT

RETURNF 1

;---------------------------------------------------------------
;エリア移動を伴わないマップ切り替え構文
;---------------------------------------------------------------
@表示マップ切り替え判定(ARG)

CALL マップ遷移判定処理(CFLAG:PLAYER:現在マップ種別, ARG)
IF RESULT
	TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, RESULT)
	RETURN 1
ENDIF

RETURN 0

;---------------------------------------------------------------
;部屋名出力（各マップERBファイルに受渡し
;---------------------------------------------------------------
@GETPLACENAME(ARG, ARG:1)
#FUNCTIONS
#DIMS ローカル文字列
#DIMS 配列分解文字列, 110

TSTR:場所名受渡 = 

;現在位置がマイナスだとエラー落ちするのでとりあえず対策
SIF ARG:1 < 0
	RETURNF TSTR:場所名受渡

IF ARG == 999
	IF ARG:1 < 1000
		IF 滞在者部屋割り配列:(ARG:1) < 1
			LOCALS = 滞在者なし
		ELSE
			LOCALS = %CALLNAME:(滞在者部屋割り配列:(ARG:1))%
		ENDIF
		RETURNF @"客室{ARG:1}号室：%LOCALS%"
	ELSEIF ARG:1 < 2000
		IF 従業員部屋割り配列:(ARG:1 - 1000) < 1
			LOCALS = 使用者なし
		ELSE
			LOCALS = %CALLNAME:(従業員部屋割り配列:(ARG:1 - 1000))%
		ENDIF
		RETURNF @"従業員室{ARG:1}号室：%LOCALS%"
	ELSE
		IF スイートルーム部屋割り配列:(ARG:1 - 2000) == ""
			LOCALS = 使用者なし
		ELSE
			VARSET 配列分解文字列
			SPLIT スイートルーム部屋割り配列:(ARG:1 - 2000), "_", 配列分解文字列
			IF STRCOUNT(スイートルーム部屋割り配列:(ARG:1 - 2000), "_") > 2
				LOCALS = %CALLNAME:(TOINT(配列分解文字列:1))%他
			ELSE
				LOCALS = %CALLNAME:(TOINT(配列分解文字列:1))%
			ENDIF
		ENDIF
		RETURNF @"スイートルーム{ARG:1}号室：%LOCALS%"
	ENDIF
ENDIF

CALLFORMF GETPLACENAME_{ARG}_{ARG:1 / 100}(ARG:1)

RETURNF TSTR:場所名受渡


;---------------------------------------------------------------
;同じ場所扱いになる部屋番号
;---------------------------------------------------------------
@同一場所判定(ARG, ARG:1)
#FUNCTION

IF GROUPMATCH(ARG, 2, 302, 502) && GROUPMATCH(ARG:1, 2, 302, 502)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 3, 303) && GROUPMATCH(ARG:1, 3, 303)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 5, 503) && GROUPMATCH(ARG:1, 5, 503)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 15, 1503) && GROUPMATCH(ARG:1, 15, 1503)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 13, 1513) && GROUPMATCH(ARG:1, 13, 1513)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 14, 1514) && GROUPMATCH(ARG:1, 14, 1514)
	RETURNF 1
ENDIF

RETURNF 0

;---------------------------------------------------------------
;数字の文字列化＆ニ桁化
;---------------------------------------------------------------
@TOSTR_ニ桁化(ARG)
#FUNCTIONS
IF ARG < 10 && ARG > -1
	RETURNF "0" + TOSTR(ARG)
ELSE
	RETURNF TOSTR(ARG)
ENDIF


;---------------------------------------------------------------
;移動先が見えない場合の処理
;---------------------------------------------------------------
@移動エラー文章表示(移動キャラ, 目的地)
#DIM 目的地
#DIM 移動キャラ

PRINTFORML 移動エラーです。TIME{TIME}：移動キャラ%CALLNAME:移動キャラ%：リゾートマップ{CFLAG:移動キャラ:現在位置}>{目的地}：スケジュール%移動スケジュール文字列:移動キャラ:(スケジュール状態:移動キャラ:現在スケジュール):スケジュール%
THROW エラーメッセージをコピーして報告をお願いします
