
@性癖取得チェック(キャラ番号, 一日終了時フラグ = 0)
#DIM キャラ番号
#DIM 取得性癖番号
#DIM 候補性癖数
#DIM 一日終了時フラグ
#DIMS 性癖名取得, 2
;起床前メニュー関連じゃないが、性癖関連をひとまとめにするためこのフォルダに置いている

IF 一日終了時フラグ
	;一日終了時はこの文字列に結果を保存して後で格納する
	RSTR:コマンド結果受渡し文字列 = 
ENDIF

SIF キャラ番号 <= 0
	RETURN 0

VARSET RESULTS
候補性癖数 = ENUMFUNCBEGINSWITH("自動取得性癖_")
FOR 取得性癖番号, 0, 候補性癖数
	RESULT = 0
	CALLFORM %RESULTS:取得性癖番号%(キャラ番号)
	IF RESULT
		WAIT
		SPLIT RESULTS:取得性癖番号, "_", 性癖名取得
		SIF 一日終了時フラグ
			RSTR:コマンド結果受渡し文字列 += @"{キャラ番号}+++%性癖名取得:1%___"
		TRYCALLFORM KOJO_性癖開拓_%性癖名取得:1%_{NO:キャラ番号}(キャラ番号,2)
		DRAWLINE
	ENDIF
NEXT

@性癖成長チェック(キャラ番号, 一日終了時フラグ = 0)
#DIM キャラ番号
#DIM 一日終了時フラグ
#DIM 取得性癖番号
#DIM 候補性癖数
#DIMS 性癖名取得, 2

IF 一日終了時フラグ
	;一日終了時はこの文字列に結果を保存して後で格納する
	RSTR:コマンド結果受渡し文字列 = 
ENDIF

SIF キャラ番号 <= 0
	RETURN 0

VARSET RESULTS
候補性癖数 = ENUMFUNCBEGINSWITH("性癖段階成長_")
FOR 取得性癖番号, 0, 候補性癖数
	RESULT = 0
	CALLFORM %RESULTS:取得性癖番号%(キャラ番号)
	IF RESULT
		WAIT
		SPLIT RESULTS:取得性癖番号, "_", 性癖名取得
		SIF 一日終了時フラグ
			RSTR:コマンド結果受渡し文字列 += @"{キャラ番号}+++%性癖名取得:1%+++{RESULT}___"
		TRYCALLFORM KOJO_性癖成長_%性癖名取得:1%_{NO:キャラ番号}(キャラ番号)
		DRAWLINE
	ENDIF
NEXT

@性癖非同室成長チェック(キャラ番号)
#DIM キャラ番号
#DIM 取得性癖番号
#DIM 候補性癖数
#DIMS 性癖名取得, 2

SIF キャラ番号 <= 0
	RETURN 0

VARSET RESULTS
候補性癖数 = ENUMFUNCBEGINSWITH("性癖段階非同室成長_")
FOR 取得性癖番号, 0, 候補性癖数
	CALLFORM %RESULTS:取得性癖番号%(キャラ番号)
	IF RESULT
		WAIT
		SPLIT RESULTS:取得性癖番号, "_", 性癖名取得
		TRYCALLFORM KOJO_性癖成長_%性癖名取得:1%_{NO:キャラ番号}(キャラ番号)
		DRAWLINE
	ENDIF
NEXT


@自動取得性癖_精飲(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:精飲
	CASE 0
		;経験が足りないとダメ
		SIF EXP:キャラ番号:精飲快楽経験 < 100
			RETURN 0
	CASE 1
		;興味があると必要経験値が大幅ダウン
		;経験が足りないとダメ
		SIF EXP:キャラ番号:精飲快楽経験 < 30
			RETURN 0
	CASEELSE
		;既に取得してるとダメ
		RETURN 0
ENDSELECT

性癖素質:キャラ番号:精飲 = 2

;取得メッセージ
CALL 自動取得性癖メッセージ_精飲(キャラ番号)
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[精飲(愛好)]を取得した</font>","うふふ")

RETURN 1

@自動取得性癖メッセージ_精飲(キャラ番号)
#DIM キャラ番号

DRAWLINE
PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
PRINTL どうやら精液を飲むと、大きな快楽を覚える身体になってしまったようだ。
PRINTL 
PRINTFORML %CALLNAME:キャラ番号%は性癖素質[精飲(愛好)]を取得した
PRINTL


@性癖段階成長_精飲(キャラ番号)
#DIM キャラ番号

;既に中毒だとダメ
SIF 性癖素質:キャラ番号:精飲 == 3
	RETURN 0
SIF 性癖素質:キャラ番号:精飲 < 0
	RETURN 0
;経験が足りないとダメ
SIF EXP:キャラ番号:精飲快楽経験 < 300
	RETURN 0

CALL 性癖段階成長メッセージ_精飲(キャラ番号, 性癖素質:キャラ番号:精飲)
性癖素質:キャラ番号:精飲 = 3
;取得メッセージ
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[精飲(愛好)]が[精飲(中毒)]に成長した</font>","うふふ")
PRINTL

RETURN 2

@性癖段階成長メッセージ_精飲(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
		PRINTL どうやら精液を飲むと、反射的に絶頂する身体になってしまったようだ。
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[精飲(愛好)]は[精飲(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_精飲(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:精飲 < 3
	RETURN 0
;MASTERにCがない場合駄目
SIF TALENT:MASTER:Ｃ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:MASTER:性別, 1) == 0
	RETURN 0

RETURN 1


@自動取得性癖_キス(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:キス
	CASE 0
		;経験が足りないとダメ
		;キス愛好があると必要値ダウン
		IF TALENT:キャラ番号:キス嗜好 >= 2
			SIF EXP:キャラ番号:キス経験 < 1
				RETURN 0
		ELSEIF TALENT:キャラ番号:キス嗜好 >= 1
			SIF EXP:キャラ番号:キス経験 < 50
				RETURN 0
		ELSE
			SIF EXP:キャラ番号:キス経験 < 200
				RETURN 0
		ENDIF
	CASE 1
		;興味があると必要経験値が大幅ダウン
		;経験が足りないとダメ
		IF TALENT:キャラ番号:キス嗜好 >= 2
			SIF EXP:キャラ番号:キス経験 < 1
				RETURN 0
		ELSE 
			SIF EXP:キャラ番号:キス経験 < 50
				RETURN 0
		ENDIF
	CASEELSE
		;既に取得してるとダメ
		RETURN 0
ENDSELECT

性癖素質:キャラ番号:キス = 2
SIF TALENT:キャラ番号:キス嗜好 < 1
	TALENT:キャラ番号:キス嗜好 = 1

;取得メッセージ
CALL 自動取得性癖メッセージ_キス(キャラ番号)
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[キス(愛好)]を取得した</font>","うふふ")

RETURN 1

@自動取得性癖メッセージ_キス(キャラ番号)
#DIM キャラ番号

DRAWLINE
PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
PRINTL どうやらキスの感覚にハマり込んでしまったようだ。
PRINTL 
PRINTFORML %CALLNAME:キャラ番号%は性癖素質[キス(愛好)]を取得した
PRINTL


@性癖段階成長_キス(キャラ番号)
#DIM キャラ番号

;既に中毒だとダメ
SIF 性癖素質:キャラ番号:キス == 3
	RETURN 0
SIF 性癖素質:キャラ番号:キス < 0
	RETURN 0
;経験が足りないとダメ

IF TALENT:キャラ番号:キス嗜好 >= 2
	SIF EXP:キャラ番号:キス経験 < 250
		RETURN 0
ELSE
	SIF EXP:キャラ番号:キス経験 < 500
		RETURN 0
ENDIF

CALL 性癖段階成長メッセージ_キス(キャラ番号, 性癖素質:キャラ番号:キス)
性癖素質:キャラ番号:キス = 3
SIF TALENT:キャラ番号:キス嗜好 < 2
	TALENT:キャラ番号:キス嗜好 = 2

;取得メッセージ
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[キス(愛好)]が[キス(中毒)]に成長した</font>","うふふ")
PRINTL

RETURN 2

@性癖段階成長メッセージ_キス(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
		PRINTL どうやらキスの感覚と快楽か忘れられず唇と舌がキスを求めて疼くようになってしまったようだ。
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[キス(愛好)]は[キス(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_キス(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:キス < 3
	RETURN 0
;MASTERとキスした事がない場合駄目
SIF !DT_SELECT("初体験データベース", @"体験キャラ人物番号 = {CFLAG:キャラ番号:人物番号} And 相手キャラ人物番号 = {CFLAG:MASTER:人物番号} And 体験名 = 'キス'")
	RETURN 0
RETURN 1


@自動取得性癖_膣内射精(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:膣内射精
	CASE 0
		;経験が足りないとダメ
		SIF EXP:キャラ番号:膣内射精絶頂経験 < 30
			RETURN 0
	CASE 1
		;興味があると必要経験値が大幅ダウン
		;経験が足りないとダメ
		SIF EXP:キャラ番号:膣内射精絶頂経験 < 10
			RETURN 0
	CASEELSE
		;既に取得してるとダメ
		RETURN 0
ENDSELECT

性癖素質:キャラ番号:膣内射精 = 2

;取得メッセージ
CALL 自動取得性癖メッセージ_膣内射精(キャラ番号)
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[膣内射精(愛好)]を取得した</font>","うふふ")

RETURN 1

@自動取得性癖メッセージ_膣内射精(キャラ番号)
#DIM キャラ番号

DRAWLINE
PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
PRINTL どうやら膣内射精をされると、大きな快楽を覚える身体になってしまったようだ。
PRINTL 
PRINTFORML %CALLNAME:キャラ番号%は性癖素質[膣内射精(愛好)]を取得した
PRINTL

@性癖段階成長_膣内射精(キャラ番号)
#DIM キャラ番号

;既に中毒だとダメ
SIF 性癖素質:キャラ番号:膣内射精 == 3
	RETURN 0
SIF 性癖素質:キャラ番号:膣内射精 < 0
	RETURN 0
;経験が足りないとダメ
SIF EXP:キャラ番号:膣内射精絶頂経験 < 100
	RETURN 0

CALL 性癖段階成長メッセージ_膣内射精(キャラ番号, 性癖素質:キャラ番号:膣内射精)
性癖素質:キャラ番号:膣内射精 = 3

;取得メッセージ
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[膣内射精(愛好)]が[膣内射精(中毒)]に成長した</font>","うふふ")
PRINTL

RETURN 2

@性癖段階成長メッセージ_膣内射精(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
		PRINTL どうやら膣内射精をされると、反射的に絶頂する身体になってしまったようだ。
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[膣内射精(愛好)]は[膣内射精(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_膣内射精(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:膣内射精 < 3
	RETURN 0
;MASTERにCがない場合駄目
SIF TALENT:MASTER:Ｃ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:MASTER:性別, 1) == 0
	RETURN 0
;キャラにVがない場合駄目
SIF TALENT:キャラ番号:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:キャラ番号:性別, 0) == 0
	RETURN 0

RETURN 1


@自動取得性癖_腸内射精(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:腸内射精
	CASE 0
		;経験が足りないとダメ
		SIF EXP:キャラ番号:腸内射精絶頂経験 < 30
			RETURN 0
	CASE 1
		;興味があると必要経験値が大幅ダウン
		;経験が足りないとダメ
		SIF EXP:キャラ番号:腸内射精絶頂経験 < 10
			RETURN 0
	CASEELSE
		;既に取得してるとダメ
		RETURN 0
ENDSELECT

性癖素質:キャラ番号:腸内射精 = 2

;取得メッセージ
CALL 自動取得性癖メッセージ_腸内射精(キャラ番号)
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[腸内射精(愛好)]を取得した</font>","うふふ")

RETURN 1

@自動取得性癖メッセージ_腸内射精(キャラ番号)
#DIM キャラ番号

DRAWLINE
PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
PRINTL どうやら腸内射精をされると、大きな快楽を覚える身体になってしまったようだ。
PRINTL 
PRINTFORML %CALLNAME:キャラ番号%は性癖素質[腸内射精(愛好)]を取得した
PRINTL

@性癖段階成長_腸内射精(キャラ番号)
#DIM キャラ番号

;既に中毒だとダメ
SIF 性癖素質:キャラ番号:腸内射精 == 3
	RETURN 0
SIF 性癖素質:キャラ番号:腸内射精 < 0
	RETURN 0
;経験が足りないとダメ
SIF EXP:キャラ番号:腸内射精絶頂経験 < 100
	RETURN 0

CALL 性癖段階成長メッセージ_腸内射精(キャラ番号, 性癖素質:キャラ番号:腸内射精)
性癖素質:キャラ番号:腸内射精 = 3

;取得メッセージ
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[腸内射精(愛好)]が[腸内射精(中毒)]に成長した</font>","うふふ")
PRINTL

RETURN 2

@性癖段階成長メッセージ_腸内射精(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
		PRINTL どうやら腸内射精をされると、反射的に絶頂する身体になってしまったようだ。
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[腸内射精(愛好)]は[腸内射精(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_腸内射精(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:腸内射精 < 3
	RETURN 0
;MASTERにCがない場合駄目
SIF TALENT:MASTER:Ｃ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:MASTER:性別, 1) == 0
	RETURN 0
;キャラにAがない場合駄目
SIF TALENT:キャラ番号:Ａ感度 == -2
	RETURN 0

RETURN 1


@自動取得性癖_同性愛(キャラ番号)
#DIM キャラ番号
#DIMS 素質名

;既に取得してるとダメ
SIF 性癖素質:キャラ番号:同性愛
	RETURN 0
;経験が足りないとダメ
SIF EXP:キャラ番号:同性愛経験 < 20
	RETURN 0
;性欲無しだとダメ
SIF TALENT:キャラ番号:性欲 == -2
	RETURN 0
;勝手に目覚めさせるには特殊能力が必要
SIF あなた特殊能力:同性殺し <= 0
	RETURN 0
;取得対象不適だとダメ
素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), キャラ番号)
SIF 素質名 != "同性愛"
	RETURN 0
;あなたが対象外じゃないとダメ
SIF 同性愛性癖判定(キャラ番号, MASTER) == 0
	RETURN 0
;馴れ合い強度1
SIF TCVAR:MASTER:馴れ合い強度 < 1 || TCVAR:キャラ番号:馴れ合い強度 < 1
	RETURN 0

;基礎確率は2/50 （本の1/5）
LOCAL = 2
;性的興味、性欲が影響
;本とは異なり自制心、性知識は影響しない
LOCAL += TALENT:キャラ番号:性的興味 * 2
LOCAL += TALENT:キャラ番号:性欲
SIF BASE:キャラ番号:性欲 > 1500
	LOCAL += 1

SELECTCASE TALENT:キャラ番号:性別嗜好
	CASE 1
		;男の男嫌いパターン
		SIF TALENT:キャラ番号:性別 == 2
			LOCAL -= 5
	CASE 2
		;女の女嫌いパターン
		SIF TALENT:キャラ番号:性別 == 1
			LOCAL -= 5
	CASE 3
		;そもそも人間が嫌い
		LOCAL -= 5
ENDSELECT

;最低確率1/50は確保
SIF RAND:50 >= MAX(LOCAL, 1)
	RETURN 0

性癖素質:キャラ番号:同性愛 = 1

;取得メッセージ
CALL 自動取得性癖メッセージ_同性愛(キャラ番号)
CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]を取得した</font>","うふふ")

RETURN 1

;同性愛性癖の他の処理は性癖開拓処理.ERBを参照

@自動取得性癖メッセージ_同性愛(キャラ番号)
#DIM キャラ番号

DRAWLINE
PRINTFORML %CALLNAME:キャラ番号%の様子がおかしい……
PRINTFORML どうやら%CALLNAME:MASTER%のことを性的に捉え、恋愛対象外のはずの相手と体を重ねることに興味を持ってしまったようだ。
PRINTL 
PRINTFORML %CALLNAME:キャラ番号%は性癖素質[%GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), キャラ番号)%(興味)]を取得した
PRINTL
