
;-------------------------------------------------
;03_304_01バーベキューエリアで食事をする
;-------------------------------------------------
@COMNAME_PLACE_380_0_304
#FUNCTION

IF 施設改造度:3:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = ＢＢＱをする
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_0_304
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_380_0_304
LOCALS = <br>■ＢＢＱをする<br>
LOCALS += "その場にいるキャラ全員とＢＢＱして食事するコマンド。一定確率で大成功、成功、失敗、大失敗に分岐。<br>所持している食材を提供すると、効果が上昇する。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：歓楽・征服"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%
		
@COM380_0_304
#DIM 料理腕前
#DIM RESULT保存
#DIM ソート後素材順, 1000
;#DIM 提供食材, 5＞ERHにて記述

VARSET 提供食材, -1
VARSET LOCAL

;RFLAG:コマンド結果受渡し変数 料理の成功度
;RFLAG:コマンド結果受渡し変数２ 料理人Lvが一番高いキャラ（複数いる場合はランダム）

;追加食材の表示
$食材_LOOP
VARSET ソート後素材順
;データベースをランク順にソート
DT_SELECT "所持素材データベース", "素材カテゴリ = '食物素材'", "素材ランク ASC, 素材属性 ASC", ソート後素材順

DRAWLINE
VARSET LOCAL
PRINTL 食材を追加しますか？
FOR COUNT, 0, 1000
	SIF ソート後素材順 == 0
		BREAK
	SIF DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "所持素材数", 1) < 1
		CONTINUE
	PRINTBUTTONLC @"%DT_CELL_GETS("所持素材データベース", ソート後素材順:COUNT, "素材アイテム名", 1), 26, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "所持素材数", 1))), 6%", COUNT
	LOCAL += 1
	IF LOCAL == 3
		PRINTL 
		LOCAL = 0
	ENDIF
NEXT
SIF LOCAL
	PRINTL 
DRAWLINE

;今選択している追加食材（３枠分）
FOR LOCAL, 0, 3
	PRINT ■
	IF 提供食材:LOCAL == -1
		PRINT ────────
	ELSE
		PRINTBUTTON @"%DT_CELL_GETS("所持素材データベース", 提供食材:LOCAL, "素材アイテム名", 1)%", LOCAL + 1000
	ENDIF
	PRINTL 
NEXT

DRAWLINE
IF MAX(提供食材:0, 提供食材:1, 提供食材:2) == -1
	PRINTBUTTONLC "[999]何も追加しない", 999
ELSE
	PRINTBUTTONLC "[999]この食材を追加する", 999
ENDIF
PRINTBUTTON "[説明]", 998

BINPUT
RESULT保存 = RESULT
SELECTCASE RESULT保存
	CASE 1000 TO 1010
		CALL 素材入手処理(DT_CELL_GETS("所持素材データベース", 提供食材:(RESULT保存 - 1000), "素材アイテム名", 1), 1, 1)
		提供食材:(RESULT保存 - 1000) = -1
		GOTO 食材_LOOP
	CASE 998
		PRINTL 手持ちの食材を追加提供することで、食事の効果を上昇させることが出来ます。
		PRINTL （好感度の上昇量が増加するなど）
		PRINTL 追加しなくてもマイナス補正が掛かったりすることはありません。
		PRINTW また、キャラの好物を提供すると効果上昇量が増加します。（未実装）
		GOTO 食材_LOOP
	CASE 999
		IF MAX(提供食材:0, 提供食材:1, 提供食材:2) == -1
		ELSE
			FOR LOCAL, 0, 3
				IF 提供食材:LOCAL == -1
				ELSE
					PRINTFORM %DT_CELL_GETS("所持素材データベース", 提供食材:LOCAL, "素材アイテム名", 1)%  
				ENDIF
			NEXT
			PRINTW を追加しました。
		ENDIF
	CASEELSE
		IF 提供食材:0 == -1
			提供食材:0 = ソート後素材順:RESULT保存
			DT_CELL_SET "所持素材データベース", ソート後素材順:RESULT保存, "所持素材数", DT_CELL_GET("所持素材データベース", ソート後素材順:RESULT保存, "所持素材数", 1) - 1, 1
		ELSEIF 提供食材:1 == -1
			提供食材:1 = ソート後素材順:RESULT保存
			DT_CELL_SET "所持素材データベース", ソート後素材順:RESULT保存, "所持素材数", DT_CELL_GET("所持素材データベース", ソート後素材順:RESULT保存, "所持素材数", 1) - 1, 1
		ELSEIF 提供食材:2 == -1
			提供食材:2 = ソート後素材順:RESULT保存
			DT_CELL_SET "所持素材データベース", ソート後素材順:RESULT保存, "所持素材数", DT_CELL_GET("所持素材データベース", ソート後素材順:RESULT保存, "所持素材数", 1) - 1, 1
		ELSE
			;枠が全部埋まってる
			PRINTW 食材枠が全て埋まっています。
		ENDIF
		GOTO 食材_LOOP
ENDSELECT

;成功度の判定
料理腕前 = 0
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	IF 料理腕前 < 知識素質:(TARGET:LOCAL):料理人
		料理腕前 = 知識素質:(TARGET:LOCAL):料理人
		RFLAG:コマンド結果受渡し変数２ = TARGET:LOCAL
	ELSEIF 料理腕前 == 知識素質:(TARGET:LOCAL):料理人
		SIF RAND:2 == 1
			RFLAG:コマンド結果受渡し変数２ = TARGET:LOCAL
	ENDIF
NEXT
IF 料理腕前 < 知識素質:PLAYER:料理人
	料理腕前 = 知識素質:PLAYER:料理人
	RFLAG:コマンド結果受渡し変数２ = PLAYER
ELSEIF 料理腕前 == 知識素質:PLAYER:料理人
	SIF RAND:2 == 1
		RFLAG:コマンド結果受渡し変数２ = PLAYER
ENDIF

料理腕前 = 料理腕前 * 15 + RAND:100
;基本方針
IF 条例発布:0 == 3
	料理腕前 += 15
ENDIF
SELECTCASE 料理腕前
	CASE IS < 10
		;大失敗
		RFLAG:コマンド結果受渡し変数 = -2
	CASE IS < 35
		;失敗
		RFLAG:コマンド結果受渡し変数 = -1
	CASE IS < 100
		;成功
		RFLAG:コマンド結果受渡し変数 = 1
		CALL 同室者縁結び進行処理(1)
	CASEELSE
		;大成功
		RFLAG:コマンド結果受渡し変数 = 2
		CALL 同室者縁結び進行処理(RAND:3 + 1)
ENDSELECT

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		BREAK
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):隠密
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM380_0_304_追加処理(提供食材:0, 提供食材:1, 提供食材:2, 提供食材:3, 提供食材:4)

	CALL ウエスト変動処理(TARGET, "食事", 3)
	TCVAR:TARGET:摂取カロリー += (6 - MATCH(提供食材, -1)) * 250
NEXT
TARGET = LOCAL:1

TCVAR:MASTER:摂取カロリー += (6 - MATCH(提供食材, -1)) * 250
RFLAG:コマンド結果受渡し変数３ = (6 - MATCH(提供食材, -1)) * 250
;食事後は三時間ほど食事不可
TCVAR:MASTER:満腹 = 180
;大失敗時は何も食べられない
SIF RFLAG:コマンド結果受渡し変数 == -2
	TCVAR:MASTER:満腹 = 0

知識素質:PLAYER:料理人経験値 += ABS(RFLAG:コマンド結果受渡し変数) * 10 + RAND:20
TIME += 60
RETURN 1

@COM380_0_304_追加処理(提供食材:0, 提供食材:1, 提供食材:2, 提供食材:3, 提供食材:4)

;満腹時間が残ってるなら何もしない
SIF TCVAR:満腹
	RETURN 0



;歓楽と征服の基礎値
LOCAL:0 = 500
LOCAL:1 = 300

FOR LOCAL:2, 0, 5
	IF 提供食材:(LOCAL:2) != -1
		LOCAL:0 += 食材効力(DT_CELL_GETS("所持素材データベース", 提供食材:(LOCAL:2), "素材アイテム名", 1))
	ENDIF
NEXT

;食事後は三時間ほど食事不可
TCVAR:満腹 = 180

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -2
		TIMES LOCAL:0 , 0
		TIMES LOCAL:1 , 0
		
		;大失敗時は何も食べられない
		TCVAR:満腹 = 0
	CASE -1
		TIMES LOCAL:0 , 0.50
		TIMES LOCAL:1 , 0.50
		BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 20 , MAXBASE:体力)
	CASE 1
		BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 10 , MAXBASE:体力)
	CASE 2
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
		BASE:体力 = MIN(BASE:体力 + MAXBASE:体力 / 5 , MAXBASE:体力)
ENDSELECT

IF TALENT:TARGET:飲食不可
	LOCAL:0 = LOCAL:0 / 2
	LOCAL:1 = LOCAL:1 / 2
ENDIF

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
CALL SOURCE_CALC_好感度要素_信頼度UP(TARGET, PLAYER, LOCAL:0 / 3)
知識素質:料理人経験値 += ABS(RFLAG:コマンド結果受渡し変数) * 10 + RAND:20

;関係性上昇(基本15分で+1、大成功で1.5倍、失敗時は半減)
LOCAL:2 = RFLAG:コマンド結果受渡し変数 > 0 ? RFLAG:コマンド結果受渡し変数 / 10# -1
CALL 関係性一括上昇(TARGET, 4, LOCAL:2)




;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_0_304
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;満腹
SIF TCVAR:MASTER:満腹
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_0_304

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@と
PRINT バーベキューをした

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -2
		;大失敗時
		PRINTL が、
		PRINTW 料理を大失敗して何も食べられなかった…
	CASE -1
		;失敗時
		PRINTL 
		PRINTW ところどころコゲた肉や野菜が目立ったが、お腹はいっぱいになった。
	CASE 1
		;成功時
		PRINTL 
		PRINTFORMW %CALLNAME:(RFLAG:コマンド結果受渡し変数２)%が美味しく焼いた肉でお腹がいっぱいになった
	CASE 2
		;大成功時
		PRINTL 
		PRINTFORMW %CALLNAME:(RFLAG:コマンド結果受渡し変数２)%が焼いた串は絶品で、\@ GET_TARGETNUM() > 1 ? みんなは # ふたりとも\@大満足で満腹になった！
ENDSELECT

PRINTFORML （摂取カロリー：{RFLAG:コマンド結果受渡し変数３}kcal）
