
;-------------------------------------------------
;05_504_01ボドゲエリアですごろくで遊ぶ
;-------------------------------------------------

@COMNAME_PLACE_380_0_504
#FUNCTION
IF 施設改造度:5:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = すごろくで遊ぶ
RETURNF 1
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_0_504
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")
		
@COM380_0_504

;勝敗決定
CALL すごろく勝負
;勝利者のキャラ番号がRFLAG:コマンド結果受渡し変数に返ってくる
;あなた勝利の場合はRFLAG:コマンド結果受渡し変数に-1が入る

;その場にいる全員で
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):隠密
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM380_0_504_追加処理
NEXT
TARGET = LOCAL:1

CALL 同室者縁結び進行処理(RAND:10 + 4)

TIME += 60
RETURN 1

@COM380_0_504_追加処理

;歓楽強度
LOCAL:0 = 700
;征服強度
LOCAL:1 = 300

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
IF TARGET == RFLAG:コマンド結果受渡し変数
	;勝利者のみ征服ＵＰ
	CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
ENDIF

;関係性上昇(基本15分で+1、勝利者は1.5倍、それ以外は半減)
LOCAL:2 = RFLAG:コマンド結果受渡し変数 == TARGET ? 1# -1
CALL 関係性一括上昇(TARGET, 4, LOCAL:2)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_0_504
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_0_504

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 達 # \@とすごろくで遊んだ。
IF RFLAG:コマンド結果受渡し変数 > 0
	PRINTFORML 白熱した戦いの結果、%CALLNAME:(RFLAG:コマンド結果受渡し変数)%が勝利した！
ELSEIF RFLAG:コマンド結果受渡し変数 < 0
	PRINTFORML 白熱した戦いの結果、%CALLNAME:MASTER%が勝利した！
ENDIF

