
@COMNAME300
#FUNCTIONS
TSTR:コマンド名受渡 = 会話

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_300
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常_飲み会")

@COM_TOOLTIP_300
LOCALS = <br>■会話<br>
LOCALS += "何気ない日常会話を交わすコマンド。一定確率で大成功、成功、失敗に分岐。<br>短時間に実行しすぎると会話のネタが無くなり失敗する。<br>素質：恋慕を所持している相手の場合、あるいは飲み会時、この制限は無効となる。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：歓楽・征服"
LOCALS += "<br>固有の取得経験：会話経験(MASTER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM300
;会話

;会話累積値が閾値を超えると失敗
IF TCVAR:話題無しフラグ && !TALENT:恋慕 && TFLAG:調教モード != 3
	PRINTFORML %CALLNAME:TARGET%に話しかけようとしたが言葉が見つからない
	PRINTW あたりに気まずい空気が流れた…
	TIME += 0
	;口上分岐としてはここは大失敗扱いがいいんじゃないかな
	RFLAG:コマンド結果受渡し変数 = -2
	RETURN 0
ENDIF

LOCAL = 0
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 1
	EXP:MASTER:会話経験 ++
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 0
ENDIF


;歓楽強度
LOCAL:0 = 1300
;征服強度
LOCAL:1 = 50

IF RFLAG:コマンド結果受渡し変数 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF
SELECTCASE ABL:MASTER:話術技能
	CASE 0
		TIMES LOCAL:0 , 0.20
		TIMES LOCAL:1 , 0.20
	CASE 1
		TIMES LOCAL:0 , 0.40
		TIMES LOCAL:1 , 0.40
	CASE 2
		TIMES LOCAL:0 , 0.70
		TIMES LOCAL:1 , 0.70
	CASE 3
		TIMES LOCAL:0 , 1.00
		TIMES LOCAL:1 , 1.00
	CASE 4
		TIMES LOCAL:0 , 1.50
		TIMES LOCAL:1 , 1.50
	CASEELSE
		TIMES LOCAL:0 , 5.00
		TIMES LOCAL:1 , 5.00
ENDSELECT


CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)

;会話累積値
LOCAL = 200 - (TALENT:TARGET:会話資質 * 50) - (TALENT:PLAYER:会話資質 * 50)
TCVAR:会話累積値 += 0

TIME += 0
EXP:MASTER:会話経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE300
;会話実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(300)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;口を何かに使ってるなら無理
SIF TEQUIP:PLAYER:口
	RETURN 0
SIF TEQUIP:TARGET:口
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM300
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%と
IF TALENT:恋慕
	PRINTDATA
		DATAFORM 指先を絡ませながら
		DATAFORM 隣りあって
		DATAFORM 少し照れながら
		DATAFORM \@ TIME >= 360 && TIME <= 1080 ? 昼の光の中で # 夜の静けさの中で \@
		DATAFORM じゃれ合いながら
		DATAFORM 微笑みを交わしながら
	ENDDATA
ELSE
	PRINTDATA
		DATAFORM 世間
		DATAFORM とりとめのない
		DATAFORM 下らない
		DATAFORM たわいない
		DATAFORM 取るに足らない
		DATAFORM 少し
	ENDDATA
ENDIF
PRINTFORML 話をした
IF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORML 楽しい時間が流れた…
ELSEIF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORML 会話が途切れると気まずい空気が流れた…
ENDIF


