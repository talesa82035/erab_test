
@指輪個別文章表示_覇業の指輪・壁
LOCALS = 
LOCALS += @"■覇業の指輪・壁<br><br>"
LOCALS += "魔力を引き出す高品質な指輪。<br>"
LOCALS += "完全に防御にのみ特化しており、騎空士には不人気。<br>"
LOCALS += "反面、護身を求める一般人には需要が高い。<br>"
LOCALS += "汎用品であるため全空で広く使われており、ダンジョンにも良く落ちている。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 35000

@指輪基礎効果文章表示_覇業の指輪・壁(指輪番号 = 0, 効果番号 = -1)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+800 防御力+400 回復力+200 行動速度+200<br>"
IF 効果番号 > -1
	;効果を指定されているのでランダム部分固定
	LOCALS:1 '= @"%属性数値文字列変換(効果番号 - 1)%耐性+25"
ELSEIF 指輪番号 < 1
	;所持してない指輪なのでランダム表記
	LOCALS:1 '= "ランダム属性耐性+25"
ELSE
	;所持してる指輪なのでランダム部分を引っ張ってくる
	LOCALS:1 '= DT_CELL_GETS("所持指輪データベース", 指輪番号, "基礎ステータス補正", 1)
	LOCALS:1 '= REPLACE(LOCALS:1, "最大ＨＰ_800_防御力_400_回復力_200_行動速度_200_命中率_10_", "")
	LOCALS:1 '= REPLACE(LOCALS:1, "_", "+")
ENDIF
LOCALS += @"命中率+10\% %LOCALS:1%<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%
;ランダム効果数を返す
RETURN 6

@指輪出現率_覇業の指輪・壁(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 4
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@指輪ステータス決定_覇業の指輪・壁(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_800_防御力_400_回復力_200_行動速度_200_命中率_10

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF

IF 効果指定 > -1
	LOCAL = 効果指定
ELSE
	LOCAL = RAND:6
ENDIF
基本補正文字列 += @"_%属性数値文字列変換(LOCAL)%耐性_25"

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "覇業の指輪・壁", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)
