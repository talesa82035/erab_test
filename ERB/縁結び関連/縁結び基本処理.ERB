
@縁結びメニュー
#DIM ページ数
#DIMS 表示文字列１
#DIMS 表示文字列２
#DIM DYNAMIC 縁結び保存配列, 100000, 6
#DIM DYNAMIC ソート用配列, 100000
#DIM CONST 対象キャラ１ = 0
#DIM CONST 対象キャラ２ = 1
#DIM CONST 進展度 = 2
#DIM CONST 縁結び種別 = 3
#DIM CONST 受け攻め = 4
#DIM CONST id = 5
#DIM 表示進展度
#DIM 表示１
#DIM 表示２
#DIM 右画面表示

;起床前から飛んでくる場所、今の縁結びが一覧で見られる
;まずは一覧を登録
VARSET ソート用配列
VARSET 縁結び保存配列
DT_SELECT "縁結びデータベース", ,"進展度 DESC", ソート用配列
LOCAL:1 = 0
FOR LOCAL, 0, DT_ROW_LENGTH("縁結びデータベース")
	縁結び保存配列:(LOCAL:1):対象キャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", ソート用配列:LOCAL, "対象キャラ１", 1))
	縁結び保存配列:(LOCAL:1):対象キャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", ソート用配列:LOCAL, "対象キャラ２", 1))
	縁結び保存配列:(LOCAL:1):進展度 = DT_CELL_GET("縁結びデータベース", ソート用配列:LOCAL, "進展度", 1)
	IF DT_CELL_GETS("縁結びデータベース", ソート用配列:LOCAL, "縁結び種別", 1) == "恋慕"
		縁結び保存配列:(LOCAL:1):縁結び種別 = 0
	ELSEIF DT_CELL_GETS("縁結びデータベース", ソート用配列:LOCAL, "縁結び種別", 1) == "セフレ"
		縁結び保存配列:(LOCAL:1):縁結び種別 = 1
	ENDIF
	IF DT_CELL_GETS("縁結びデータベース", ソート用配列:LOCAL, "受け攻め設定", 1) == ""
		縁結び保存配列:(LOCAL:1):受け攻め = 0
	ELSEIF DT_CELL_GETS("縁結びデータベース", ソート用配列:LOCAL, "受け攻め設定", 1) == "順"
		縁結び保存配列:(LOCAL:1):受け攻め = 1
	ELSEIF DT_CELL_GETS("縁結びデータベース", ソート用配列:LOCAL, "受け攻め設定", 1) == "逆"
		縁結び保存配列:(LOCAL:1):受け攻め = 2
	ENDIF
	縁結び保存配列:(LOCAL:1):id = ソート用配列:LOCAL
	LOCAL:1 ++
NEXT

DRAWLINE
PRINTL 任意のキャラクターが他のキャラクターと仲を深めるように誘導します。
PRINTL 仲が十分深まった時に恋人同士、あるいはセフレとなります。
PRINTL ※成立したキャラの縁結びは取り消すことが出来ません。
DRAWLINE

;左側ウィンドウ
LOCALS = <div rect='81,0,1937,2812' padding='50, 25, 50' border='31' bcolor='#C0C0C0'>
LOCALS += "<button value='-1'>[-1] 新しい縁結びを設定する</button><br>"
LOCALS += "<br>"
LOCALS += "<button value='-2'>[-2] ランダムに縁を結ぶ</button><br>"
LOCALS += "<br>"
LOCALS += "<button value='1000'>[1000] システムの説明</button><br>"
LOCALS += "<button value='1001'>[1001] オプション</button><br>"
IF 右画面表示 == 0
	LOCALS += "<button value='1002'>[1002] 成立済関係を見る</button><br>"
ELSE
	LOCALS += "<button value='1002'>[1002] 縁結びを見る</button><br>"
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='9999'>[9999] 戻る</button><br>"
LOCALS += "</div>"

;右側ウィンドウ
LOCALS += "<div rect='2050,0,3875,2812' padding='50, 25, 50' border='31' bcolor='#C0C0C0'>"
IF 右画面表示 == 0
	LOCALS += "■設定中縁結びリスト<br>"
	FOR LOCAL, 0, VARSIZE("縁結び保存配列", 1)
		IF 縁結び保存配列:LOCAL:対象キャラ１ < 1
			BREAK
		ENDIF
		SIF ページ数 != LOCAL / 21
			CONTINUE
		表示文字列１ = %CALLNAME:(縁結び保存配列:LOCAL:対象キャラ１)%
		SELECTCASE TALENT:(縁結び保存配列:LOCAL:対象キャラ１):性別
			CASE 0
				表示文字列１ += "(無"
			CASE 1
				表示文字列１ += "(♀"
			CASE 2
				表示文字列１ += "(♂"
			CASE 3
				表示文字列１ += "(双"
		ENDSELECT
		SELECTCASE 縁結び保存配列:LOCAL:受け攻め
			CASE 0
				表示文字列１ += ")"
			CASE 1
				表示文字列１ += "攻)"
			CASE 2
				表示文字列１ += "受)"
		ENDSELECT
		表示文字列２ = %CALLNAME:(縁結び保存配列:LOCAL:対象キャラ２)%
		SELECTCASE TALENT:(縁結び保存配列:LOCAL:対象キャラ２):性別
			CASE 0
				表示文字列２ += "(無"
			CASE 1
				表示文字列２ += "(♀"
			CASE 2
				表示文字列２ += "(♂"
			CASE 3
				表示文字列２ += "(双"
		ENDSELECT
		SELECTCASE 縁結び保存配列:LOCAL:受け攻め
			CASE 0
				表示文字列２ += ")"
			CASE 1
				表示文字列２ += "受)"
			CASE 2
				表示文字列２ += "攻)"
		ENDSELECT
		VARSET ソート用配列
		表示進展度 = 縁結び保存配列:LOCAL:進展度
		IF 縁結び保存配列:LOCAL:縁結び種別 == 0
			;恋慕
			IF DT_SELECT("縁結びデータベース", @"対象キャラ１ = {CFLAG:(縁結び保存配列:LOCAL:対象キャラ２):人物番号} And 対象キャラ２ = {CFLAG:(縁結び保存配列:LOCAL:対象キャラ１):人物番号} And 縁結び種別 = '恋慕'", , ソート用配列)
				表示進展度 = MAX(表示進展度, DT_CELL_GET("縁結びデータベース", ソート用配列:0, "進展度", 1))
			ENDIF
			IF 表示進展度 >= 100
				LOCALS += @"　<button value='{LOCAL}'>[{LOCAL}] %表示文字列１, 20, LEFT%✕%表示文字列２, 20, LEFT% <img src='えっちハート'><font color='#%カラーパレット_HTML("ピンク")%'>恋慕関係</font><img src='えっちハート'></button><br>"
			ELSE
				LOCALS += @"　<button value='{LOCAL}'>[{LOCAL}] %表示文字列１, 20, LEFT%＞%表示文字列２, 20, LEFT% 恋慕進展度：{表示進展度}％</button><br>"
			ENDIF
		ELSEIF 縁結び保存配列:LOCAL:縁結び種別 == 1
			;セフレ
			IF DT_SELECT("縁結びデータベース", @"対象キャラ１ = {CFLAG:(縁結び保存配列:LOCAL:対象キャラ２):人物番号} And 対象キャラ２ = {CFLAG:(縁結び保存配列:LOCAL:対象キャラ１):人物番号} And 縁結び種別 = 'セフレ'")
				表示進展度 = MAX(表示進展度, DT_CELL_GET("縁結びデータベース", ソート用配列:0, "進展度", 1))
			ENDIF
			IF 表示進展度 >= 100
				LOCALS += @"　<button value='{LOCAL}'>[{LOCAL}] %表示文字列１, 20, LEFT%✕%表示文字列２, 20, LEFT% <font color='#%カラーパレット_HTML("薄ピンク")%'>セフレ関係</font><img src='えっちハート'></button><br>"
			ELSE
				LOCALS += @"　<button value='{LOCAL}'>[{LOCAL}] %表示文字列１, 20, LEFT%＞%表示文字列２, 20, LEFT% セフレ進展度：{表示進展度}％</button><br>"
			ENDIF
		ENDIF
	NEXT
ELSE
	LOCALS += "■成立関係性リスト<br>"
	FOR LOCAL, 0, DT_ROW_LENGTH("関係性データベース")
		SIF ページ数 != LOCAL / 21
			CONTINUE
		表示１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", LOCAL, "対象キャラ１"))
		表示文字列１ = %CALLNAME:表示１%
		SELECTCASE TALENT:表示１:性別
			CASE 0
				表示文字列１ += "(無"
			CASE 1
				表示文字列１ += "(♀"
			CASE 2
				表示文字列１ += "(♂"
			CASE 3
				表示文字列１ += "(双"
		ENDSELECT
		表示文字列１ += ")"
		; SELECTCASE 縁結び保存配列:LOCAL:受け攻め
		; 	CASE 0
		; 		表示文字列１ += ")"
		; 	CASE 1
		; 		表示文字列１ += "攻)"
		; 	CASE 2
		; 		表示文字列１ += "受)"
		; ENDSELECT
		表示２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", LOCAL, "対象キャラ２"))
		表示文字列２ = %CALLNAME:表示２%
		SELECTCASE TALENT:表示２:性別
			CASE 0
				表示文字列２ += "(無"
			CASE 1
				表示文字列２ += "(♀"
			CASE 2
				表示文字列２ += "(♂"
			CASE 3
				表示文字列２ += "(双"
		ENDSELECT
		表示文字列２ += ")"
		; SELECTCASE 縁結び保存配列:LOCAL:受け攻め
		; 	CASE 0
		; 		表示文字列２ += ")"
		; 	CASE 1
		; 		表示文字列２ += "受)"
		; 	CASE 2
		; 		表示文字列２ += "攻)"
		; ENDSELECT
		SELECTCASE DT_CELL_GETS("関係性データベース", LOCAL, "関係性種別")
			CASE "恋慕"
				LOCALS += @"　<button value='{LOCAL}'>[{LOCAL}] %表示文字列１, 20, LEFT%✕%表示文字列２, 20, LEFT% <img src='えっちハート'><font color='#%カラーパレット_HTML("ピンク")%'>恋慕関係</font><img src='えっちハート'></button><br>"
			CASE "セフレ"
				LOCALS += @"　<button value='{LOCAL}'>[{LOCAL}] %表示文字列１, 20, LEFT%✕%表示文字列２, 20, LEFT% <font color='#%カラーパレット_HTML("薄ピンク")%'>セフレ関係</font><img src='えっちハート'></button><br>"
		ENDSELECT
	NEXT
ENDIF
LOCALS += "</div>"
LOCALS += "<div rect='2200,2650,3875,200'>"
IF ページ数 > 0
	LOCALS += "<button value='9002'>[前のページへ]</button>　　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 縁結び保存配列:(ページ数 * 21 + 21):対象キャラ１ > 0
	LOCALS += "<button value='9003'>[次のページへ]</button>　　　"
ENDIF
LOCALS += "</div>"

HTML_PRINT LOCALS

FOR LOCAL, 0, 30
	PRINTL
NEXT

IF チュートリアルフラグ:縁結び == 0
	LOCAL = 1000
ELSE
	BINPUT
	LOCAL = RESULT
ENDIF
SELECTCASE LOCAL
	CASE -1
		CALL 縁結び左キャラセット()
	CASE -2
		CALL ランダム縁結び処理()
	CASE 9999
		ページ数 = 0
		RETURN 0
	CASE 1000
		CALL 縁結びシステムチュートリアル
		DRAWLINE
	CASE 1001
		CALL OPTION_機能切り替え(700)
	CASE 1002
		INVERTBIT 右画面表示, 0
	CASE 9002
		ページ数 --
	CASE 9003
		ページ数 ++
	CASEELSE
		IF 右画面表示 == 0
			CALL 縁結び詳細処理(縁結び保存配列:LOCAL:id)
		ELSE
			CALL 関係性詳細処理(DT_CELL_GET("関係性データベース", LOCAL, "id"))
		ENDIF
ENDSELECT

RESTART

@縁結び左キャラセット()
#DIM 表示候補, キャラクタ数上限
#DIM ページ数
#DIM 登録総数
#DIMS 表示文字列
#DIMS ソート情報

VARSET 表示候補

登録総数 = 0
LOCALS = 
;配列に登録
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	RESULT = 0
	TRYCALLFORM 縁結び特殊条件_左キャラ時_{NO:LOCAL}(LOCAL)
	IF RESULT == 0
		SIF TALENT:LOCAL:性別 == 0
			CONTINUE
		SIF TALENT:LOCAL:性欲 == -2
			CONTINUE
		SIF 知識素質:LOCAL:性知識 < 0 && 恋慕ありチェック(LOCAL) < 1 && セフレありチェック(LOCAL) < 1
			CONTINUE
		SIF GETBIT(CSVTALENT(NO:LOCAL, GETNUM(TALENT, "陥落不可")), 0) && GETBIT(TALENT:LOCAL:陥落不可, 1)
			CONTINUE
	ELSEIF RESULT == -1
		CONTINUE
	ENDIF
	表示候補:登録総数 = LOCAL
	登録総数 ++
NEXT
CALL キャラ配列フィルタ処理(表示候補)

$表示位置

;キャラ一覧を表示
DRAWLINE
PRINTL 誰の縁を結びますか？
PRINTL ※現在リゾートにいるキャラが対象です
PRINTL ※性別なし、性欲なし、性知識がマイナス、元が陥落不可のキャラについては選択出来ません
PRINTL 　（特定のキャラについては選択出来ることがあります）
DRAWLINE

LOCAL:1 = 0
LOCALS = 
FOR LOCAL, 0, CHARANUM
	SIF 表示候補:LOCAL < 1
		BREAK
	表示文字列 = 
	IF ページ数 == LOCAL:1 / 30
		表示文字列 '= @"<button value='{LOCAL}'>[{LOCAL, 3}] %GET_NAME(表示候補:LOCAL, 1)%</button>　"
		IF 文字色グループ取得(表示候補:LOCAL) >= 0
			LOCALS += @"<font color='#%十六進数文字列化(同室時_グループ文字色:(文字色グループ取得(表示候補:LOCAL)))%'>%表示文字列%</font>"
		ELSEIF CFLAG:(表示候補:LOCAL):同室文字色
			LOCALS += @"<font color='#%十六進数文字列化(CFLAG:(表示候補:LOCAL):同室文字色)%'>%表示文字列%</font>"
		ELSE
			LOCALS += 表示文字列
		ENDIF
		ソート情報 '= ソート情報表示(表示候補:LOCAL, 1)
		SIF ソート情報 != ""
			LOCALS += @"　%ソート情報%"
		LOCALS += "　"
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
DRAWLINE
IF ページ数 > 0
	PRINTBUTTONLC "[9000] 前のページへ", 9000
ELSE
	PRINTC   
ENDIF
IF ページ数 < (LOCAL:1 + 1) / 30
	PRINTBUTTONLC "[9001] 次のページへ", 9001
ELSE
	PRINTC   
ENDIF
PRINTL
DRAWLINE
CALL フィルタソートボタンセット表示(9020, 9030, 9031, 9040)
DRAWLINE
PRINTBUTTON "[9999] 戻る", 9999

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9020, 9030, 9031, 9040
		CALL フィルタソートボタンセット実行部(LOCAL, 9020, 9030, 9031, 9040)
		ページ数 = 0
		RESTART
	CASE 9999
		ページ数 = 0
		RETURN 0
	CASE 9000
		ページ数 --
		GOTO 表示位置
	CASE 9001
		ページ数 ++
		GOTO 表示位置
	CASEELSE
		CALL 縁結び右キャラセット(表示候補:LOCAL)
ENDSELECT

RESTART

;恋愛対象外チェック()

@縁結び右キャラセット(左キャラ, 全キャラランダムモード = 0, 候補取得モード = 0)
#DIM 左キャラ
#DIM 全キャラランダムモード
#DIM 対象キャラ
#DIM 候補取得モード
#DIM ページ数
#DIM 表示数
#DIM 初期進展度
#DIM セットモード
#DIM 非滞在非表示
#DIM 既に恋慕フラグ
#DIM 候補配列, キャラクタ数上限
#DIM 汎用セット不可フラグ
#DIM 固有条件なし
#DIMS 判別文字列
#DIMS 表示文字列
#DIMS ソート情報

;ランダムモードに１が入ってれば恋慕、２が入ってればセフレで処理する
SIF 全キャラランダムモード
	セットモード = 全キャラランダムモード - 1

;左側問題の先判定
汎用セット不可フラグ = 0
IF セットモード
	;基本的に特別な条件が無い限り、左キャラにセフレ陥落がないと選べない
	SIF セフレありチェック(左キャラ) == 0 && フィールド展開:セフレ縁結び緩和 == 0
		汎用セット不可フラグ = 1
	;緩和してようが無知・獣並みキャラは既にセフレがないと選べない
	SIF セフレありチェック(左キャラ) == 0 && 知識素質:左キャラ:性知識 < 0
		汎用セット不可フラグ = 1
	;セフレ不可はダメ
	SIF GETBIT(TALENT:左キャラ:陥落不可, 1)
		汎用セット不可フラグ = 1
	判別文字列 = セフレ
ELSE
	IF TALENT:左キャラ:ハーレムの心得 == 0
		;恋慕陥落状態の時は恋慕陥落は選べない
		IF TALENT:左キャラ:恋慕 > 0 || TALENT:左キャラ:疑似恋慕 > 0
			汎用セット不可フラグ = 2
		ENDIF
		IF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:左キャラ:人物番号} Or 対象キャラ２ = {CFLAG:左キャラ:人物番号}) And 関係性種別 = '恋慕'")
			汎用セット不可フラグ = 2
		ENDIF
		;既に恋慕セットしてたらアウト
		IF DT_SELECT("縁結びデータベース", @"対象キャラ１ = {CFLAG:左キャラ:人物番号} And 縁結び種別 = '恋慕'")
			汎用セット不可フラグ = 3
		ENDIF
	ENDIF
	;無知・獣並みキャラは既に恋慕がないと選べない（実質ハーレム必須）
	SIF 恋慕ありチェック(左キャラ) == 0 && 知識素質:左キャラ:性知識 < 0
		汎用セット不可フラグ = 1
	;CSVで恋慕不可はダメ
	SIF	GETBIT(CSVTALENT(NO:左キャラ, GETNUM(TALENT, "陥落不可")), 0)
		汎用セット不可フラグ = 1
	判別文字列 = 恋慕
ENDIF

表示数 = 0
既に恋慕フラグ = 0
VARSET 候補配列
FOR 対象キャラ, 1, CHARANUM
	固有条件なし = 0
	SIF 対象キャラ == 左キャラ
		CONTINUE
	;優先条件
	;招待可能フラグによる制御
	SIF CFLAG:対象キャラ:招待不可フラグ
		CONTINUE
	;非滞在非表示
	SIF 非滞在非表示 && CFLAG:対象キャラ:滞在期間 < 0
		CONTINUE
	;同じ相手はダメ
	SIF DT_SELECT("縁結びデータベース", @"対象キャラ１ = {CFLAG:左キャラ:人物番号} And 対象キャラ２ = {CFLAG:対象キャラ:人物番号}")
		CONTINUE
	SIF DT_SELECT("関係性データベース", @"対象キャラ１ = {CFLAG:左キャラ:人物番号} And 対象キャラ２ = {CFLAG:対象キャラ:人物番号} And 関係性種別 = '%判別文字列%'")
		CONTINUE
	SIF DT_SELECT("関係性データベース", @"対象キャラ１ = {CFLAG:対象キャラ:人物番号} And 対象キャラ２ = {CFLAG:左キャラ:人物番号} And 関係性種別 = '%判別文字列%'")
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(対象キャラ) && CFLAG:対象キャラ:滞在期間 < 0
		CONTINUE
		
	;オート縁結びの制限
	IF RFLAG:オート縁結び中
		;滞在者のみ
		SIF CFLAG:対象キャラ:滞在期間 <= 0
			CONTINUE
		;ある程度仲の良い相手のみ。互いに10以上かこっち側が30以上
		SIF GET_RELATION(左キャラ, 対象キャラ) < 30 && (GET_RELATION(左キャラ, 対象キャラ) < 10 || GET_RELATION(対象キャラ, 左キャラ) < 10)
			CONTINUE
		;性別嗜好を見る
		SIF TALENT:左キャラ:性別嗜好 == 1 && TALENT:対象キャラ:性別 == 2
			CONTINUE
		SIF TALENT:左キャラ:性別嗜好 == 2 && TALENT:対象キャラ:性別 == 1
			CONTINUE
		SIF TALENT:左キャラ:性別嗜好 == 3
			CONTINUE
		;オプションを見る
		IF GETBIT(OPTION変数:オート縁結び詳細, 2) == 0
			IF CSTR:左キャラ:出演作品 == "" || CSTR:左キャラ:出演作品 == "グランブルーファンタジー"
				SIF CSTR:対象キャラ:出演作品 != "" && CSTR:対象キャラ:出演作品 != "グランブルーファンタジー"
					CONTINUE
			ELSE
				SIF CSTR:左キャラ:出演作品 != CSTR:対象キャラ:出演作品
					CONTINUE
			ENDIF
		ENDIF
	ENDIF

	;口上側で設定可能な条件
	RESULT = 0
	IF セットモード
		TRYCCALLFORM 縁結び相手条件_セフレ_{NO:左キャラ}(左キャラ, 対象キャラ)
		CATCH
			固有条件なし = 1
		ENDCATCH
	ELSE
		TRYCCALLFORM 縁結び相手条件_恋慕_{NO:左キャラ}(左キャラ, 対象キャラ)
		CATCH
			固有条件なし = 1
		ENDCATCH
	ENDIF
	IF RESULT == 0
		;基本的に無性別は対象にならない
		SIF TALENT:対象キャラ:性別 == 0
			CONTINUE

		; ;性知識がマイナスなら恋愛という概念がないので対象外
		; IF 知識素質:対象キャラ:性知識 < 0
		; 	;ただし、恋慕やセフレがあるなら対象になる
		; 	IF TALENT:対象キャラ:恋慕 || TALENT:対象キャラ:恋人持ち
		; 	ELSEIF TALENT:対象キャラ:セフレ || TALENT:対象キャラ:セフレあり
		; 	ELSE
		; 		CONTINUE
		; 	ENDIF
		; ENDIF

		IF OPTION変数:縁結び同性愛 == 0
			IF 同性愛性癖判定(左キャラ, 対象キャラ)
				;同性は同性愛系の性癖があれば対象になる
				IF 性癖素質:左キャラ:同性愛 <= 0
					CONTINUE
				ENDIF
			ENDIF
		ENDIF
		;汎用条件を持ってくる
		IF 汎用セット不可フラグ
			SIF 既に恋慕フラグ >= 0
				既に恋慕フラグ = 汎用セット不可フラグ - 1
			SIF 固有条件なし
				BREAK
			CONTINUE
		ENDIF
	ELSEIF RESULT == -1
		CONTINUE
	ELSEIF RESULT == 1
		既に恋慕フラグ = -1
	ENDIF

	候補配列:表示数 = 対象キャラ
	表示数 ++

	;候補取得モードが１なら候補が存在する時点で戻す
	IF 候補取得モード == 1
		RETURN 1
	ENDIF
NEXT

;候補取得モードが１ならここに来た時点で候補なしなので戻す
SIF 候補取得モード == 1
	RETURN 0


IF 全キャラランダムモード == 0
	CALL キャラ配列フィルタ処理(候補配列)

	$表示位置
	;キャラ一覧を表示
	DRAWLINE
	PRINTFORML %NAME表示(左キャラ)%　と誰の縁を結びますか？
	PRINTL ※リゾートにいないキャラも選択出来ます。
	PRINTL ※恋愛対象外のキャラについては選択出来ません。
	PRINTL 　（特定のキャラについては選択出来ることがあります）
	IF 非滞在非表示
		HTML_PRINT @"<button value='9002'>[9002] リゾートに滞在していないキャラを<font color='#%カラーパレット_HTML("グレーアウト")%'>表示</font> / 非表示</button>"
	ELSE
		HTML_PRINT @"<button value='9002'>[9002] リゾートに滞在していないキャラを表示 / <font color='#%カラーパレット_HTML("グレーアウト")%'>非表示</font></button>"
	ENDIF
	DRAWLINE
	IF セットモード == 0
		HTML_PRINT @"　　<font color='#%カラーパレット_HTML("黄")%'>[恋慕の縁]</font>　　<button value='9011'>[セフレの縁]</button>"
	ELSE
		HTML_PRINT @"　　<button value='9010'>[恋慕の縁]</button>　　<font color='#%カラーパレット_HTML("黄")%'>[セフレの縁]</font>"
	ENDIF
	DRAWLINE
	LOCALS = 
	IF 既に恋慕フラグ == 1
		LOCALS += "既に恋人がいます。"
		LOCALS += "<br><br><br><br><br><br><br><br><br><br><br><br><br><br><br>" 
		表示数 = 0
	ELSEIF 既に恋慕フラグ == 2
		LOCALS += "既に恋慕の縁結びを設定済みです。"
		LOCALS += "<br><br><br><br><br><br><br><br><br><br><br><br><br><br><br>" 
		表示数 = 0
	ELSE
		FOR 対象キャラ, ページ数 * 30, ページ数 * 30 + 30
			IF 対象キャラ % 2 == 0 && 対象キャラ != ページ数 * 30
				LOCALS += "<br>" 
			ENDIF
			表示文字列 = 
			IF 候補配列:対象キャラ > 0
				表示文字列 '= @"<button value='{候補配列:対象キャラ}'>[{候補配列:対象キャラ, 4}] %GET_NAME(候補配列:対象キャラ, 1)%</button>　　　　　　　　"
				IF 文字色グループ取得(候補配列:対象キャラ) >= 0
					LOCALS += @"<font color='#%十六進数文字列化(同室時_グループ文字色:(文字色グループ取得(候補配列:対象キャラ)))%'>%表示文字列%</font>"
				ELSEIF CFLAG:(候補配列:対象キャラ):同室文字色
					LOCALS += @"<font color='#%十六進数文字列化(CFLAG:(候補配列:対象キャラ):同室文字色)%'>%表示文字列%</font>"
				ELSE
					LOCALS += 表示文字列
				ENDIF
				ソート情報 '= ソート情報表示(候補配列:対象キャラ, 1)
				SIF ソート情報 != ""
					LOCALS += @"　%ソート情報%"
				LOCALS += "　"
			ENDIF
		NEXT

		IF 表示数
			PRINTBUTTON "[9100] 候補からランダムに選ぶ", 9100
			PRINTL 
			DRAWLINE
		ENDIF
	ENDIF
	HTML_PRINT LOCALS
	DRAWLINE
	IF ページ数 > 0
		PRINTBUTTONLC "[9000] 前のページへ", 9000
	ELSE
		PRINTC   
	ENDIF
	IF 表示数 >= ページ数 * 30 + 30
		PRINTBUTTONLC "[9001] 次のページへ", 9001
	ELSE
		PRINTC   
	ENDIF
	PRINTL
	DRAWLINE
	CALL フィルタソートボタンセット表示(9020, 9030, 9031, 9040)
	DRAWLINE
	PRINTBUTTON "[9999] 戻る", 9999

	BINPUT
	対象キャラ = RESULT
ELSE
	SIF 既に恋慕フラグ > 0
		表示数 = 0
	SIF 表示数 < 1
		RETURN 0
	対象キャラ = 9100
ENDIF
SELECTCASE 対象キャラ
	CASE 9002
		INVERTBIT 非滞在非表示, 0
		RESTART
	CASE 9100
		対象キャラ = 候補配列:(RAND:表示数)
		;データベースにセット
		IF セットモード
			LOCALS = セフレ
		ELSE
			LOCALS = 恋慕
		ENDIF
		IF 全キャラランダムモード == 0
			PRINTFORML %CALLNAME:左キャラ%に%CALLNAME:対象キャラ%への『%LOCALS%の縁』を導きました。
			PRINTW 今後のリゾート生活で縁が進行していきます。
		ELSEIF RFLAG:オート縁結び中
			RSTR:縁結び報告_記録文字列 += @"<font color='#%カラーパレット_HTML("青緑")%'>"
			RSTR:縁結び報告_記録文字列 += @"%CALLNAME:左キャラ%に%CALLNAME:対象キャラ%への『%LOCALS%の縁』が繋がりました。"
			RSTR:縁結び報告_記録文字列 +=  "</font><br>"
		ENDIF
		初期進展度 = 0
		FOR LOCAL, 0, VARSIZE("同行候補キャラ番号")
			IF 同行候補キャラ番号:対象キャラ:LOCAL == 左キャラ
				初期進展度 = MIN(同行候補キャラ確率:対象キャラ:LOCAL, 100) / 2
				BREAK
			ENDIF
		NEXT
		[IF_DEBUG]
		初期進展度 = 100
		[ENDIF]
		DT_ROW_ADD "縁結びデータベース", "対象キャラ１", CFLAG:左キャラ:人物番号, "対象キャラ２", CFLAG:対象キャラ:人物番号, "進展度", 初期進展度, "縁結び種別", LOCALS
		IF セットモード == 0 || 全キャラランダムモード
			ページ数 = 0
			セットモード = 0
			RETURN 1
		ENDIF
	CASE 9020, 9030, 9031, 9040
		CALL フィルタソートボタンセット実行部(対象キャラ, 9020, 9030, 9031, 9040)
		ページ数 = 0
		RESTART
	CASE 9999
		ページ数 = 0
		セットモード = 0
		RETURN 0
	CASE 9000
		ページ数 --
		GOTO 表示位置
	CASE 9001
		ページ数 ++
		GOTO 表示位置
	CASE 9010
		セットモード = 0
	CASE 9011
		セットモード = 1
	CASEELSE
		;データベースにセット
		PRINTFORM %CALLNAME:左キャラ%に%CALLNAME:対象キャラ%との
		IF セットモード
			PRINT 『セフレの縁』
			LOCALS = セフレ
		ELSE
			PRINT 『恋慕の縁』
			LOCALS = 恋慕
		ENDIF
		PRINTL を導きました。
		PRINTW 今後のリゾート生活で縁が進行していきます。
		初期進展度 = 0
		FOR LOCAL, 0, VARSIZE("同行候補キャラ番号")
			IF 同行候補キャラ番号:対象キャラ:LOCAL == 左キャラ
				初期進展度 = MIN(同行候補キャラ確率:対象キャラ:LOCAL, 100) / 2
				BREAK
			ENDIF
		NEXT
		[IF_DEBUG]
		初期進展度 = 100
		[ENDIF]
		DT_ROW_ADD "縁結びデータベース", "対象キャラ１", CFLAG:左キャラ:人物番号, "対象キャラ２", CFLAG:対象キャラ:人物番号, "進展度", 初期進展度, "縁結び種別", LOCALS
		IF セットモード == 0
			ページ数 = 0
			セットモード = 0
			RETURN 1
		ENDIF
ENDSELECT

RESTART

@縁結び詳細処理(表示id)
#DIM 表示id
#DIM 関連id
#DIM 対象キャラ１
#DIM 対象キャラ２
#DIMS 表示文字列１
#DIMS 表示文字列２
#DIMS 設定文字

対象キャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", 表示id, "対象キャラ１", 1))
対象キャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", 表示id, "対象キャラ２", 1))

表示文字列１ = %NAME表示(対象キャラ１)%
SELECTCASE TALENT:対象キャラ１:性別
	CASE 0
		表示文字列１ += "(無)"
	CASE 1
		表示文字列１ += "(♀)"
	CASE 2
		表示文字列１ += "(♂)"
	CASE 3
		表示文字列１ += "(双)"
ENDSELECT
表示文字列２ = %NAME表示(対象キャラ２)%
SELECTCASE TALENT:対象キャラ２:性別
	CASE 0
		表示文字列２ += "(無)"
	CASE 1
		表示文字列２ += "(♀)"
	CASE 2
		表示文字列２ += "(♂)"
	CASE 3
		表示文字列２ += "(双)"
ENDSELECT
;表示し、取り消すことが可能
DRAWLINE
PRINTFORML %表示文字列１% ＞ %表示文字列２%
PRINTFORML 　　　進展度：{DT_CELL_GET("縁結びデータベース", 表示id, "進展度", 1), 3}％
PRINTFORML 　　　　種別：%DT_CELL_GETS("縁結びデータベース", 表示id, "縁結び種別", 1)%
IF GETBIT(TALENT:対象キャラ１:性別, 1) == GETBIT(TALENT:対象キャラ２:性別, 1)
	PRINT 受け攻め設定：
	SELECTCASE DT_CELL_GETS("縁結びデータベース", 表示id, "受け攻め設定", 1)
		CASE ""
			PRINTL なし（リバ可）
		CASE "順"
			PRINTFORML %CALLNAME:対象キャラ１%攻め ✕ %CALLNAME:対象キャラ２%受け
		CASE "逆"
			PRINTFORML %CALLNAME:対象キャラ２%攻め ✕ %CALLNAME:対象キャラ１%受け
	ENDSELECT
ENDIF
DRAWLINE

PRINTL
IF GETBIT(TALENT:対象キャラ１:性別, 1) == GETBIT(TALENT:対象キャラ２:性別, 1)
	PRINTBUTTON "[0] 受けと攻めを設定する", 0
	PRINTL
ENDIF
PRINTBUTTON "[999] 戻る", 999
IF DT_CELL_GET("縁結びデータベース", 表示id, "完遂フラグ", 1)
	PRINTL
	PRINTL
	PRINTL
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [10] この縁結びを解除する
	PRINTL
	RESETCOLOR
	PRINTL ※関係成立済みの縁結びは解除することが出来ません。
ELSE
	PRINTL
	PRINTL
	PRINTL
	SETCOLOR カラーパレット("赤")
	PRINTBUTTON "[10] この縁結びを解除する", 10
	RESETCOLOR
ENDIF
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0
		DRAWLINE
		PRINTBUTTON @"[0] %NAME表示(対象キャラ１)%を攻めにする", 0
		PRINTL
		PRINTBUTTON @"[1] %NAME表示(対象キャラ２)%を攻めにする", 1
		PRINTL
		PRINTBUTTON @"[2] 受け攻め設定を削除する", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[999] 戻る", 999
		PRINTL
		DRAWLINE
		BINPUT
		SELECTCASE RESULT
			CASE 0
				設定文字 = 順
				PRINTFORMW %NAME表示(対象キャラ１)%を攻めに設定しました。
			CASE 1
				設定文字 = 逆
				PRINTFORMW %NAME表示(対象キャラ２)%を攻めに設定しました。
			CASE 2
				設定文字 = 
				PRINTFORMW 受け攻め設定を削除しました。
		ENDSELECT
		DT_CELL_SET "縁結びデータベース", 表示id, "受け攻め設定", 設定文字, 1
		IF DT_CELL_GET("縁結びデータベース", 表示id, "完遂フラグ", 1)
			;完遂時、同じ組み合わせの関係性データがあるはずなのでそっちも修正
			DT_SELECT "関係性データベース", @"対象キャラ１ = {CFLAG:対象キャラ１:人物番号} And 対象キャラ２ = {CFLAG:対象キャラ２:人物番号} And 関係性種別 = '%DT_CELL_GETS("縁結びデータベース", 表示id, "縁結び種別", 1)%'"
			関連id = RESULT:1
			DT_CELL_SET "関係性データベース", 関連id, "受け攻め設定", 設定文字, 1
		ENDIF
	CASE 999
		RETURN 0
	CASE 10
		DRAWLINE
		PRINTL この縁結びを削除してよろしいですか？
		PRINTL ※進展度は削除され、同じ組み合わせにした場合には引き継がれません。
		PRINTBUTTONLC "[0] 削除する", 0
		PRINTBUTTONLC "[1] 削除せず戻る", 1
		BINPUT
		IF RESULT == 0
			DT_ROW_REMOVE "縁結びデータベース", 表示id
			PRINTW 縁結びを削除しました。
			RETURN 0
		ENDIF
ENDSELECT

RESTART

@関係性詳細処理(表示id)
#DIM 表示id
#DIM 関連id
#DIM 対象キャラ１
#DIM 対象キャラ２
#DIMS 表示文字列１
#DIMS 表示文字列２
#DIMS 設定文字

対象キャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", 表示id, "対象キャラ１", 1))
対象キャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", 表示id, "対象キャラ２", 1))

表示文字列１ = %NAME表示(対象キャラ１)%
SELECTCASE TALENT:対象キャラ１:性別
	CASE 0
		表示文字列１ += "(無)"
	CASE 1
		表示文字列１ += "(♀)"
	CASE 2
		表示文字列１ += "(♂)"
	CASE 3
		表示文字列１ += "(双)"
ENDSELECT
表示文字列２ = %NAME表示(対象キャラ２)%
SELECTCASE TALENT:対象キャラ２:性別
	CASE 0
		表示文字列２ += "(無)"
	CASE 1
		表示文字列２ += "(♀)"
	CASE 2
		表示文字列２ += "(♂)"
	CASE 3
		表示文字列２ += "(双)"
ENDSELECT
;表示し、取り消すことが可能
DRAWLINE
PRINTFORML %表示文字列１% ＞ %表示文字列２%
PRINTFORML 　　　　種別：%DT_CELL_GETS("関係性データベース", 表示id, "関係性種別", 1)%
PRINT 　　成立経緯：
IF STRFIND(CSTR:対象キャラ１:初期関係性, @"%NAME:対象キャラ２%_%DT_CELL_GETS("関係性データベース", 表示id, "関係性種別", 1)%") >= 0 || STRFIND(CSTR:対象キャラ２:初期関係性, @"%NAME:対象キャラ１%_%DT_CELL_GETS("関係性データベース", 表示id, "関係性種別", 1)%") >= 0
	PRINTL 初期成立
ELSEIF DT_SELECT("縁結びデータベース", @"(対象キャラ１ = {CFLAG:対象キャラ１:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ１:人物番号}) And (対象キャラ１ = {CFLAG:対象キャラ２:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ２:人物番号}) And 縁結び種別 = '%DT_CELL_GETS("関係性データベース", 表示id, "縁結び種別", 1)%' And 完遂フラグ = 1")
	PRINTL 縁結びによる成立
ELSE
	PRINTL 
ENDIF
IF GETBIT(TALENT:対象キャラ１:性別, 1) == GETBIT(TALENT:対象キャラ２:性別, 1)
	PRINT 受け攻め設定：
	SELECTCASE DT_CELL_GETS("関係性データベース", 表示id, "受け攻め設定", 1)
		CASE ""
			PRINTL なし（リバ可）
		CASE "順"
			PRINTFORML %CALLNAME:対象キャラ１%攻め ✕ %CALLNAME:対象キャラ２%受け
		CASE "逆"
			PRINTFORML %CALLNAME:対象キャラ２%攻め ✕ %CALLNAME:対象キャラ１%受け
	ENDSELECT
ENDIF
DRAWLINE

PRINTL
IF GETBIT(TALENT:対象キャラ１:性別, 1) == GETBIT(TALENT:対象キャラ２:性別, 1)
	PRINTBUTTON "[0] 受けと攻めを設定する", 0
	PRINTL
ENDIF
PRINTBUTTON "[999] 戻る", 999
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0
		DRAWLINE
		PRINTBUTTON @"[0] %NAME表示(対象キャラ１)%を攻めにする", 0
		PRINTL
		PRINTBUTTON @"[1] %NAME表示(対象キャラ２)%を攻めにする", 1
		PRINTL
		PRINTBUTTON @"[2] 受け攻め設定を削除する", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[999] 戻る", 999
		PRINTL
		DRAWLINE
		BINPUT
		SELECTCASE RESULT
			CASE 0
				設定文字 = 順
				PRINTFORMW %NAME表示(対象キャラ１)%を攻めに設定しました。
			CASE 1
				設定文字 = 逆
				PRINTFORMW %NAME表示(対象キャラ２)%を攻めに設定しました。
			CASE 2
				設定文字 = 
				PRINTFORMW 受け攻め設定を削除しました。
		ENDSELECT
		DT_CELL_SET "関係性データベース", 表示id, "受け攻め設定", 設定文字, 1
		IF DT_SELECT("縁結びデータベース", @"対象キャラ１ = {CFLAG:対象キャラ１:人物番号} And 対象キャラ２ = {CFLAG:対象キャラ２:人物番号} And 縁結び種別 = '%DT_CELL_GETS("関係性データベース", 表示id, "関係性種別", 1)%'")
			;まったく同じ組み合わせがある時、そちらも修正
			関連id = RESULT:1
			DT_CELL_SET "縁結びデータベース", 関連id, "受け攻め設定", 設定文字, 1
		ENDIF
	CASE 999
		RETURN 0
ENDSELECT

RESTART


@縁結び進展しない判定(実行キャラ, 対象キャラ, 縁結び種別)
#DIM 実行キャラ
#DIM 対象キャラ
#DIM 対象外フラグ
#DIMS 縁結び種別
対象外フラグ = 0

RESULT = 0
IF 縁結び種別 == "恋慕"
	TRYCALLFORM 縁結び相手条件_恋慕_{NO:対象キャラ}(対象キャラ, 実行キャラ)
ELSE
	TRYCALLFORM 縁結び相手条件_セフレ_{NO:対象キャラ}(対象キャラ, 実行キャラ)
ENDIF
IF RESULT == 0
	IF 縁結び種別 == "恋慕"
		;既に恋慕の時、対象外
		IF TALENT:実行キャラ:ハーレムの心得 == 0
			SIF TALENT:実行キャラ:恋慕 || TALENT:実行キャラ:疑似恋慕
				対象外フラグ = 1
			SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:実行キャラ:人物番号} Or 対象キャラ２ = {CFLAG:実行キャラ:人物番号}) And 関係性種別 = '恋慕'")
				対象外フラグ = 1
		ENDIF
		IF TALENT:対象キャラ:ハーレムの心得 == 0
			SIF TALENT:対象キャラ:恋慕 || TALENT:実行キャラ:疑似恋慕
				対象外フラグ = 1
			SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And 関係性種別 = '恋慕'")
				対象外フラグ = 1
		ENDIF

	
		;CSVで恋慕不可はダメ
		SIF	GETBIT(CSVTALENT(NO:実行キャラ, GETNUM(TALENT, "陥落不可")), 0)
			対象外フラグ = 1
		SIF	GETBIT(CSVTALENT(NO:対象キャラ, GETNUM(TALENT, "陥落不可")), 0)
			対象外フラグ = 1

		;性知識がマイナスなら恋愛という概念がないので対象外
		IF 知識素質:実行キャラ:性知識 < 0
			;ただし、セフレがあるなら対象になる
			IF TALENT:実行キャラ:セフレ || TALENT:実行キャラ:セフレあり
			ELSE
				対象外フラグ = 1
			ENDIF
		ENDIF
		IF 知識素質:対象キャラ:性知識 < 0
			;ただし、セフレがあるなら対象になる
			IF TALENT:対象キャラ:セフレ || TALENT:対象キャラ:セフレあり
			ELSE
				対象外フラグ = 1
			ENDIF
		ENDIF
	ELSE
		;セフレ不可はダメ
		SIF GETBIT(TALENT:対象キャラ:陥落不可, 1)
			対象外フラグ = 1
		;フィールドない＆セフレ持ってない場合は進展しない
		SIF セフレありチェック(実行キャラ) == 0 && フィールド展開:セフレ縁結び緩和 == 0
			対象外フラグ = 1
		;セフレ時は性知識の条件なし
	ENDIF

	IF OPTION変数:縁結び同性愛 == 0
		;性別が同じ、かつふたなりでないなら恋愛対象外
		IF 同性愛性癖判定(対象キャラ, 実行キャラ)
			;ただし同性愛系の性癖があれば対象になる
			IF 性癖素質:対象キャラ:同性愛 > 0
			ELSE
				対象外フラグ = 1
			ENDIF
		ENDIF
	ENDIF
ELSEIF RESULT == -1
	対象外フラグ = 1
ELSEIF RESULT == 1
	対象外フラグ = 0
ENDIF

RETURN 対象外フラグ

@同室者縁結び進行処理(進行量)
#DIM 進行量
#DIM 実行TARGET
#DIM 相手キャラ
#DIM 対象２
#DIM 対象配列
#DIM 関係数

;同室してるTARGET全員の組み合わせについて縁結びを進展させる

; FOR 実行TARGET, 1, CHARANUM
; 	SIF TARGET:実行TARGET < 0
; 		BREAK
; 	FOR 対象TARGET, 1, CHARANUM
; 		SIF TARGET:対象TARGET < 0
; 			BREAK
; 		SIF 実行TARGET == 対象TARGET
; 			CONTINUE
; 		VARSET RESULT
; 		IF DT_SELECT("縁結びデータベース", @"対象キャラ１ = {CFLAG:(TARGET:実行TARGET):人物番号} And 対象キャラ２ = {CFLAG:(TARGET:対象TARGET):人物番号}")
; 			LOCAL = RESULT:1
; 			;完了してるならスキップ
; 			SIF DT_CELL_GET("縁結びデータベース", LOCAL, "完遂フラグ", 1)
; 				CONTINUE
; 			;判定をチェック
; 			CALL 縁結び進展しない判定(TARGET:実行TARGET, TARGET:対象TARGET, DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別", 1))
; 			SIF RESULT:0
; 				CONTINUE

; 			TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別", 1)%_実行側_{NO:(TARGET:実行TARGET)}(TARGET:実行TARGET, TARGET:対象TARGET, 進行量)
; 			TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別", 1)%_対象側_{NO:(TARGET:対象TARGET)}(TARGET:対象TARGET, TARGET:実行TARGET, 進行量)

; 			;同じ組み合わせは複数存在しないはずなのでそのまま進展
; 			DT_CELL_SET "縁結びデータベース", LOCAL, "進展度", DT_CELL_GET("縁結びデータベース", LOCAL, "進展度", 1) + 進行量, 1
; 		ENDIF
; 	NEXT
; NEXT

;同室してるTARGET全員の組み合わせについて縁結びを進展させる

FOR 実行TARGET, 1, CHARANUM
	SIF TARGET:実行TARGET < 0
		BREAK
	;完遂してない縁結びを検索
	関係数 = DT_SELECT("縁結びデータベース", @"(対象キャラ１ = {CFLAG:(TARGET:実行TARGET):人物番号} Or 対象キャラ２ = {CFLAG:(TARGET:実行TARGET):人物番号}) And 完遂フラグ = 0")
	IF 関係数
		FOR 対象配列, 1, 関係数 + 1
			LOCAL = RESULT:対象配列
			;対象キャラを抜き出して比較
			相手キャラ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１", 1))
			IF 相手キャラ == TARGET:実行TARGET
				相手キャラ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２", 1))
			ENDIF
			IF CFLAG:相手キャラ:現在マップ種別 == CFLAG:(TARGET:実行TARGET):現在マップ種別 && CFLAG:相手キャラ:現在位置 == CFLAG:(TARGET:実行TARGET):現在位置
				;判定をチェック
				CALL 縁結び進展しない判定(TARGET:実行TARGET, 相手キャラ, DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別", 1))
				SIF RESULT:0
					CONTINUE

				TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別", 1)%_実行側_{NO:(TARGET:実行TARGET)}(TARGET:実行TARGET, 相手キャラ, 進行量)
				TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別", 1)%_対象側_{NO:相手キャラ}(相手キャラ, TARGET:実行TARGET, 進行量)

				;同じ組み合わせは複数存在しないはずなのでそのまま進展
				DT_CELL_SET "縁結びデータベース", LOCAL, "進展度", DT_CELL_GET("縁結びデータベース", LOCAL, "進展度", 1) + 進行量, 1
			ENDIF
		NEXT
	ENDIF
NEXT

@縁結び完了処理
#DIM 対象キャラ１
#DIM 対象キャラ２
#DIM 配列番号保存
#DIM 進展区切り
#DIM 同行基準
#DIMS 素質名
#DIM 消去候補ID, 500
#DIM 消去候補総数


LOCAL:10 = 0
SIF DT_ROW_LENGTH("縁結びデータベース") < 1
	RETURN 0

VARSET 消去候補ID
消去候補総数 = 0
;進展度が100以上になった縁結びを完了させる
FOR LOCAL, 0, DT_ROW_LENGTH("縁結びデータベース")

	LOCAL:10 = 0
	SIF DT_CELL_GET("縁結びデータベース", LOCAL, "完遂フラグ")
		CONTINUE

	IF DT_SELECT("関係性データベース", @"(対象キャラ１={DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１")} Or 対象キャラ１={DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２")}) And (対象キャラ２ = {DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１")} Or 対象キャラ２={DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２")}) And 関係性種別 = '%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別")%'")
		DT_CELL_SET "縁結びデータベース", LOCAL, "完遂フラグ", 1
		CONTINUE
	ENDIF

	対象キャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１"))
	対象キャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２"))
	
	;ここで同行確率を上げておく
	FOR LOCAL:1, 0, 5
		SELECTCASE LOCAL:1
			CASE 4
				進展区切り = 30
				同行基準 = 15
			CASE 3
				進展区切り = 50
				同行基準 = 25
			CASE 2
				進展区切り = 70
				同行基準 = 40
			CASE 1
				進展区切り = 90
				同行基準 = 60
			CASE 0
				進展区切り = 100
				同行基準 = 80
		ENDSELECT
		IF DT_CELL_GET("縁結びデータベース", LOCAL, "進展度") >= 進展区切り
			配列番号保存 = FINDELEMENT(同行候補キャラ番号:対象キャラ１:0, 対象キャラ２)
			IF 配列番号保存 > -1
				SIF 同行候補キャラ確率:対象キャラ１:配列番号保存 < 同行基準
					CALL ADD_RELATION(対象キャラ１, 対象キャラ２, (同行基準 - 同行候補キャラ確率:対象キャラ１:配列番号保存) * 100)
			ELSE
				CALL ADD_RELATION(対象キャラ１, 対象キャラ２, 同行基準 * 100)
			ENDIF
			配列番号保存 = FINDELEMENT(同行候補キャラ番号:対象キャラ２:0, 対象キャラ１)
			IF 配列番号保存 > -1
				SIF 同行候補キャラ確率:対象キャラ２:配列番号保存 < 同行基準
					CALL ADD_RELATION(対象キャラ２, 対象キャラ１, (同行基準 - 同行候補キャラ確率:対象キャラ２:配列番号保存) * 100)
			ELSE
				CALL ADD_RELATION(対象キャラ２, 対象キャラ１, 同行基準 * 100)
			ENDIF
			BREAK
		ENDIF
	NEXT

	;ついでにここで同性愛取得させとく
	IF DT_CELL_GET("縁結びデータベース", LOCAL, "進展度") > 50
		IF 同性愛性癖判定(対象キャラ１, 対象キャラ２)
			IF 性癖素質:対象キャラ１:同性愛 == 0
				IF TALENT:対象キャラ１:性愛傾向 == 1 ||TALENT:対象キャラ１:性愛傾向 == 2
					素質名 = 異性愛
				ELSE
					素質名 = 同性愛
				ENDIF
				性癖素質:対象キャラ１:同性愛 = 1
				PRINTFORML %CALLNAME:対象キャラ１%は%CALLNAME:対象キャラ２%と仲を深め、[%素質名%(興味)]を取得した
				CALL 履歴データベース登録(CFLAG:対象キャラ１:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]を取得した</font>","うふふ")
				LOCAL:10 ++
			ENDIF
		ENDIF
		IF 同性愛性癖判定(対象キャラ２, 対象キャラ１)
			IF 性癖素質:対象キャラ２:同性愛 == 0
				IF TALENT:対象キャラ２:性愛傾向 == 1 ||TALENT:対象キャラ２:性愛傾向 == 2
					素質名 = 異性愛
				ELSE
					素質名 = 同性愛
				ENDIF
				性癖素質:対象キャラ２:同性愛 = 1
				PRINTFORML %CALLNAME:対象キャラ２%は%CALLNAME:対象キャラ１%と仲を深め、[%素質名%(興味)]を取得した
				CALL 履歴データベース登録(CFLAG:対象キャラ２:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]を取得した</font>","うふふ")
				LOCAL:10 ++
			ENDIF
		ENDIF
	ENDIF

	SIF LOCAL:10
		DRAWLINE

	SIF DT_CELL_GET("縁結びデータベース", LOCAL, "進展度") < 100
		CONTINUE

	DT_CELL_SET "縁結びデータベース", LOCAL, "完遂フラグ", 1
	LOCAL:10 ++

	;関係性データベースに登録
	IF DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別") == "恋慕"
		SIF TALENT:対象キャラ１:恋人持ち != 2
			TALENT:対象キャラ１:恋人持ち = 1
		SIF TALENT:対象キャラ２:恋人持ち != 2
			TALENT:対象キャラ２:恋人持ち = 1

		DT_ROW_ADD "関係性データベース", "対象キャラ１", DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１"), "対象キャラ２", DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２"), "関係性種別", "恋慕", "受け攻め設定", DT_CELL_GETS("縁結びデータベース", LOCAL, "受け攻め設定")
		SETCOLOR カラーパレット("えっちな色")
		PRINTFORML %CALLNAME:対象キャラ１%と%CALLNAME:対象キャラ２%が『恋慕関係』になった！
		IF TALENT:対象キャラ１:陥落不可 == 2
			PRINTFORML %CALLNAME:対象キャラ１%は素質「陥落不可」を得ました。
		ELSEIF TALENT:対象キャラ１:陥落不可 == 0
			PRINTFORML %CALLNAME:対象キャラ１%は素質「恋慕不可」を得ました。
		ENDIF
		SETBIT TALENT:対象キャラ１:陥落不可, 0
		IF TALENT:対象キャラ２:陥落不可 == 2
			PRINTFORML %CALLNAME:対象キャラ２%は素質「陥落不可」を得ました。
		ELSEIF TALENT:対象キャラ２:陥落不可 == 0
			PRINTFORML %CALLNAME:対象キャラ２%は素質「恋慕不可」を得ました。
		ENDIF
		SETBIT TALENT:対象キャラ２:陥落不可, 0
		RESETCOLOR
		CALL 履歴データベース登録(CFLAG:対象キャラ１:人物番号, @"%CALLNAME:対象キャラ２%と恋慕関係になった","うふふ実績")
		CALL 履歴データベース登録(CFLAG:対象キャラ２:人物番号, @"%CALLNAME:対象キャラ１%と恋慕関係になった","うふふ実績")
	ELSEIF DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別") == "セフレ"
		TALENT:対象キャラ１:セフレあり = 1
		TALENT:対象キャラ２:セフレあり = 1

		DT_ROW_ADD "関係性データベース", "対象キャラ１", DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１"), "対象キャラ２", DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２"), "関係性種別", "セフレ", "受け攻め設定", DT_CELL_GETS("縁結びデータベース", LOCAL, "受け攻め設定")
		SETCOLOR カラーパレット("えっちな色")
		PRINTFORML %CALLNAME:対象キャラ１%と%CALLNAME:対象キャラ２%が『セフレ関係』になった！
		RESETCOLOR
		CALL 履歴データベース登録(CFLAG:対象キャラ１:人物番号, @"%CALLNAME:対象キャラ２%とセフレ関係になった","うふふ実績")
		CALL 履歴データベース登録(CFLAG:対象キャラ２:人物番号, @"%CALLNAME:対象キャラ１%とセフレ関係になった","うふふ実績")
	ENDIF
	DRAWLINE

	;もし同じ組み合わせ・同じ種別の縁結びがある場合消す
	IF DT_SELECT("縁結びデータベース", @"対象キャラ１={DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２")} And 対象キャラ２ = {DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１")} And 縁結び種別 = '%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別")%'")
		消去候補ID:消去候補総数 = RESULT:1
		消去候補総数 ++
	ENDIF
NEXT

IF 消去候補総数 > 0
	DT_ROW_REMOVE "縁結びデータベース", 消去候補ID, 消去候補総数
ENDIF

@縁結び進展処理()
#DIM 対象キャラ１
#DIM 対象キャラ２
#DIM 進行量
#DIMS 好み配列, 50
#DIM 候補番号
#DIM 伝播元キャラ
#DIM 伝播先キャラ
#DIM 配列番号

FOR LOCAL, 0, DT_ROW_LENGTH("縁結びデータベース")
	対象キャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ１"))
	対象キャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("縁結びデータベース", LOCAL, "対象キャラ２"))

	IF ABS(CFLAG:対象キャラ１:デート) == 対象キャラ２ + 100 && ABS(CFLAG:対象キャラ２:デート) == 対象キャラ１ + 100
		;デート状態の記録
		RSTR:縁結び報告_記録文字列 += @"%@"%CALLNAME:対象キャラ１%と%CALLNAME:対象キャラ２%は一日デートを楽しんだようです。", 80, LEFT%"
		CALL 口上変数初期化
		;非表示状態で一旦口上を読んで、ちゃんと文字が入ってるなら表示ボタンを出す
		TRYCCALLFORM KOJO_キャラ同士デート_{NO:対象キャラ１}(対象キャラ１)
			IF 口上有無確認(0,1)
				RSTR:縁結び報告_記録文字列 += @"<button value='{60000 + 対象キャラ１}'>[口上を表示]</button>"
			ELSE
				TRYCCALLFORM KOJO_キャラ同士デート_{NO:対象キャラ２}(対象キャラ２)
					IF 口上有無確認(0,1)
						RSTR:縁結び報告_記録文字列 += @"<button value='{60000 + 対象キャラ１}'>[口上を表示]</button>"
					ENDIF
				CATCH
				ENDCATCH
			ENDIF
		CATCH
			TRYCCALLFORM KOJO_キャラ同士デート_{NO:対象キャラ２}(対象キャラ２)
				IF 口上有無確認(0,1)
					RSTR:縁結び報告_記録文字列 += @"<button value='{60000 + 対象キャラ１}'>[口上を表示]</button>"
				ENDIF
			CATCH
			ENDCATCH
		ENDCATCH
		RSTR:縁結び報告_記録文字列 += "<br>"

		;ランダムでどちらかの嗜好が伝播する、とりあえず20%
		IF RAND:5 == 0 && (CSTR:対象キャラ１:プレゼント好み_恋慕影響 == "" || CSTR:対象キャラ２:プレゼント好み_恋慕影響 == "")
			;基本的に誘われた方が影響されやすい
			IF CSTR:対象キャラ２:プレゼント好み_恋慕影響 == ""
				伝播元キャラ = 対象キャラ１
				伝播先キャラ = 対象キャラ２
			ELSE
				伝播元キャラ = 対象キャラ２
				伝播先キャラ = 対象キャラ１
			ENDIF

			;伝播元の好みを配列に分解
			VARSET RESULTS
			VARSET 好み配列
			候補番号 = 0
			SPLIT CSTR:伝播元キャラ:プレゼント好み, "_", RESULTS
			FOR 配列番号, 0, RESULT
				IF STRFIND(CSTR:伝播先キャラ:プレゼント好み, RESULTS:配列番号) < 0
					;好みにないなら候補に登録
					好み配列:候補番号 '= RESULTS:配列番号
					候補番号++
				ENDIF
			NEXT
			IF 候補番号 > 0
				CSTR:伝播先キャラ:プレゼント好み_恋慕影響 '= 好み配列:(RAND:候補番号)
			ENDIF
		ENDIF
	ENDIF

	;成立済みはスキップ
	SIF DT_CELL_GET("縁結びデータベース", LOCAL, "完遂フラグ")
		CONTINUE

	;対象キャラ２の判定をチェック
	CALL 縁結び進展しない判定(対象キャラ１, 対象キャラ２, DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別"))
	SIF RESULT
		CONTINUE

	IF CFLAG:対象キャラ１:滞在期間 > 0 && CFLAG:対象キャラ２:滞在期間
		;ふたりともリゾートにいる時、微妙に進展する
		進行量 = RAND:3 + 1

		TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別")%_実行側_{NO:対象キャラ１}(対象キャラ１, 対象キャラ２, 進行量)
		TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別")%_対象側_{NO:対象キャラ２}(対象キャラ２, 対象キャラ１, 進行量)

		DT_CELL_SET "縁結びデータベース", LOCAL, "進展度", MIN(100, DT_CELL_GET("縁結びデータベース", LOCAL, "進展度") + 進行量)
	ENDIF

	IF ABS(CFLAG:対象キャラ１:デート) == 対象キャラ２ + 100 && ABS(CFLAG:対象キャラ２:デート) == 対象キャラ１ + 100
		;１日終了時にデート状態なら更に進展する
		進行量 = RAND:5 + 2

		TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別")%_実行側_{NO:対象キャラ１}(対象キャラ１, 対象キャラ２, 進行量)
		TRYCALLFORM 縁結び進行固有補正_%DT_CELL_GETS("縁結びデータベース", LOCAL, "縁結び種別")%_対象側_{NO:対象キャラ２}(対象キャラ２, 対象キャラ１, 進行量)

		DT_CELL_SET "縁結びデータベース", LOCAL, "進展度", MIN(100, DT_CELL_GET("縁結びデータベース", LOCAL, "進展度") + 進行量)
	ENDIF
NEXT

@ランダム縁結び処理()
#DIM 設定数

DRAWLINE
PRINTL キャラの縁結びをランダムに結びます。
PRINTL 設定方法を選んでください。
DRAWLINE

PRINTBUTTON "[0] リゾートにいるキャラ全てに恋慕の縁をランダムに結ぶ", 0
PRINTL
PRINTBUTTON "[1] リゾートにいるキャラ全てにセフレの縁をランダムに結ぶ", 1
PRINTL
PRINTBUTTON "[999] 戻る", 999
PRINTL
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0
		DRAWLINE
		PRINTL リゾートにいるキャラ全てに恋慕の縁をランダムに結びます。
		PRINTL ※縁を結ぶ候補がいない場合はスキップされます。
		PRINTL ※全キャラが他のランダムなキャラに向けた縁が結ばれます。１対１で対応しているわけではありません。
		PRINTL ※滞在人数によっては無限ループ警告が出る場合がありますが、そのまま処理を継続してください。
		PRINTL
		PRINTL よろしいですか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			設定数 = 0
			;左キャラセット
			FOR LOCAL, 1, CHARANUM
				SIF CFLAG:LOCAL:滞在期間 < 0
					CONTINUE
				RESULT = 0
				TRYCALLFORM 縁結び特殊条件_左キャラ時_{NO:LOCAL}(LOCAL)
				IF RESULT == 0
					SIF TALENT:LOCAL:性別 == 0
						CONTINUE
					SIF TALENT:LOCAL:性欲 == -2
						CONTINUE
					SIF 知識素質:LOCAL:性知識 < 0
						CONTINUE
					SIF GETBIT(CSVTALENT(NO:LOCAL, GETNUM(TALENT, "陥落不可")), 0) && GETBIT(TALENT:LOCAL:陥落不可, 1)
						CONTINUE
				ELSEIF RESULT == -1
					CONTINUE
				ENDIF
				;ランダムで右キャラセット
				CALL 縁結び右キャラセット(LOCAL, 1)
				SIF RESULT
					設定数 ++
			NEXT
			PRINTFORMW {設定数}名のキャラに恋慕の縁を結びました。
		ENDIF
	CASE 1
		DRAWLINE
		PRINTL リゾートにいるキャラ全てにセフレの縁をランダムに結びます。
		PRINTL ※縁を結ぶ候補がいない場合はスキップされます。
		PRINTL ※全キャラが他のランダムなキャラに向けた縁が結ばれます。１対１で対応しているわけではありません。
		PRINTL ※滞在人数によっては無限ループ警告が出る場合がありますが、そのまま処理を継続してください。
		PRINTL
		PRINTL よろしいですか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			設定数 = 0
			;左キャラセット
			FOR LOCAL, 1, CHARANUM
				SIF CFLAG:LOCAL:滞在期間 < 0
					CONTINUE
				RESULT = 0
				TRYCALLFORM 縁結び特殊条件_左キャラ時_{NO:LOCAL}(LOCAL)
				IF RESULT == 0
					SIF TALENT:LOCAL:性別 == 0
						CONTINUE
					SIF TALENT:LOCAL:性欲 == -2
						CONTINUE
					SIF 知識素質:LOCAL:性知識 < 0
						CONTINUE
					SIF GETBIT(CSVTALENT(NO:LOCAL, GETNUM(TALENT, "陥落不可")), 0) && GETBIT(TALENT:LOCAL:陥落不可, 1)
						CONTINUE
				ELSEIF RESULT == -1
					CONTINUE
				ENDIF
				;ランダムで右キャラセット
				CALL 縁結び右キャラセット(LOCAL, 2)
				SIF RESULT
					設定数 ++
			NEXT
			PRINTFORMW {設定数}名のキャラにセフレの縁を結びました。
		ENDIF
	CASE 999
		RETURN 0
ENDSELECT

RESTART


@オート縁結び処理(順番シャッフル用配列)
#DIM 設定カップル数
#DIM 許可フラグ
#DIM 対象キャラ１
#DIM 候補配列, キャラクタ数上限
#DIM 候補総数
#DIM 縁結び組数
#DIM ループ用
#DIM 候補カウント
#DIM REF 順番シャッフル用配列

;オプションで禁止なら戻す
SIF OPTION変数:オート縁結び == 0
	RETURN 0
SIF GETBIT(OPTION変数:オート縁結び詳細, 0) == 0 && GETBIT(OPTION変数:オート縁結び詳細, 1) == 0
	RETURN 0

;設定カップル数を設定
設定カップル数 = RAND:4
;設定が０なら戻す
SIF 設定カップル数 <= 0
	RETURN 0

候補カウント = 0
VARSET 候補配列
;滞在キャラを全員見て候補配列に登録
FOR ループ用, 0, CHARANUM - 1
	SIF 候補カウント >= 設定カップル数
		BREAK
	対象キャラ１ = 順番シャッフル用配列:ループ用
	SIF CFLAG:対象キャラ１:滞在期間 <= 0
		CONTINUE
	;特殊条件チェック
	TRYCALLFORM 縁結び特殊条件_左キャラ時_{NO:対象キャラ１}(対象キャラ１)
	IF RESULT == 0
		;性欲なし・性別なしはダメ
		SIF TALENT:対象キャラ１:性欲 == -2
			CONTINUE
		SIF TALENT:対象キャラ１:性別 == 0
			CONTINUE
		;無知もダメ
		SIF 知識素質:対象キャラ１:性知識 < 0
			CONTINUE
		;陥落不可素質
		SIF GETBIT(CSVTALENT(NO:対象キャラ１, GETNUM(TALENT, "陥落不可")), 0) && GETBIT(TALENT:対象キャラ１:陥落不可, 1)
			CONTINUE
	ELSEIF RESULT == -1
		CONTINUE
	ENDIF
	;ちゃんと相手候補がいるかチェック
	IF GETBIT(OPTION変数:オート縁結び詳細, 0)
		;恋慕
		;既に恋慕セットするorされてたらアウト
		
		IF 縁結びセット数取得(対象キャラ１, "恋慕")
			CONTINUE
		ENDIF
		;相手が存在するか
		CALL 縁結び候補存在判定(対象キャラ１, "恋慕")
		IF RESULT
			;相手候補がいるなら配列にセットして次へ
			候補配列:候補カウント = 対象キャラ１
			候補カウント ++
			CONTINUE
		ENDIF
	ENDIF
	IF GETBIT(OPTION変数:オート縁結び詳細, 1)
		;セフレ
		;オートで結べるセフレは３組まで
		IF 縁結びセット数取得(対象キャラ１, "セフレ") >= 3
			CONTINUE
		ENDIF
		;相手が存在するか
		CALL 縁結び候補存在判定(対象キャラ１, "セフレ")
		IF RESULT
			;相手候補がいるなら配列にセットして次へ
			候補配列:候補カウント = 対象キャラ１
			候補カウント ++
			CONTINUE
		ENDIF
	ENDIF
NEXT

縁結び組数 = 0
;候補から選出
FOR LOCAL, 0, 候補カウント
	対象キャラ１ = 候補配列:LOCAL
	RESULT = 0
	IF GETBIT(OPTION変数:オート縁結び詳細, 0) && GETBIT(OPTION変数:オート縁結び詳細, 1)
		;恋慕が入る確率の方がやや高い
		IF RAND:3 && GETBIT(OPTION変数:オート縁結び詳細, 0)
			CALL 縁結び右キャラセット(対象キャラ１, 1)
			SIF RESULT == 0 && GETBIT(OPTION変数:オート縁結び詳細, 1)
				CALL 縁結び右キャラセット(対象キャラ１, 2)
		ELSEIF GETBIT(OPTION変数:オート縁結び詳細, 1)
			CALL 縁結び右キャラセット(対象キャラ１, 2)
			SIF RESULT == 0 && GETBIT(OPTION変数:オート縁結び詳細, 0)
				CALL 縁結び右キャラセット(対象キャラ１, 1)
		ENDIF
	ELSEIF GETBIT(OPTION変数:オート縁結び詳細, 0)
		CALL 縁結び右キャラセット(対象キャラ１, 1)
	ELSEIF GETBIT(OPTION変数:オート縁結び詳細, 1)
		CALL 縁結び右キャラセット(対象キャラ１, 2)
	ENDIF
	SIF RESULT
		縁結び組数 ++
NEXT
RETURN 縁結び組数

